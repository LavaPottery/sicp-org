* 4 Metalinguistic Abstraction
:properties:
:custom_id: section-4
:end:
** Introduction
:properties:
:VISIBILITY: notoc
:end:

#+begin_quote
... It's in words that the magic is--Abracadabra, Open Sesame, and the rest--but the magic words in one story aren't magical in the next.  The real magic is to understand which words work, and when, and for what; the trick is to learn the trick.

... And those words are made from the letters of our alphabet: a couple-dozen squiggles we can draw with the pen.  This is the key!  And the treasure, too, if we can only get our hands on it!  It's as if--as if the key to the treasure /is/ the treasure!

--John Barth, 'Chimera'
#+end_quote

In our study of program design, we have seen that expert programmers control the complexity of their designs with the same general techniques used by designers of all complex systems.  They combine primitive elements to form compound objects, they abstract compound objects to form higher-level building blocks, and they preserve modularity by adopting appropriate large-scale views of system structure.  In illustrating these techniques, we have used Lisp as a language for describing processes and for constructing computational data objects and processes to model complex phenomena in the real world.  However, as we confront increasingly complex problems, we will find that Lisp, or indeed any fixed programming language, is not sufficient for our needs.  We must constantly turn to new languages in order to express our ideas more effectively.  Establishing new languages is a powerful strategy for controlling complexity in engineering design; we can often enhance our ability to deal with a complex problem by adopting a new language that enables us to describe (and hence to think about) the problem in a different way, using primitives, means of combination, and means of abstraction that are particularly well suited to the problem at hand.[fn:205]

Programming is endowed with a multitude of languages.  There are physical languages, such as the machine languages for particular computers.  These languages are concerned with the representation of data and control in terms of individual bits of storage and primitive machine instructions.  The machine-language programmer is concerned with using the given hardware to erect systems and utilities for the efficient implementation of resource-limited computations.  High-level languages, erected on a machine-language substrate, hide concerns about the representation of data as collections of bits and the representation of programs as sequences of primitive instructions.  These languages have means of combination and abstraction, such as procedure definition, that are appropriate to the larger-scale organization of systems.

<<i237>> Metalinguistic abstraction--establishing new languages--plays an important role in all branches of engineering design.  It is particularly important to computer programming, because in programming not only can we formulate new languages but we can also implement these languages by constructing evaluators.  An <<i129>> evaluator (or <<i195>> interpreter) for a programming language is a procedure that, when applied to an expression of the language, performs the actions required to evaluate that expression.

It is no exaggeration to regard this as the most fundamental idea in programming:

#+begin_quote
The evaluator, which determines the meaning of expressions in a programming language, is just another program.
#+end_quote

To appreciate this point is to change our images of ourselves as programmers.  We come to see ourselves as designers of languages, rather than only users of languages designed by others.

In fact, we can regard almost any program as the evaluator for some language.  For instance, the polynomial manipulation system of section [[#section-2.5.3][2.5.3]] embodies the rules of polynomial arithmetic and implements them in terms of operations on list-structured data.  If we augment this system with procedures to read and print polynomial expressions, we have the core of a special-purpose language for dealing with problems in symbolic mathematics.  The digital-logic simulator of section [[#section-3.3.4][3.3.4]] and the constraint propagator of section [[#section-3.3.5][3.3.5]] are legitimate languages in their own right, each with its own primitives, means of combination, and means of abstraction.  Seen from this perspective, the technology for coping with large-scale computer systems merges with the technology for building new computer languages, and computer science itself becomes no more (and no less) than the discipline of constructing appropriate descriptive languages.

We now embark on a tour of the technology by which languages are established in terms of other languages.  In this chapter we shall use Lisp as a base, implementing evaluators as Lisp procedures.  Lisp is particularly well suited to this task, because of its ability to represent and manipulate symbolic expressions.  We will take the first step in understanding how languages are implemented by building an evaluator for Lisp itself.  The language implemented by our evaluator will be a subset of the Scheme dialect of Lisp that we use in this book.  Although the evaluator described in this chapter is written for a particular dialect of Lisp, it contains the essential structure of an evaluator for any expression-oriented language designed for writing programs for a sequential machine.  (In fact, most language processors contain, deep within them, a little "Lisp" evaluator.)  The evaluator has been simplified for the purposes of illustration and discussion, and some features have been left out that would be important to include in a production-quality Lisp system.  Nevertheless, this simple evaluator is adequate to execute most of the programs in this book.[fn:206]

An important advantage of making the evaluator accessible as a Lisp program is that we can implement alternative evaluation rules by describing these as modifications to the evaluator program.  One place where we can use this power to good effect is to gain extra control over the ways in which computational models embody the notion of time, which was so central to the discussion in [[#section-3][Chapter 3]].  There, we mitigated some of the complexities of state and assignment by using streams to decouple the representation of time in the world from time in the computer.  Our stream programs, however, were sometimes cumbersome, because they were constrained by the applicative-order evaluation of Scheme.  In section [[#section-4.2][4.2]], we'll change the underlying language to provide for a more elegant approach, by modifying the evaluator to provide for <<i264>> normal-order evaluation.

Section [[#section-4.3][4.3]] implements a more ambitious linguistic change, whereby expressions have many values, rather than just a single value.  In this language of <<i260>> nondeterministic computing, it is natural to express processes that generate all possible values for expressions and then search for those values that satisfy certain constraints.  In terms of models of computation and time, this is like having time branch into a set of "possible futures" and then searching for appropriate time lines.  With our nondeterministic evaluator, keeping track of multiple values and performing searches are handled automatically by the underlying mechanism of the language.

In section [[#section-4.4][4.4]] we implement a <<i219>> logic-programming language in which knowledge is expressed in terms of relations, rather than in terms of computations with inputs and outputs.  Even though this makes the language drastically different from Lisp, or indeed from any conventional language, we will see that the logic-programming evaluator shares the essential structure of the Lisp evaluator.

** 4.1 The Metacircular Evaluator
:properties:
:custom_id: section-4.1
:end:

Our evaluator for Lisp will be implemented as a Lisp program.  It may seem circular to think about evaluating Lisp programs using an evaluator that is itself implemented in Lisp.  However, evaluation is a process, so it is appropriate to describe the evaluation process using Lisp, which, after all, is our tool for describing processes.[fn:207] An evaluator that is written in the same language that it evaluates is said to be <<i236>> metacircular.

The metacircular evaluator is essentially a Scheme formulation of the environment model of evaluation described in section [[#section-3.2][3.2]].  Recall that the model has two basic parts:

1. To evaluate a combination (a compound expression other than a special form), evaluate the subexpressions and then apply the value of the operator subexpression to the values of the operand subexpressions.

2. To apply a compound procedure to a set of arguments, evaluate the body of the procedure in a new environment.  To construct this environment, extend the environment part of the procedure object by a frame in which the formal parameters of the procedure are bound to the arguments to which the procedure is applied.

These two rules describe the essence of the evaluation process, a basic cycle in which expressions to be evaluated in environments are reduced to procedures to be applied to arguments, which in turn are reduced to new expressions to be evaluated in new environments, and so on, until we get down to symbols, whose values are looked up in the environment, and to primitive procedures, which are applied directly (see [[figure-4.1][Figure 4.1]]).[fn:208] This evaluation cycle will be embodied by the interplay between the two critical procedures in the evaluator, ~eval~ and ~apply~, which are described in section [[#section-4.1.1][4.1.1]] (see [[figure-4.1][Figure 4.1]]).

The implementation of the evaluator will depend upon procedures that define the <<i383>> syntax of the expressions to be evaluated.  We will use data abstraction to make the evaluator independent of the representation of the language.  For example, rather than committing to a choice that an assignment is to be represented by a list beginning with the symbol ~set!~ we use an abstract predicate ~assignment?~ to test for an assignment, and we use abstract selectors ~assignment-variable~ and ~assignment-value~ to access the parts of an assignment.  Implementation of expressions will be described in detail in section [[#section-4.1.2][4.1.2]].  There are also operations, described in section [[#section-4.1.3][4.1.3]], that specify the representation of procedures and environments.  For example, ~make-procedure~ constructs compound procedures, ~lookup-variable-value~ accesses the values of variables, and ~apply-primitive-procedure~ applies a primitive procedure to a given list of arguments.

*** 4.1.1 The Core of the Evaluator
:properties:
:custom_id: section-4.1.1
:end:

<<figure-4.1>> The ~eval~-~apply~ cycle exposes the essence of a computer language.

#+begin_example
                            .,ad88888888baa,
                   _    ,d8P"""        ""9888ba.      _
                  /  .a8"          ,ad88888888888a   |\
                /   aP'          ,88888888888888888a   \
               /  ,8"           ,88888888888888888888,  \
              |  ,8'            (888888888888888888888, |
             /  ,8'             '8888888888888888888888  \
             |  8)               '888888888888888888888, |
 Procedure,  |  8                  "88888 Apply 8888888) | Expression
 Arguments   |  8     Eval          '888888888888888888) | Environment
             |  8)                    "8888888888888888  |
             \  (b                     "88888888888888'  /
              | '8,                     8888888888888)  |
              \  "8a                   ,888888888888)  /
               \   V8,                 d88888888888"  /
               _\| '8b,             ,d8888888888P' _/
                      'V8a,       ,ad8888888888P'
                         ""88888888888888888P"
                              """"""""""""

                                [graphic by Normand Veillux, modified]
#+end_example

The evaluation process can be described as the interplay between two procedures: ~eval~ and ~apply~.

*Eval*

~eval~ takes as arguments an expression and an environment.  It classifies the expression and directs its evaluation.  ~eval~ is structured as a case analysis of the syntactic type of the expression to be evaluated.  In order to keep the procedure general, we express the determination of the type of an expression abstractly, making no commitment to any particular representation for the various types of expressions.  Each type of expression has a predicate that tests for it and an abstract means for selecting its parts.  This <<i2>> abstract syntax makes it easy to see how we can change the syntax of the language by using the same evaluator, but with a different collection of syntax procedures.

Primitive expressions

- For self-evaluating expressions, such as numbers, ~eval~ returns the expression itself.

- ~Eval~ must look up variables in the environment to find their values.

Special forms

- For quoted expressions, ~eval~ returns the expression that was quoted.

- An assignment to (or a definition of) a variable must recursively call ~eval~ to compute the new value to be associated with the variable.  The environment must be modified to change (or create) the binding of the variable.

- An ~if~ expression requires special processing of its parts, so as to evaluate the consequent if the predicate is true, and otherwise to evaluate the alternative.

- A ~lambda~ expression must be transformed into an applicable procedure by packaging together the parameters and body specified by the ~lambda~ expression with the environment of the evaluation.

- A ~begin~ expression requires evaluating its sequence of expressions in the order in which they appear.

- A case analysis (~cond~) is transformed into a nest of ~if~ expressions and then evaluated.

Combinations

- For a procedure application, ~eval~ must recursively evaluate the operator part and the operands of the combination.  The resulting procedure and arguments are passed to ~apply~, which handles the actual procedure application.

Here is the definition of ~eval~:

#+begin_src scheme
(define (eval exp env)
  (cond ((self-evaluating? exp) exp)
        ((variable? exp) (lookup-variable-value exp env))
        ((quoted? exp) (text-of-quotation exp))
        ((assignment? exp) (eval-assignment exp env))
        ((definition? exp) (eval-definition exp env))
        ((if? exp) (eval-if exp env))
        ((lambda? exp)
         (make-procedure (lambda-parameters exp)
                         (lambda-body exp)
                         env))
        ((begin? exp)
         (eval-sequence (begin-actions exp) env))
        ((cond? exp) (eval (cond->if exp) env))
        ((application? exp)
         (apply (eval (operator exp) env)
                (list-of-values (operands exp) env)))
        (else
         (error "Unknown expression type - EVAL" exp))))
#+end_src

For clarity, ~eval~ has been implemented as a case analysis using ~cond~.  The disadvantage of this is that our procedure handles only a few distinguishable types of expressions, and no new ones can be defined without editing the definition of ~eval~.  In most Lisp implementations, dispatching on the type of an expression is done in a data-directed style.  This allows a user to add new types of expressions that ~eval~ can distinguish, without modifying the definition of ~eval~ itself.  (See [[#exercise-4.3][Exercise 4.3]].)

*Apply*

~apply~ takes two arguments, a procedure and a list of arguments to which the procedure should be applied.  ~apply~ classifies procedures into two kinds: It calls ~apply-primitive-procedure~ to apply primitives; it applies compound procedures by sequentially evaluating the expressions that make up the body of the procedure.  The environment for the evaluation of the body of a compound procedure is constructed by extending the base environment carried by the procedure to include a frame that binds the parameters of the procedure to the arguments to which the procedure is to be applied.  Here is the definition of ~apply~:

#+begin_src scheme
(define (apply procedure arguments)
  (cond ((primitive-procedure? procedure)
         (apply-primitive-procedure procedure arguments))
        ((compound-procedure? procedure)
         (eval-sequence
          (procedure-body procedure)
          (extend-environment
           (procedure-parameters procedure)
           arguments
           (procedure-environment procedure))))
        (else
         (error
          "Unknown procedure type - APPLY" procedure))))
#+end_src

*Procedure arguments*

When ~eval~ processes a procedure application, it uses ~list-of-values~ to produce the list of arguments to which the procedure is to be applied.  ~list-of-values~ takes as an argument the operands of the combination.  It evaluates each operand and returns a list of the corresponding values:[fn:209]

#+begin_src scheme
(define (list-of-values exps env)
  (if (no-operands? exps)
      '()
      (cons (eval (first-operand exps) env)
            (list-of-values (rest-operands exps) env))))
#+end_src

*Conditionals*

~eval-if~ evaluates the predicate part of an ~if~ expression in the given environment.  If the result is true, ~eval-if~ evaluates the consequent, otherwise it evaluates the alternative:

#+begin_src scheme
(define (eval-if exp env)
  (if (true? (eval (if-predicate exp) env))
      (eval (if-consequent exp) env)
      (eval (if-alternative exp) env)))
#+end_src

The use of ~true?~ in ~eval-if~ highlights the issue of the connection between an implemented language and an implementation language.  The ~if-predicate~ is evaluated in the language being implemented and thus yields a value in that language.  The interpreter predicate ~true?~ translates that value into a value that can be tested by the ~if~ in the implementation language: The metacircular representation of truth might not be the same as that of the underlying Scheme.[fn:210]

*Sequences*

~eval-sequence~ is used by ~apply~ to evaluate the sequence of expressions in a procedure body and by ~eval~ to evaluate the sequence of expressions in a ~begin~ expression.  It takes as arguments a sequence of expressions and an environment, and evaluates the expressions in the order in which they occur.  The value returned is the value of the final expression.

#+begin_src scheme
(define (eval-sequence exps env)
  (cond ((last-exp? exps) (eval (first-exp exps) env))
        (else (eval (first-exp exps) env)
              (eval-sequence (rest-exps exps) env))))
#+end_src

*Assignments and definitions*

The following procedure handles assignments to variables.  It calls ~eval~ to find the value to be assigned and transmits the variable and the resulting value to ~set-variable-value!~ to be installed in the designated environment.

#+begin_src scheme
(define (eval-assignment exp env)
  (set-variable-value! (assignment-variable exp)
                       (eval (assignment-value exp) env)
                       env)
  'ok)
#+end_src

Definitions of variables are handled in a similar manner.[fn:211]

#+begin_src scheme
(define (eval-definition exp env)
  (define-variable! (definition-variable exp)
    (eval (definition-value exp) env)
    env)
  'ok)
#+end_src

We have chosen here to return the symbol ~ok~ as the value of an assignment or a definition.[fn:212]

**** Exercise 4.1
:properties:
:custom_id: exercise-4.1
:end:

Notice that we cannot tell whether the metacircular evaluator evaluates operands from left to right or from right to left.  Its evaluation order is inherited from the underlying Lisp: If the arguments to ~cons~ in ~list-of-values~ are evaluated from left to right, then ~list-of-values~ will evaluate operands from left to right; and if the arguments to ~cons~ are evaluated from right to left, then ~list-of-values~ will evaluate operands from right to left.

Write a version of ~list-of-values~ that evaluates operands from left to right regardless of the order of evaluation in the underlying Lisp.  Also write a version of ~list-of-values~ that evaluates operands from right to left.

*** 4.1.2 Representing Expressions
:properties:
:custom_id: section-4.1.2
:end:

The evaluator is reminiscent of the symbolic differentiation program discussed in section [[#section-2.3.2][2.3.2]].  Both programs operate on symbolic expressions.  In both programs, the result of operating on a compound expression is determined by operating recursively on the pieces of the expression and combining the results in a way that depends on the type of the expression.  In both programs we used data abstraction to decouple the general rules of operation from the details of how expressions are represented.  In the differentiation program this meant that the same differentiation procedure could deal with algebraic expressions in prefix form, in infix form, or in some other form.  For the evaluator, this means that the syntax of the language being evaluated is determined solely by the procedures that classify and extract pieces of expressions.

Here is the specification of the syntax of our language:

1. The only self-evaluating items are numbers and strings:

#+begin_src scheme
(define (self-evaluating? exp)
  (cond ((number? exp) true)
        ((string? exp) true)
        (else false)))
#+end_src

2. Variables are represented by symbols:

#+begin_src scheme
(define (variable? exp) (symbol? exp))
#+end_src

3. Quotations have the form '(quote <TEXT-OF-QUOTATION>)':[fn:213]

#+begin_src scheme
(define (quoted? exp)
  (tagged-list? exp 'quote))

(define (text-of-quotation exp) (cadr exp))
#+end_src

~quoted?~ is defined in terms of the procedure ~tagged-list?~, which identifies lists beginning with a designated symbol:

#+begin_src scheme
(define (tagged-list? exp tag)
  (if (pair? exp)
      (eq? (car exp) tag)
      false))
#+end_src

4. Assignments have the form '(set! <VAR> <VALUE>)':

#+begin_src scheme
(define (assignment? exp)
  (tagged-list? exp 'set!))

(define (assignment-variable exp) (cadr exp))

(define (assignment-value exp) (caddr exp))
#+end_src

5. Definitions have the form

#+begin_src scheme
(define <VAR> <VALUE>)
#+end_src

or the form

#+begin_src scheme
(define (<VAR> <PARAMETER_1> ... <PARAMETER_N>)
  <BODY>)
#+end_src

The latter form (standard procedure definition) is syntactic sugar for

#+begin_src scheme
(define <VAR>
  (lambda (<PARAMETER_1> ... <PARAMETER_N>)
    <BODY>))
#+end_src

The corresponding syntax procedures are the following:

#+begin_src scheme
(define (definition? exp)
  (tagged-list? exp 'define))

(define (definition-variable exp)
  (if (symbol? (cadr exp))
      (cadr exp)
      (caadr exp)))

(define (definition-value exp)
  (if (symbol? (cadr exp))
      (caddr exp)
      (make-lambda (cdadr exp)          ; formal parameters
                   (cddr exp)))) ; body
#+end_src

6. ~lambda~ expressions are lists that begin with the symbol ~lambda~:

#+begin_src scheme
(define (lambda? exp) (tagged-list? exp 'lambda))

(define (lambda-parameters exp) (cadr exp))

(define (lambda-body exp) (cddr exp))
#+end_src

We also provide a constructor for ~lambda~ expressions, which is used by ~definition-value~, above:

#+begin_src scheme
(define (make-lambda parameters body)
  (cons 'lambda (cons parameters body)))
#+end_src

7. Conditionals begin with ~if~ and have a predicate, a consequent, and an (optional) alternative.  If the expression has no alternative part, we provide ~false~ as the alternative.[fn:214]

#+begin_src scheme
(define (if? exp) (tagged-list? exp 'if))

(define (if-predicate exp) (cadr exp))

(define (if-consequent exp) (caddr exp))

(define (if-alternative exp)
  (if (not (null? (cdddr exp)))
      (cadddr exp)
      'false))
#+end_src

We also provide a constructor for ~if~ expressions, to be used by ~cond->if~ to transform ~cond~ expressions into ~if~ expressions:

#+begin_src scheme
(define (make-if predicate consequent alternative)
  (list 'if predicate consequent alternative))
#+end_src

8. ~begin~ packages a sequence of expressions into a single expression. We include syntax operations on ~begin~ expressions to extract the actual sequence from the ~begin~ expression, as well as selectors that return the first expression and the rest of the expressions in the sequence.[fn:215]

#+begin_src scheme
(define (begin? exp) (tagged-list? exp 'begin))

(define (begin-actions exp) (cdr exp))

(define (last-exp? seq) (null? (cdr seq)))

(define (first-exp seq) (car seq))

(define (rest-exps seq) (cdr seq))
#+end_src

We also include a constructor ~sequence->exp~ (for use by ~cond->if~) that transforms a sequence into a single expression, using ~begin~ if necessary:

#+begin_src scheme
(define (sequence->exp seq)
  (cond ((null? seq) seq)
        ((last-exp? seq) (first-exp seq))
        (else (make-begin seq))))

(define (make-begin seq) (cons 'begin seq))
#+end_src

9. A procedure application is any compound expression that is not one of the above expression types.  The ~car~ of the expression is the operator, and the ~cdr~ is the list of operands:

#+begin_src scheme
(define (application? exp) (pair? exp))

(define (operator exp) (car exp))

(define (operands exp) (cdr exp))

(define (no-operands? ops) (null? ops))

(define (first-operand ops) (car ops))

(define (rest-operands ops) (cdr ops))
#+end_src

*Derived expressions*

Some special forms in our language can be defined in terms of expressions involving other special forms, rather than being implemented directly.  One example is ~cond~, which can be implemented as a nest of ~if~ expressions.  For example, we can reduce the problem of evaluating the expression

#+begin_src scheme
(cond ((> x 0) x)
      ((= x 0) (display 'zero) 0)
      (else (- x)))
#+end_src

to the problem of evaluating the following expression involving ~if~ and ~begin~ expressions:

#+begin_src scheme
(if (> x 0)
    x
    (if (= x 0)
        (begin (display 'zero)
               0)
        (- x)))
#+end_src

Implementing the evaluation of ~cond~ in this way simplifies the evaluator because it reduces the number of special forms for which the evaluation process must be explicitly specified.

We include syntax procedures that extract the parts of a ~cond~ expression, and a procedure ~cond->if~ that transforms ~cond~ expressions into ~if~ expressions.  A case analysis begins with ~cond~ and has a list of predicate-action clauses.  A clause is an ~else~ clause if its predicate is the symbol ~else~.[fn:216]

#+begin_src scheme
(define (cond? exp) (tagged-list? exp 'cond))

(define (cond-clauses exp) (cdr exp))

(define (cond-else-clause? clause)
  (eq? (cond-predicate clause) 'else))

(define (cond-predicate clause) (car clause))

(define (cond-actions clause) (cdr clause))

(define (cond->if exp)
  (expand-clauses (cond-clauses exp)))

(define (expand-clauses clauses)
  (if (null? clauses)
      'false                            ; no ~else~ clause
      (let ((first (car clauses))
            (rest (cdr clauses)))
        (if (cond-else-clause? first)
            (if (null? rest)
                (sequence->exp (cond-actions first))
                (error "ELSE clause isn't last - COND->IF"
                       clauses))
            (make-if (cond-predicate first)
                     (sequence->exp (cond-actions first))
                     (expand-clauses rest))))))
#+end_src

Expressions (such as ~cond~) that we choose to implement as syntactic transformations are called <<i112>> derived expressions.  ~let~ expressions are also derived expressions (see [[#exercise-4.6][Exercise 4.6]]).[fn:217]

**** Exercise 4.2
:properties:
:custom_id: exercise-4.2
:end:

Louis Reasoner plans to reorder the ~cond~ clauses in ~eval~ so that the clause for procedure applications appears before the clause for assignments.  He argues that this will make the interpreter more efficient: Since programs usually contain more applications than assignments, definitions, and so on, his modified ~eval~ will usually check fewer clauses than the original ~eval~ before identifying the type of an expression.

a. What is wrong with Louis's plan?  (Hint: What will Louis's evaluator do with the expression ~(define x 3)~?)

b. Louis is upset that his plan didn't work.  He is willing to go to any lengths to make his evaluator recognize procedure applications before it checks for most other kinds of expressions.  Help him by changing the syntax of the evaluated language so that procedure applications start with ~call~.  For example, instead of ~(factorial 3)~ we will now have to write ~(call factorial 3)~ and instead of ~(+ 1 2)~ we will have to write ~(call
   + 1 2)~.

**** Exercise 4.3
:properties:
:custom_id: exercise-4.3
:end:

Rewrite ~eval~ so that the dispatch is done in data-directed style.  Compare this with the data-directed differentiation procedure of [[#exercise-2.73][Exercise 2.73]].  (You may use the ~car~ of a compound expression as the type of the expression, as is appropriate for the syntax implemented in this section.)

**** Exercise 4.4
:properties:
:custom_id: exercise-4.4
:end:

Recall the definitions of the special forms ~and~ and ~or~ from [[#section-1][Chapter 1]]:

- ~and~: The expressions are evaluated from left to right.  If any expression evaluates to false, false is returned; any remaining expressions are not evaluated.  If all the expressions evaluate to true values, the value of the last expression is returned.  If there are no expressions then true is returned.

- ~or~: The expressions are evaluated from left to right.  If any expression evaluates to a true value, that value is returned; any remaining expressions are not evaluated.  If all expressions evaluate to false, or if there are no expressions, then false is returned.

Install ~and~ and ~or~ as new special forms for the evaluator by defining appropriate syntax procedures and evaluation procedures ~eval-and~ and ~eval-or~.  Alternatively, show how to implement ~and~ and ~or~ as derived expressions.

**** Exercise 4.5
:properties:
:custom_id: exercise-4.5
:end:

Scheme allows an additional syntax for ~cond~ clauses, '(<TEST> => <RECIPIENT>)'.  If <TEST> evaluates to a true value, then <RECIPIENT> is evaluated.  Its value must be a procedure of one argument; this procedure is then invoked on the value of the <TEST>, and the result is returned as the value of the ~cond~ expression.  For example

#+begin_src scheme
(cond ((assoc 'b '((a 1) (b 2))) => cadr)
      (else false))
#+end_src

returns 2.  Modify the handling of ~cond~ so that it supports this extended syntax.

**** Exercise 4.6
:properties:
:custom_id: exercise-4.6
:end:

~let~ expressions are derived expressions, because

#+begin_src scheme
(let ((<VAR_1> <EXP_1>) ... (<VAR_N> <EXP_N>))
  <BODY>)
#+end_src

is equivalent to

#+begin_src scheme
((lambda (<VAR_1> ... <VAR_N>)
   <BODY>)
 <EXP_1>
 ...
 <EXP_N>)
#+end_src

Implement a syntactic transformation ~let->combination~ that reduces evaluating ~let~ expressions to evaluating combinations of the type shown above, and add the appropriate clause to ~eval~ to handle ~let~ expressions.

**** Exercise 4.7
:properties:
:custom_id: exercise-4.7
:end:

~let*~ is similar to ~let~, except that the bindings of the ~let~ variables are performed sequentially from left to right, and each binding is made in an environment in which all of the preceding bindings are visible.  For example

#+begin_src scheme
(let* ((x 3)
       (y (+ x 2))
       (z (+ x y 5)))
  (* x z))
#+end_src

returns 39.  Explain how a ~let*~ expression can be rewritten as a set of nested ~let~ expressions, and write a procedure ~let*->nested-lets~ that performs this transformation.  If we have already implemented ~let~ ([[#exercise-4.6][Exercise 4.6]]) and we want to extend the evaluator to handle ~let*~, is it sufficient to add a clause to ~eval~ whose action is

#+begin_src scheme
(eval (let*->nested-lets exp) env)
#+end_src

or must we explicitly expand ~let*~ in terms of non-derived expressions?

**** Exercise 4.8
:properties:
:custom_id: exercise-4.8
:end:

"Named ~let~" is a variant of ~let~ that has the form

#+begin_src scheme
(let <VAR> <BINDINGS> <BODY>)
#+end_src

The ~<BINDINGS>~ and ~<BODY>~ are just as in ordinary ~let~, except that ~<VAR>~ is bound within ~<BODY>~ to a procedure whose body is ~<BODY>~ and whose parameters are the variables in the ~<BINDINGS>~.  Thus, one can repeatedly execute the ~<BODY>~ by invoking the procedure named ~<VAR>~.  For example, the iterative Fibonacci procedure (section [[#section-1.2.2][1.2.2]]) can be rewritten using named ~let~ as follows:

#+begin_src scheme
(define (fib n)
  (let fib-iter ((a 1)
                 (b 0)
                 (count n))
    (if (= count 0)
        b
        (fib-iter (+ a b) a (- count 1)))))
#+end_src

Modify ~let->combination~ of [[#exercise-4.6][Exercise 4.6]] to also support named ~let~.

**** Exercise 4.9
:properties:
:custom_id: exercise-4.9
:end:

Many languages support a variety of iteration constructs, such as ~do~, ~for~, ~while~, and ~until~.  In Scheme, iterative processes can be expressed in terms of ordinary procedure calls, so special iteration constructs provide no essential gain in computational power.  On the other hand, such constructs are often convenient.  Design some iteration constructs, give examples of their use, and show how to implement them as derived expressions.

**** Exercise 4.10
:properties:
:custom_id: exercise-4.10
:end:

By using data abstraction, we were able to write an ~eval~ procedure that is independent of the particular syntax of the language to be evaluated.  To illustrate this, design and implement a new syntax for Scheme by modifying the procedures in this section, without changing ~eval~ or ~apply~.

*** 4.1.3 Evaluator Data Structures
:properties:
:custom_id: section-4.1.3
:end:

In addition to defining the external syntax of expressions, the evaluator implementation must also define the data structures that the evaluator manipulates internally, as part of the execution of a program, such as the representation of procedures and environments and the representation of true and false.

*Testing of predicates*

For conditionals, we accept anything to be true that is not the explicit ~false~ object.

#+begin_src scheme
(define (true? x)
  (not (eq? x false)))

(define (false? x)
  (eq? x false))
#+end_src

*Representing procedures*

To handle primitives, we assume that we have available the following procedures:

- ~(apply-primitive-procedure <PROC> <ARGS>)~

  applies the given primitive procedure to the argument values in the list <ARGS> and returns the result of the application.

- ~(primitive-procedure? <PROC>)~

tests whether ~<PROC>~ is a primitive procedure.

These mechanisms for handling primitives are further described in section [[#section-4.1.4][4.1.4]].

Compound procedures are constructed from parameters, procedure bodies, and environments using the constructor ~make-procedure~:

#+begin_src scheme
(define (make-procedure parameters body env)
  (list 'procedure parameters body env))

(define (compound-procedure? p)
  (tagged-list? p 'procedure))

(define (procedure-parameters p) (cadr p))

(define (procedure-body p) (caddr p))

(define (procedure-environment p) (cadddr p))
#+end_src

*Operations on Environments*

The evaluator needs operations for manipulating environments.  As explained in section [[#section-3.2][3.2]], an environment is a sequence of frames, where each frame is a table of bindings that associate variables with their corresponding values.  We use the following operations for manipulating environments:

- ~(lookup-variable-value <VAR> <ENV>)~

returns the value that is bound to the symbol ~<VAR>~ in the environment ~<ENV>~, or signals an error if the variable is unbound.

- ~(extend-environment <VARIABLES> <VALUES> <BASE-ENV>)~

returns a new environment, consisting of a new frame in which the symbols in the list ~<VARIABLES>~ are bound to the corresponding elements in the list ~<VALUES>~, where the enclosing environment is the environment ~<BASE-ENV>~.

- ~(define-variable! <VAR> <VALUE> <ENV>)~

adds to the first frame in the environment ~<ENV>~ a new binding that associates the variable ~<VAR>~ with the value ~<VALUE>~.

- ~(set-variable-value! <VAR> <VALUE> <ENV>)~

changes the binding of the variable ~<VAR>~ in the environment ~<ENV>~ so that the variable is now bound to the value ~<VALUE>~, or signals an error if the variable is unbound.

To implement these operations we represent an environment as a list of frames.  The enclosing environment of an environment is the ~cdr~ of the list.  The empty environment is simply the empty list.

#+begin_src scheme
(define (enclosing-environment env) (cdr env))

(define (first-frame env) (car env))

(define the-empty-environment '())
#+end_src

Each frame of an environment is represented as a pair of lists: a list of the variables bound in that frame and a list of the associated values.[fn:218]

#+begin_src scheme
(define (make-frame variables values)
  (cons variables values))

(define (frame-variables frame) (car frame))

(define (frame-values frame) (cdr frame))

(define (add-binding-to-frame! var val frame)
  (set-car! frame (cons var (car frame)))
  (set-cdr! frame (cons val (cdr frame))))
#+end_src

To extend an environment by a new frame that associates variables with values, we make a frame consisting of the list of variables and the list of values, and we adjoin this to the environment.  We signal an error if the number of variables does not match the number of values.

#+begin_src scheme
(define (extend-environment vars vals base-env)
  (if (= (length vars) (length vals))
      (cons (make-frame vars vals) base-env)
      (if (< (length vars) (length vals))
          (error "Too many arguments supplied" vars vals)
          (error "Too few arguments supplied" vars vals))))
#+end_src

To look up a variable in an environment, we scan the list of variables in the first frame.  If we find the desired variable, we return the corresponding element in the list of values.  If we do not find the variable in the current frame, we search the enclosing environment, and so on.  If we reach the empty environment, we signal an "unbound variable" error.

#+begin_src scheme
(define (lookup-variable-value var env)
  (define (env-loop env)
    (define (scan vars vals)
      (cond ((null? vars)
             (env-loop (enclosing-environment env)))
            ((eq? var (car vars))
             (car vals))
            (else (scan (cdr vars) (cdr vals)))))
    (if (eq? env the-empty-environment)
        (error "Unbound variable" var)
        (let ((frame (first-frame env)))
          (scan (frame-variables frame)
                (frame-values frame)))))
  (env-loop env))
#+end_src

To set a variable to a new value in a specified environment, we scan for the variable, just as in ~lookup-variable-value~, and change the corresponding value when we find it.

#+begin_src scheme
(define (set-variable-value! var val env)
  (define (env-loop env)
    (define (scan vars vals)
      (cond ((null? vars)
             (env-loop (enclosing-environment env)))
            ((eq? var (car vars))
             (set-car! vals val))
            (else (scan (cdr vars) (cdr vals)))))
    (if (eq? env the-empty-environment)
        (error "Unbound variable - SET!" var)
        (let ((frame (first-frame env)))
          (scan (frame-variables frame)
                (frame-values frame)))))
  (env-loop env))
#+end_src

To define a variable, we search the first frame for a binding for the variable, and change the binding if it exists (just as in ~set-variable-value!~).  If no such binding exists, we adjoin one to the first frame.

#+begin_src scheme
(define (define-variable! var val env)
  (let ((frame (first-frame env)))
    (define (scan vars vals)
      (cond ((null? vars)
             (add-binding-to-frame! var val frame))
            ((eq? var (car vars))
             (set-car! vals val))
            (else (scan (cdr vars) (cdr vals)))))
    (scan (frame-variables frame)
          (frame-values frame))))
#+end_src

The method described here is only one of many plausible ways to represent environments.  Since we used data abstraction to isolate the rest of the evaluator from the detailed choice of representation, we could change the environment representation if we wanted to.  (See [[#exercise-4.11][Exercise 4.11]].)  In a production-quality Lisp system, the speed of the evaluator's environment operations--especially that of variable lookup--has a major impact on the performance of the system.  The representation described here, although conceptually simple, is not efficient and would not ordinarily be used in a production system.[fn:219]

**** Exercise 4.11
:properties:
:custom_id: exercise-4.11
:end:

Instead of representing a frame as a pair of lists, we can represent a frame as a list of bindings, where each binding is a name-value pair.  Rewrite the environment operations to use this alternative representation.

**** Exercise 4.12
:properties:
:custom_id: exercise-4.12
:end:

The procedures ~set-variable-value!~, ~define-variable!~, and ~lookup-variable-value~ can be expressed in terms of more abstract procedures for traversing the environment structure.  Define abstractions that capture the common patterns and redefine the three procedures in terms of these abstractions.

**** Exercise 4.13
:properties:
:custom_id: exercise-4.13
:end:

Scheme allows us to create new bindings for variables by means of ~define~, but provides no way to get rid of bindings.  Implement for the evaluator a special form ~make-unbound!~ that removes the binding of a given symbol from the environment in which the ~make-unbound!~ expression is evaluated.  This problem is not completely specified.  For example, should we remove only the binding in the first frame of the environment?  Complete the specification and justify any choices you make.

*** 4.1.4 Running the Evaluator as a Program
:properties:
:custom_id: section-4.1.4
:end:

Given the evaluator, we have in our hands a description (expressed in Lisp) of the process by which Lisp expressions are evaluated.  One advantage of expressing the evaluator as a program is that we can run the program.  This gives us, running within Lisp, a working model of how Lisp itself evaluates expressions.  This can serve as a framework for experimenting with evaluation rules, as we shall do later in this chapter.

Our evaluator program reduces expressions ultimately to the application of primitive procedures.  Therefore, all that we need to run the evaluator is to create a mechanism that calls on the underlying Lisp system to model the application of primitive procedures.

There must be a binding for each primitive procedure name, so that when ~eval~ evaluates the operator of an application of a primitive, it will find an object to pass to ~apply~.  We thus set up a global environment that associates unique objects with the names of the primitive procedures that can appear in the expressions we will be evaluating.  The global environment also includes bindings for the symbols ~true~ and ~false~, so that they can be used as variables in expressions to be evaluated.

#+begin_src scheme
(define (setup-environment)
  (let ((initial-env
         (extend-environment (primitive-procedure-names)
                             (primitive-procedure-objects)
                             the-empty-environment)))
    (define-variable! 'true true initial-env)
    (define-variable! 'false false initial-env)
    initial-env))

(define the-global-environment (setup-environment))
#+end_src

It does not matter how we represent the primitive procedure objects, so long as ~apply~ can identify and apply them by using the procedures ~primitive-procedure?~ and ~apply-primitive-procedure~.  We have chosen to represent a primitive procedure as a list beginning with the symbol ~primitive~ and containing a procedure in the underlying Lisp that implements that primitive.

#+begin_src scheme
(define (primitive-procedure? proc)
  (tagged-list? proc 'primitive))

(define (primitive-implementation proc) (cadr proc))
#+end_src

~setup-environment~ will get the primitive names and implementation procedures from a list:[fn:220]

#+begin_src scheme
(define primitive-procedures
  (list (list 'car car)
        (list 'cdr cdr)
        (list 'cons cons)
        (list 'null? null?)
        <MORE PRIMITIVES>
        ))

(define (primitive-procedure-names)
  (map car
       primitive-procedures))

(define (primitive-procedure-objects)
  (map (lambda (proc) (list 'primitive (cadr proc)))
       primitive-procedures))
#+end_src

To apply a primitive procedure, we simply apply the implementation procedure to the arguments, using the underlying Lisp system:[fn:221]

#+begin_src scheme
(define (apply-primitive-procedure proc args)
  (apply-in-underlying-scheme
   (primitive-implementation proc) args))
#+end_src

For convenience in running the metacircular evaluator, we provide a <<i117>> driver loop that models the read-eval-print loop of the underlying Lisp system.  It prints a <<i307>> prompt, reads an input expression, evaluates this expression in the global environment, and prints the result.  We precede each printed result by an <<i275>> output prompt so as to distinguish the value of the expression from other output that may be printed.[fn:222]

#+begin_src scheme
(define input-prompt ";;; M-Eval input:")
(define output-prompt ";;; M-Eval value:")

(define (driver-loop)
  (prompt-for-input input-prompt)
  (let ((input (read)))
    (let ((output (eval input the-global-environment)))
      (announce-output output-prompt)
      (user-print output)))
  (driver-loop))

(define (prompt-for-input string)
  (newline) (newline) (display string) (newline))

(define (announce-output string)
  (newline) (display string) (newline))
#+end_src

We use a special printing procedure, ~user-print~, to avoid printing the environment part of a compound procedure, which may be a very long list (or may even contain cycles).

#+begin_src scheme
(define (user-print object)
  (if (compound-procedure? object)
      (display (list 'compound-procedure
                     (procedure-parameters object)
                     (procedure-body object)
                     '<procedure-env>))
      (display object)))
#+end_src

Now all we need to do to run the evaluator is to initialize the global environment and start the driver loop.  Here is a sample interaction:

#+begin_src scheme
(define the-global-environment (setup-environment))

(driver-loop)

;;; M-Eval input:
(define (append x y)
  (if (null? x)
      y
      (cons (car x)
            (append (cdr x) y))))
;;; M-Eval value:
ok

;;; M-Eval input:
(append '(a b c) '(d e f))
;;; M-Eval value:
(a b c d e f)
#+end_src

**** Exercise 4.14
:properties:
:custom_id: exercise-4.14
:end:

Eva Lu Ator and Louis Reasoner are each experimenting with the metacircular evaluator.  Eva types in the definition of ~map~, and runs some test programs that use it.  They work fine.  Louis, in contrast, has installed the system version of ~map~ as a primitive for the metacircular evaluator.  When he tries it, things go terribly wrong.  Explain why Louis's ~map~ fails even though Eva's works.

*** 4.1.5 Data as Programs
:properties:
:custom_id: section-4.1.5
:end:

In thinking about a Lisp program that evaluates Lisp expressions, an analogy might be helpful.  One operational view of the meaning of a program is that a program is a description of an abstract (perhaps infinitely large) machine.  For example, consider the familiar program to compute factorials:

#+begin_src scheme
(define (factorial n)
  (if (= n 1)
      1
      (* (factorial (- n 1)) n)))
#+end_src

We may regard this program as the description of a machine containing parts that decrement, multiply, and test for equality, together with a two-position switch and another factorial machine.  (The factorial machine is infinite because it contains another factorial machine within it.)  [[figure-4.2][Figure 4.2]] is a flow diagram for the factorial machine, showing how the parts are wired together.

<<figure-4.2>> The factorial program, viewed as an abstract machine.

#+begin_example
     +-----------------------------------+
     | factorial                   |1    |
     |              |1             V     |
     |              |           +-----+  |
     |              V           | #   |  |
     |           +-----+        |     |  |
 6 --------*-----|  =  |------->|   #-+-----> 720
     |     |     +-----+        |  /  |  |
     |     |                    | #   |  |
     |     |                    +-----+  |
     |     |                       ^     |
     |     |                       |     |
     |     |                    +--+--+  |
     |     *------------------->|  *  |  |
     |     |                    +-----+  |
     |     V                       ^     |
     |  +-----+    +-----------+   |     |
     |  |  -  +--->| factorial +---+     |
     |  +-----+    +-----------+         |
     |     ^                             |
     |     |1                            |
     +-----------------------------------+
#+end_example

In a similar way, we can regard the evaluator as a very special machine that takes as input a description of a machine.  Given this input, the evaluator configures itself to emulate the machine described.  For example, if we feed our evaluator the definition of ~factorial~, as shown in [[figure-4.3][Figure 4.3]], the evaluator will be able to compute factorials.

<<figure-4.3>> The evaluator emulating a factorial machine.

#+begin_example
                 +--------+
          6 ---->|  eval  |----> 720
                 +--------+
                     /
           . . .    /  . . .
     . . .       ../. .      .
   .                           ..
  .   (define (factorial n)      . . .
 .      (if (= n 1)                   . .
  .         1                            .
  .         (* (factorial (- n 1)) n)))   .
    . .                       . .        .
        . .  . .      . . . .     . . . .
                 . ..
#+end_example

From this perspective, our evaluator is seen to be a <<i415>> universal machine.  It mimics other machines when these are described as Lisp programs.[fn:223] This is striking.  Try to imagine an analogous evaluator for electrical circuits.  This would be a circuit that takes as input a signal encoding the plans for some other circuit, such as a filter.  Given this input, the circuit evaluator would then behave like a filter with the same description.  Such a universal electrical circuit is almost unimaginably complex.  It is remarkable that the program evaluator is a rather simple program.[fn:224]

Another striking aspect of the evaluator is that it acts as a bridge between the data objects that are manipulated by our programming language and the programming language itself.  Imagine that the evaluator program (implemented in Lisp) is running, and that a user is typing expressions to the evaluator and observing the results.  From the perspective of the user, an input expression such as ~(* x x)~ is an expression in the programming language, which the evaluator should execute.  From the perspective of the evaluator, however, the expression is simply a list (in this case, a list of three symbols: ~*~, ~x~, and ~x~) that is to be manipulated according to a well-defined set of rules.

That the user's programs are the evaluator's data need not be a source of confusion.  In fact, it is sometimes convenient to ignore this distinction, and to give the user the ability to explicitly evaluate a data object as a Lisp expression, by making ~eval~ available for use in programs.  Many Lisp dialects provide a primitive ~eval~ procedure that takes as arguments an expression and an environment and evaluates the expression relative to the environment.[fn:225] Thus,

#+begin_src scheme
(eval '(* 5 5) user-initial-environment)
#+end_src

and

#+begin_src scheme
(eval (cons '* (list 5 5)) user-initial-environment)
#+end_src

will both return 25.[fn:226]

**** Exercise 4.15
:properties:
:custom_id: exercise-4.15
:end:

Given a one-argument procedure ~p~ and an object ~a~, ~p~ is said to "halt" on ~a~ if evaluating the expression ~(p a)~ returns a value (as opposed to terminating with an error message or running forever).  Show that it is impossible to write a procedure ~halts?~ that correctly determines whether ~p~ halts on ~a~ for any procedure ~p~ and object ~a~.  Use the following reasoning: If you had such a procedure ~halts?~, you could implement the following program:

#+begin_src scheme
(define (run-forever) (run-forever))

(define (try p)
  (if (halts? p p)
      (run-forever)
      'halted))
#+end_src

Now consider evaluating the expression ~(try try)~ and show that any possible outcome (either halting or running forever) violates the intended behavior of ~halts?~.[fn:227]

*** 4.1.6 Internal Definitions
:properties:
:custom_id: section-4.1.6
:end:

Our environment model of evaluation and our metacircular evaluator execute definitions in sequence, extending the environment frame one definition at a time.  This is particularly convenient for interactive program development, in which the programmer needs to freely mix the application of procedures with the definition of new procedures.  However, if we think carefully about the internal definitions used to implement block structure (introduced in section [[#section-1.1.8][1.1.8]]), we will find that name-by-name extension of the environment may not be the best way to define local variables.

Consider a procedure with internal definitions, such as

#+begin_src scheme
(define (f x)
  (define (even? n)
    (if (= n 0)
        true
        (odd? (- n 1))))
  (define (odd? n)
    (if (= n 0)
        false
        (even? (- n 1))))
  <REST OF BODY OF ~F~>)
#+end_src

Our intention here is that the name ~odd?~ in the body of the procedure ~even?~ should refer to the procedure ~odd?~ that is defined after ~even?~.  The scope of the name ~odd?~ is the entire body of ~f~, not just the portion of the body of ~f~ starting at the point where the ~define~ for ~odd?~ occurs.  Indeed, when we consider that ~odd?~ is itself defined in terms of ~even?~--so that ~even?~ and ~odd?~ are mutually recursive procedures--we see that the only satisfactory interpretation of the two ~define~s is to regard them as if the names ~even?~ and ~odd?~ were being added to the environment simultaneously.  More generally, in block structure, the scope of a local name is the entire procedure body in which the ~define~ is evaluated.

As it happens, our interpreter will evaluate calls to ~f~ correctly, but for an "accidental" reason: Since the definitions of the internal procedures come first, no calls to these procedures will be evaluated until all of them have been defined.  Hence, ~odd?~ will have been defined by the time ~even?~ is executed.  In fact, our sequential evaluation mechanism will give the same result as a mechanism that directly implements simultaneous definition for any procedure in which the internal definitions come first in a body and evaluation of the value expressions for the defined variables doesn't actually use any of the defined variables.  (For an example of a procedure that doesn't obey these restrictions, so that sequential definition isn't equivalent to simultaneous definition, see [[#exercise-4.19][Exercise 4.19]].)[fn:228]

There is, however, a simple way to treat definitions so that internally defined names have truly simultaneous scope--just create all local variables that will be in the current environment before evaluating any of the value expressions.  One way to do this is by a syntax transformation on ~lambda~ expressions.  Before evaluating the body of a ~lambda~ expression, we "scan out" and eliminate all the internal definitions in the body.  The internally defined variables will be created with a ~let~ and then set to their values by assignment.  For example, the procedure

#+begin_src scheme
(lambda <VARS>
  (define u <E1>)
  (define v <E2>)
  <E3>)
#+end_src

would be transformed into

#+begin_src scheme
(lambda <VARS>
  (let ((u '*unassigned*)
        (v '*unassigned*))
    (set! u <E1>)
    (set! v <E2>)
    <E3>))
#+end_src

where ~*unassigned*~ is a special symbol that causes looking up a variable to signal an error if an attempt is made to use the value of the not-yet-assigned variable.

An alternative strategy for scanning out internal definitions is shown in [[#exercise-4.18][Exercise 4.18]].  Unlike the transformation shown above, this enforces the restriction that the defined variables' values can be evaluated without using any of the variables' values.[fn:229]

**** Exercise 4.16
:properties:
:custom_id: exercise-4.16
:end:

In this exercise we implement the method just described for interpreting internal definitions.  We assume that the evaluator supports ~let~ (see [[#exercise-4.6][Exercise 4.6]]).

a. Change ~lookup-variable-value~ (section [[#section-4.1.3][4.1.3]]) to signal an error if the value it finds is the symbol ~*unassigned*~.

b. Write a procedure ~scan-out-defines~ that takes a procedure body and returns an equivalent one that has no internal definitions, by making the transformation described above.

c. Install ~scan-out-defines~ in the interpreter, either in ~make-procedure~ or in ~procedure-body~ (see section [[#section-4.1.3][4.1.3]]).  Which place is better?  Why?

**** Exercise 4.17
:properties:
:custom_id: exercise-4.17
:end:

Draw diagrams of the environment in effect when evaluating the expression <E3> in the procedure in the text, comparing how this will be structured when definitions are interpreted sequentially with how it will be structured if definitions are scanned out as described.  Why is there an extra frame in the transformed program?  Explain why this difference in environment structure can never make a difference in the behavior of a correct program.  Design a way to make the interpreter implement the "simultaneous" scope rule for internal definitions without constructing the extra frame.

**** Exercise 4.18
:properties:
:custom_id: exercise-4.18
:end:

Consider an alternative strategy for scanning out definitions that translates the example in the text to

#+begin_src scheme
(lambda <VARS>
  (let ((u '*unassigned*)
        (v '*unassigned*))
    (let ((a <E1>)
          (b <E2>))
      (set! u a)
      (set! v b))
    <E3>))
#+end_src

Here ~a~ and ~b~ are meant to represent new variable names, created by the interpreter, that do not appear in the user's program.  Consider the ~solve~ procedure from section [[#section-3.5.4][3.5.4]]:

#+begin_src scheme
(define (solve f y0 dt)
  (define y (integral (delay dy) y0 dt))
  (define dy (stream-map f y))
  y)
#+end_src

Will this procedure work if internal definitions are scanned out as shown in this exercise?  What if they are scanned out as shown in the text?  Explain.

**** Exercise 4.19
:properties:
:custom_id: exercise-4.19
:end:

Ben Bitdiddle, Alyssa P. Hacker, and Eva Lu Ator are arguing about the desired result of evaluating the expression

#+begin_src scheme
(let ((a 1))
  (define (f x)
    (define b (+ a x))
    (define a 5)
    (+ a b))
  (f 10))
#+end_src

Ben asserts that the result should be obtained using the sequential rule for ~define~: ~b~ is defined to be 11, then ~a~ is defined to be 5, so the result is 16.  Alyssa objects that mutual recursion requires the simultaneous scope rule for internal procedure definitions, and that it is unreasonable to treat procedure names differently from other names.  Thus, she argues for the mechanism implemented in [[#exercise-4.16][Exercise 4.16]].  This would lead to ~a~ being unassigned at the time that the value for ~b~ is to be computed.  Hence, in Alyssa's view the procedure should produce an error.  Eva has a third opinion.  She says that if the definitions of ~a~ and ~b~ are truly meant to be simultaneous, then the value 5 for ~a~ should be used in evaluating ~b~.  Hence, in Eva's view ~a~ should be 5, ~b~ should be 15, and the result should be 20.  Which (if any) of these viewpoints do you support?  Can you devise a way to implement internal definitions so that they behave as Eva prefers?[fn:230]

**** Exercise 4.20
:properties:
:custom_id: exercise-4.20
:end:

Because internal definitions look sequential but are actually simultaneous, some people prefer to avoid them entirely, and use the special form ~letrec~ instead.  ~letrec~ looks like ~let~, so it is not surprising that the variables it binds are bound simultaneously and have the same scope as each other.  The sample procedure ~f~ above can be written without internal definitions, but with exactly the same meaning, as

#+begin_src scheme
(define (f x)
  (letrec ((even?
            (lambda (n)
              (if (= n 0)
                  true
                  (odd? (- n 1)))))
           (odd?
            (lambda (n)
              (if (= n 0)
                  false
                  (even? (- n 1))))))
    <REST OF BODY OF ~F~>))
#+end_src

~letrec~ expressions, which have the form

#+begin_src scheme
(letrec ((<VAR_1> <EXP_1>) ... (<VAR_N> <EXP_N>))
  <BODY>)
#+end_src

are a variation on ~let~ in which the expressions <EXP_K> that provide the initial values for the variables <VAR_K> are evaluated in an environment that includes all the ~letrec~ bindings.  This permits recursion in the bindings, such as the mutual recursion of ~even?~ and ~odd?~ in the example above, or the evaluation of 10 factorial with

#+begin_src scheme
(letrec ((fact
          (lambda (n)
            (if (= n 1)
                1
                (* n (fact (- n 1)))))))
  (fact 10))
#+end_src

a. Implement ~letrec~ as a derived expression, by transforming a ~letrec~ expression into a ~let~ expression as shown in the text above or in [[#exercise-4.18][Exercise 4.18]].  That is, the ~letrec~ variables should be created with a ~let~ and then be assigned their values with ~set!~.

b. Louis Reasoner is confused by all this fuss about internal definitions.  The way he sees it, if you don't like to use ~define~ inside a procedure, you can just use ~let~.  Illustrate what is loose about his reasoning by drawing an environment diagram that shows the environment in which the <REST OF BODY OF F> is evaluated during evaluation of the expression ~(f 5)~, with ~f~ defined as in this exercise.  Draw an environment diagram for the same evaluation, but with ~let~ in place of ~letrec~ in the definition of ~f~.

**** Exercise 4.21
:properties:
:custom_id: exercise-4.21
:end:

Amazingly, Louis's intuition in [[#exercise-4.20][Exercise 4.20]] is correct.  It is indeed possible to specify recursive procedures without using ~letrec~ (or even ~define~), although the method for accomplishing this is much more subtle than Louis imagined.  The following expression computes 10 factorial by applying a recursive factorial procedure:[fn:231]

#+begin_src scheme
((lambda (n)
   ((lambda (fact)
      (fact fact n))
    (lambda (ft k)
      (if (= k 1)
          1
          (* k (ft ft (- k 1)))))))
 10)
#+end_src

a. Check (by evaluating the expression) that this really does compute factorials.  Devise an analogous expression for computing Fibonacci numbers.

b. Consider the following procedure, which includes mutually recursive internal definitions:

#+begin_src scheme
(define (f x)
  (define (even? n)
    (if (= n 0)
        true
        (odd? (- n 1))))
  (define (odd? n)
    (if (= n 0)
        false
        (even? (- n 1))))
  (even? x))
#+end_src

Fill in the missing expressions to complete an alternative definition of ~f~, which uses neither internal definitions nor ~letrec~:

#+begin_src scheme
(define (f x)
  ((lambda (even? odd?)
     (even? even? odd? x))
   (lambda (ev? od? n)
     (if (= n 0) true (od? <??> <??> <??>)))
   (lambda (ev? od? n)
     (if (= n 0) false (ev? <??> <??> <??>)))))
#+end_src

*** 4.1.7 Separating Syntactic Analysis from Execution
:properties:
:custom_id: section-4.1.7
:end:

The evaluator implemented above is simple, but it is very inefficient, because the syntactic analysis of expressions is interleaved with their execution.  Thus if a program is executed many times, its syntax is analyzed many times.  Consider, for example, evaluating ~(factorial 4)~ using the following definition of ~factorial~:

#+begin_src scheme
(define (factorial n)
  (if (= n 1)
      1
      (* (factorial (- n 1)) n)))
#+end_src

Each time ~factorial~ is called, the evaluator must determine that the body is an ~if~ expression and extract the predicate.  Only then can it evaluate the predicate and dispatch on its value.  Each time it evaluates the expression ~(* (factorial (- n 1)) n)~, or the subexpressions ~(factorial (- n 1))~ and ~(- n 1)~, the evaluator must perform the case analysis in ~eval~ to determine that the expression is an application, and must extract its operator and operands.  This analysis is expensive.  Performing it repeatedly is wasteful.

We can transform the evaluator to be significantly more efficient by arranging things so that syntactic analysis is performed only once.[fn:232] We split ~eval~, which takes an expression and an environment, into two parts.  The procedure ~analyze~ takes only the expression.  It performs the syntactic analysis and returns a new procedure, the <<i132>> execution procedure, that encapsulates the work to be done in executing the analyzed expression.  The execution procedure takes an environment as its argument and completes the evaluation.  This saves work because ~analyze~ will be called only once on an expression, while the execution procedure may be called many times.

With the separation into analysis and execution, ~eval~ now becomes

#+begin_src scheme
(define (eval exp env)
  ((analyze exp) env))
#+end_src

The result of calling ~analyze~ is the execution procedure to be applied to the environment.  The ~analyze~ procedure is the same case analysis as performed by the original ~eval~ of section [[#section-4.1.1][4.1.1]], except that the procedures to which we dispatch perform only analysis, not full evaluation:

#+begin_src scheme
(define (analyze exp)
  (cond ((self-evaluating? exp)
         (analyze-self-evaluating exp))
        ((quoted? exp) (analyze-quoted exp))
        ((variable? exp) (analyze-variable exp))
        ((assignment? exp) (analyze-assignment exp))
        ((definition? exp) (analyze-definition exp))
        ((if? exp) (analyze-if exp))
        ((lambda? exp) (analyze-lambda exp))
        ((begin? exp) (analyze-sequence (begin-actions exp)))
        ((cond? exp) (analyze (cond->if exp)))
        ((application? exp) (analyze-application exp))
        (else
         (error "Unknown expression type - ANALYZE" exp))))
#+end_src

Here is the simplest syntactic analysis procedure, which handles self-evaluating expressions.  It returns an execution procedure that ignores its environment argument and just returns the expression:

#+begin_src scheme
(define (analyze-self-evaluating exp)
  (lambda (env) exp))
#+end_src

For a quoted expression, we can gain a little efficiency by extracting the text of the quotation only once, in the analysis phase, rather than in the execution phase.

#+begin_src scheme
(define (analyze-quoted exp)
  (let ((qval (text-of-quotation exp)))
    (lambda (env) qval)))
#+end_src

Looking up a variable value must still be done in the execution phase, since this depends upon knowing the environment.[fn:233]

#+begin_src scheme
(define (analyze-variable exp)
  (lambda (env) (lookup-variable-value exp env)))
#+end_src

~analyze-assignment~ also must defer actually setting the variable until the execution, when the environment has been supplied.  However, the fact that the ~assignment-value~ expression can be analyzed (recursively) during analysis is a major gain in efficiency, because the ~assignment-value~ expression will now be analyzed only once.  The same holds true for definitions.

#+begin_src scheme
(define (analyze-assignment exp)
  (let ((var (assignment-variable exp))
        (vproc (analyze (assignment-value exp))))
    (lambda (env)
      (set-variable-value! var (vproc env) env)
      'ok)))

(define (analyze-definition exp)
  (let ((var (definition-variable exp))
        (vproc (analyze (definition-value exp))))
    (lambda (env)
      (define-variable! var (vproc env) env)
      'ok)))
#+end_src

For ~if~ expressions, we extract and analyze the predicate, consequent, and alternative at analysis time.

#+begin_src scheme
(define (analyze-if exp)
  (let ((pproc (analyze (if-predicate exp)))
        (cproc (analyze (if-consequent exp)))
        (aproc (analyze (if-alternative exp))))
    (lambda (env)
      (if (true? (pproc env))
          (cproc env)
          (aproc env)))))
#+end_src

Analyzing a ~lambda~ expression also achieves a major gain in efficiency: We analyze the ~lambda~ body only once, even though procedures resulting from evaluation of the ~lambda~ may be applied many times.

#+begin_src scheme
(define (analyze-lambda exp)
  (let ((vars (lambda-parameters exp))
        (bproc (analyze-sequence (lambda-body exp))))
    (lambda (env) (make-procedure vars bproc env))))
#+end_src

Analysis of a sequence of expressions (as in a ~begin~ or the body of a ~lambda~ expression) is more involved.[fn:234] Each expression in the sequence is analyzed, yielding an execution procedure.  These execution procedures are combined to produce an execution procedure that takes an environment as argument and sequentially calls each individual execution procedure with the environment as argument.

#+begin_src scheme
(define (analyze-sequence exps)
  (define (sequentially proc1 proc2)
    (lambda (env) (proc1 env) (proc2 env)))
  (define (loop first-proc rest-procs)
    (if (null? rest-procs)
        first-proc
        (loop (sequentially first-proc (car rest-procs))
              (cdr rest-procs))))
  (let ((procs (map analyze exps)))
    (if (null? procs)
        (error "Empty sequence - ANALYZE"))
    (loop (car procs) (cdr procs))))
#+end_src

To analyze an application, we analyze the operator and operands and construct an execution procedure that calls the operator execution procedure (to obtain the actual procedure to be applied) and the operand execution procedures (to obtain the actual arguments).  We then pass these to ~execute-application~, which is the analog of ~apply~ in section [[#section-4.1.1][4.1.1]].  ~execute-application~ differs from ~apply~ in that the procedure body for a compound procedure has already been analyzed, so there is no need to do further analysis.  Instead, we just call the execution procedure for the body on the extended environment.

#+begin_src scheme
(define (analyze-application exp)
  (let ((fproc (analyze (operator exp)))
        (aprocs (map analyze (operands exp))))
    (lambda (env)
      (execute-application (fproc env)
                           (map (lambda (aproc) (aproc env))
                                aprocs)))))

(define (execute-application proc args)
  (cond ((primitive-procedure? proc)
         (apply-primitive-procedure proc args))
        ((compound-procedure? proc)
         ((procedure-body proc)
          (extend-environment (procedure-parameters proc)
                              args
                              (procedure-environment proc))))
        (else
         (error
          "Unknown procedure type - EXECUTE-APPLICATION"
          proc))))
#+end_src

Our new evaluator uses the same data structures, syntax procedures, and run-time support procedures as in sections [[#section-4.1.2][4.1.2]], [[#section-4.1.3][4.1.3]], and [[#section-4.1.4][4.1.4]].

**** Exercise 4.22
:properties:
:custom_id: exercise-4.22
:end:

Extend the evaluator in this section to support the special form ~let~.  (See [[#exercise-4.6][Exercise 4.6]].)

**** Exercise 4.23
:properties:
:custom_id: exercise-4.23
:end:

Alyssa P. Hacker doesn't understand why ~analyze-sequence~ needs to be so complicated.  All the other analysis procedures are straightforward transformations of the corresponding evaluation procedures (or ~eval~ clauses) in section [[#section-4.1.1][4.1.1]].  She expected ~analyze-sequence~ to look like this:

#+begin_src scheme
(define (analyze-sequence exps)
  (define (execute-sequence procs env)
    (cond ((null? (cdr procs)) ((car procs) env))
          (else ((car procs) env)
                (execute-sequence (cdr procs) env))))
  (let ((procs (map analyze exps)))
    (if (null? procs)
        (error "Empty sequence - ANALYZE"))
    (lambda (env) (execute-sequence procs env))))
#+end_src

Eva Lu Ator explains to Alyssa that the version in the text does more of the work of evaluating a sequence at analysis time.  Alyssa's sequence-execution procedure, rather than having the calls to the individual execution procedures built in, loops through the procedures in order to call them: In effect, although the individual expressions in the sequence have been analyzed, the sequence itself has not been.

Compare the two versions of ~analyze-sequence~.  For example, consider the common case (typical of procedure bodies) where the sequence has just one expression.  What work will the execution procedure produced by Alyssa's program do?  What about the execution procedure produced by the program in the text above?  How do the two versions compare for a sequence with two expressions?

**** Exercise 4.24
:properties:
:custom_id: exercise-4.24
:end:

Design and carry out some experiments to compare the speed of the original metacircular evaluator with the version in this section.  Use your results to estimate the fraction of time that is spent in analysis versus execution for various procedures.
