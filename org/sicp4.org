* 4 Metalinguistic Abstraction
:properties:
:custom_id: section-4
:end:
** Introduction
:properties:
:VISIBILITY: notoc
:end:

#+begin_quote
... It's in words that the magic is--Abracadabra, Open Sesame, and the rest--but the magic words in one story aren't magical in the next.  The real magic is to understand which words work, and when, and for what; the trick is to learn the trick.

... And those words are made from the letters of our alphabet: a couple-dozen squiggles we can draw with the pen.  This is the key!  And the treasure, too, if we can only get our hands on it!  It's as if--as if the key to the treasure /is/ the treasure!

--John Barth, 'Chimera'
#+end_quote

In our study of program design, we have seen that expert programmers control the complexity of their designs with the same general techniques used by designers of all complex systems.  They combine primitive elements to form compound objects, they abstract compound objects to form higher-level building blocks, and they preserve modularity by adopting appropriate large-scale views of system structure.  In illustrating these techniques, we have used Lisp as a language for describing processes and for constructing computational data objects and processes to model complex phenomena in the real world.  However, as we confront increasingly complex problems, we will find that Lisp, or indeed any fixed programming language, is not sufficient for our needs.  We must constantly turn to new languages in order to express our ideas more effectively.  Establishing new languages is a powerful strategy for controlling complexity in engineering design; we can often enhance our ability to deal with a complex problem by adopting a new language that enables us to describe (and hence to think about) the problem in a different way, using primitives, means of combination, and means of abstraction that are particularly well suited to the problem at hand.[fn:205]

Programming is endowed with a multitude of languages.  There are physical languages, such as the machine languages for particular computers.  These languages are concerned with the representation of data and control in terms of individual bits of storage and primitive machine instructions.  The machine-language programmer is concerned with using the given hardware to erect systems and utilities for the efficient implementation of resource-limited computations.  High-level languages, erected on a machine-language substrate, hide concerns about the representation of data as collections of bits and the representation of programs as sequences of primitive instructions.  These languages have means of combination and abstraction, such as procedure definition, that are appropriate to the larger-scale organization of systems.

<<i237>> Metalinguistic abstraction--establishing new languages--plays an important role in all branches of engineering design.  It is particularly important to computer programming, because in programming not only can we formulate new languages but we can also implement these languages by constructing evaluators.  An <<i129>> evaluator (or <<i195>> interpreter) for a programming language is a procedure that, when applied to an expression of the language, performs the actions required to evaluate that expression.

It is no exaggeration to regard this as the most fundamental idea in programming:

#+begin_quote
The evaluator, which determines the meaning of expressions in a programming language, is just another program.
#+end_quote

To appreciate this point is to change our images of ourselves as programmers.  We come to see ourselves as designers of languages, rather than only users of languages designed by others.

In fact, we can regard almost any program as the evaluator for some language.  For instance, the polynomial manipulation system of section [[#section-2.5.3][2.5.3]] embodies the rules of polynomial arithmetic and implements them in terms of operations on list-structured data.  If we augment this system with procedures to read and print polynomial expressions, we have the core of a special-purpose language for dealing with problems in symbolic mathematics.  The digital-logic simulator of section [[#section-3.3.4][3.3.4]] and the constraint propagator of section [[#section-3.3.5][3.3.5]] are legitimate languages in their own right, each with its own primitives, means of combination, and means of abstraction.  Seen from this perspective, the technology for coping with large-scale computer systems merges with the technology for building new computer languages, and computer science itself becomes no more (and no less) than the discipline of constructing appropriate descriptive languages.

We now embark on a tour of the technology by which languages are established in terms of other languages.  In this chapter we shall use Lisp as a base, implementing evaluators as Lisp procedures.  Lisp is particularly well suited to this task, because of its ability to represent and manipulate symbolic expressions.  We will take the first step in understanding how languages are implemented by building an evaluator for Lisp itself.  The language implemented by our evaluator will be a subset of the Scheme dialect of Lisp that we use in this book.  Although the evaluator described in this chapter is written for a particular dialect of Lisp, it contains the essential structure of an evaluator for any expression-oriented language designed for writing programs for a sequential machine.  (In fact, most language processors contain, deep within them, a little "Lisp" evaluator.)  The evaluator has been simplified for the purposes of illustration and discussion, and some features have been left out that would be important to include in a production-quality Lisp system.  Nevertheless, this simple evaluator is adequate to execute most of the programs in this book.[fn:206]

An important advantage of making the evaluator accessible as a Lisp program is that we can implement alternative evaluation rules by describing these as modifications to the evaluator program.  One place where we can use this power to good effect is to gain extra control over the ways in which computational models embody the notion of time, which was so central to the discussion in [[#section-3][Chapter 3]].  There, we mitigated some of the complexities of state and assignment by using streams to decouple the representation of time in the world from time in the computer.  Our stream programs, however, were sometimes cumbersome, because they were constrained by the applicative-order evaluation of Scheme.  In section [[#section-4.2][4.2]], we'll change the underlying language to provide for a more elegant approach, by modifying the evaluator to provide for <<i264>> normal-order evaluation.

Section [[#section-4.3][4.3]] implements a more ambitious linguistic change, whereby expressions have many values, rather than just a single value.  In this language of <<i260>> nondeterministic computing, it is natural to express processes that generate all possible values for expressions and then search for those values that satisfy certain constraints.  In terms of models of computation and time, this is like having time branch into a set of "possible futures" and then searching for appropriate time lines.  With our nondeterministic evaluator, keeping track of multiple values and performing searches are handled automatically by the underlying mechanism of the language.

In section [[#section-4.4][4.4]] we implement a <<i219>> logic-programming language in which knowledge is expressed in terms of relations, rather than in terms of computations with inputs and outputs.  Even though this makes the language drastically different from Lisp, or indeed from any conventional language, we will see that the logic-programming evaluator shares the essential structure of the Lisp evaluator.

** 4.1 The Metacircular Evaluator
:properties:
:custom_id: section-4.1
:end:

Our evaluator for Lisp will be implemented as a Lisp program.  It may seem circular to think about evaluating Lisp programs using an evaluator that is itself implemented in Lisp.  However, evaluation is a process, so it is appropriate to describe the evaluation process using Lisp, which, after all, is our tool for describing processes.[fn:207] An evaluator that is written in the same language that it evaluates is said to be <<i236>> metacircular.

The metacircular evaluator is essentially a Scheme formulation of the environment model of evaluation described in section [[#section-3.2][3.2]].  Recall that the model has two basic parts:

1. To evaluate a combination (a compound expression other than a special form), evaluate the subexpressions and then apply the value of the operator subexpression to the values of the operand subexpressions.

2. To apply a compound procedure to a set of arguments, evaluate the body of the procedure in a new environment.  To construct this environment, extend the environment part of the procedure object by a frame in which the formal parameters of the procedure are bound to the arguments to which the procedure is applied.

These two rules describe the essence of the evaluation process, a basic cycle in which expressions to be evaluated in environments are reduced to procedures to be applied to arguments, which in turn are reduced to new expressions to be evaluated in new environments, and so on, until we get down to symbols, whose values are looked up in the environment, and to primitive procedures, which are applied directly (see [[figure-4.1][Figure 4.1]]).[fn:208] This evaluation cycle will be embodied by the interplay between the two critical procedures in the evaluator, ~eval~ and ~apply~, which are described in section [[#section-4.1.1][4.1.1]] (see [[figure-4.1][Figure 4.1]]).

The implementation of the evaluator will depend upon procedures that define the <<i383>> syntax of the expressions to be evaluated.  We will use data abstraction to make the evaluator independent of the representation of the language.  For example, rather than committing to a choice that an assignment is to be represented by a list beginning with the symbol ~set!~ we use an abstract predicate ~assignment?~ to test for an assignment, and we use abstract selectors ~assignment-variable~ and ~assignment-value~ to access the parts of an assignment.  Implementation of expressions will be described in detail in section [[#section-4.1.2][4.1.2]].  There are also operations, described in section [[#section-4.1.3][4.1.3]], that specify the representation of procedures and environments.  For example, ~make-procedure~ constructs compound procedures, ~lookup-variable-value~ accesses the values of variables, and ~apply-primitive-procedure~ applies a primitive procedure to a given list of arguments.

*** 4.1.1 The Core of the Evaluator
:properties:
:custom_id: section-4.1.1
:end:

<<figure-4.1>> The ~eval~-~apply~ cycle exposes the essence of a computer language.

#+begin_example
                            .,ad88888888baa,
                   _    ,d8P"""        ""9888ba.      _
                  /  .a8"          ,ad88888888888a   |\
                /   aP'          ,88888888888888888a   \
               /  ,8"           ,88888888888888888888,  \
              |  ,8'            (888888888888888888888, |
             /  ,8'             '8888888888888888888888  \
             |  8)               '888888888888888888888, |
 Procedure,  |  8                  "88888 Apply 8888888) | Expression
 Arguments   |  8     Eval          '888888888888888888) | Environment
             |  8)                    "8888888888888888  |
             \  (b                     "88888888888888'  /
              | '8,                     8888888888888)  |
              \  "8a                   ,888888888888)  /
               \   V8,                 d88888888888"  /
               _\| '8b,             ,d8888888888P' _/
                      'V8a,       ,ad8888888888P'
                         ""88888888888888888P"
                              """"""""""""

                                [graphic by Normand Veillux, modified]
#+end_example

The evaluation process can be described as the interplay between two procedures: ~eval~ and ~apply~.

*Eval*

~eval~ takes as arguments an expression and an environment.  It classifies the expression and directs its evaluation.  ~eval~ is structured as a case analysis of the syntactic type of the expression to be evaluated.  In order to keep the procedure general, we express the determination of the type of an expression abstractly, making no commitment to any particular representation for the various types of expressions.  Each type of expression has a predicate that tests for it and an abstract means for selecting its parts.  This <<i2>> abstract syntax makes it easy to see how we can change the syntax of the language by using the same evaluator, but with a different collection of syntax procedures.

Primitive expressions

- For self-evaluating expressions, such as numbers, ~eval~ returns the expression itself.

- ~Eval~ must look up variables in the environment to find their values.

Special forms

- For quoted expressions, ~eval~ returns the expression that was quoted.

- An assignment to (or a definition of) a variable must recursively call ~eval~ to compute the new value to be associated with the variable.  The environment must be modified to change (or create) the binding of the variable.

- An ~if~ expression requires special processing of its parts, so as to evaluate the consequent if the predicate is true, and otherwise to evaluate the alternative.

- A ~lambda~ expression must be transformed into an applicable procedure by packaging together the parameters and body specified by the ~lambda~ expression with the environment of the evaluation.

- A ~begin~ expression requires evaluating its sequence of expressions in the order in which they appear.

- A case analysis (~cond~) is transformed into a nest of ~if~ expressions and then evaluated.

Combinations

- For a procedure application, ~eval~ must recursively evaluate the operator part and the operands of the combination.  The resulting procedure and arguments are passed to ~apply~, which handles the actual procedure application.

Here is the definition of ~eval~:

#+begin_src scheme
(define (eval exp env)
  (cond ((self-evaluating? exp) exp)
        ((variable? exp) (lookup-variable-value exp env))
        ((quoted? exp) (text-of-quotation exp))
        ((assignment? exp) (eval-assignment exp env))
        ((definition? exp) (eval-definition exp env))
        ((if? exp) (eval-if exp env))
        ((lambda? exp)
         (make-procedure (lambda-parameters exp)
                         (lambda-body exp)
                         env))
        ((begin? exp)
         (eval-sequence (begin-actions exp) env))
        ((cond? exp) (eval (cond->if exp) env))
        ((application? exp)
         (apply (eval (operator exp) env)
                (list-of-values (operands exp) env)))
        (else
         (error "Unknown expression type - EVAL" exp))))
#+end_src

For clarity, ~eval~ has been implemented as a case analysis using ~cond~.  The disadvantage of this is that our procedure handles only a few distinguishable types of expressions, and no new ones can be defined without editing the definition of ~eval~.  In most Lisp implementations, dispatching on the type of an expression is done in a data-directed style.  This allows a user to add new types of expressions that ~eval~ can distinguish, without modifying the definition of ~eval~ itself.  (See [[#exercise-4.3][Exercise 4.3]].)

*Apply*

~apply~ takes two arguments, a procedure and a list of arguments to which the procedure should be applied.  ~apply~ classifies procedures into two kinds: It calls ~apply-primitive-procedure~ to apply primitives; it applies compound procedures by sequentially evaluating the expressions that make up the body of the procedure.  The environment for the evaluation of the body of a compound procedure is constructed by extending the base environment carried by the procedure to include a frame that binds the parameters of the procedure to the arguments to which the procedure is to be applied.  Here is the definition of ~apply~:

#+begin_src scheme
(define (apply procedure arguments)
  (cond ((primitive-procedure? procedure)
         (apply-primitive-procedure procedure arguments))
        ((compound-procedure? procedure)
         (eval-sequence
          (procedure-body procedure)
          (extend-environment
           (procedure-parameters procedure)
           arguments
           (procedure-environment procedure))))
        (else
         (error
          "Unknown procedure type - APPLY" procedure))))
#+end_src

*Procedure arguments*

When ~eval~ processes a procedure application, it uses ~list-of-values~ to produce the list of arguments to which the procedure is to be applied.  ~list-of-values~ takes as an argument the operands of the combination.  It evaluates each operand and returns a list of the corresponding values:[fn:209]

#+begin_src scheme
(define (list-of-values exps env)
  (if (no-operands? exps)
      '()
      (cons (eval (first-operand exps) env)
            (list-of-values (rest-operands exps) env))))
#+end_src

*Conditionals*

~eval-if~ evaluates the predicate part of an ~if~ expression in the given environment.  If the result is true, ~eval-if~ evaluates the consequent, otherwise it evaluates the alternative:

#+begin_src scheme
(define (eval-if exp env)
  (if (true? (eval (if-predicate exp) env))
      (eval (if-consequent exp) env)
      (eval (if-alternative exp) env)))
#+end_src

The use of ~true?~ in ~eval-if~ highlights the issue of the connection between an implemented language and an implementation language.  The ~if-predicate~ is evaluated in the language being implemented and thus yields a value in that language.  The interpreter predicate ~true?~ translates that value into a value that can be tested by the ~if~ in the implementation language: The metacircular representation of truth might not be the same as that of the underlying Scheme.[fn:210]

*Sequences*

~eval-sequence~ is used by ~apply~ to evaluate the sequence of expressions in a procedure body and by ~eval~ to evaluate the sequence of expressions in a ~begin~ expression.  It takes as arguments a sequence of expressions and an environment, and evaluates the expressions in the order in which they occur.  The value returned is the value of the final expression.

#+begin_src scheme
(define (eval-sequence exps env)
  (cond ((last-exp? exps) (eval (first-exp exps) env))
        (else (eval (first-exp exps) env)
              (eval-sequence (rest-exps exps) env))))
#+end_src

*Assignments and definitions*

The following procedure handles assignments to variables.  It calls ~eval~ to find the value to be assigned and transmits the variable and the resulting value to ~set-variable-value!~ to be installed in the designated environment.

#+begin_src scheme
(define (eval-assignment exp env)
  (set-variable-value! (assignment-variable exp)
                       (eval (assignment-value exp) env)
                       env)
  'ok)
#+end_src

Definitions of variables are handled in a similar manner.[fn:211]

#+begin_src scheme
(define (eval-definition exp env)
  (define-variable! (definition-variable exp)
    (eval (definition-value exp) env)
    env)
  'ok)
#+end_src

We have chosen here to return the symbol ~ok~ as the value of an assignment or a definition.[fn:212]

**** Exercise 4.1
:properties:
:custom_id: exercise-4.1
:end:

Notice that we cannot tell whether the metacircular evaluator evaluates operands from left to right or from right to left.  Its evaluation order is inherited from the underlying Lisp: If the arguments to ~cons~ in ~list-of-values~ are evaluated from left to right, then ~list-of-values~ will evaluate operands from left to right; and if the arguments to ~cons~ are evaluated from right to left, then ~list-of-values~ will evaluate operands from right to left.

Write a version of ~list-of-values~ that evaluates operands from left to right regardless of the order of evaluation in the underlying Lisp.  Also write a version of ~list-of-values~ that evaluates operands from right to left.

*** 4.1.2 Representing Expressions
:properties:
:custom_id: section-4.1.2
:end:

The evaluator is reminiscent of the symbolic differentiation program discussed in section [[#section-2.3.2][2.3.2]].  Both programs operate on symbolic expressions.  In both programs, the result of operating on a compound expression is determined by operating recursively on the pieces of the expression and combining the results in a way that depends on the type of the expression.  In both programs we used data abstraction to decouple the general rules of operation from the details of how expressions are represented.  In the differentiation program this meant that the same differentiation procedure could deal with algebraic expressions in prefix form, in infix form, or in some other form.  For the evaluator, this means that the syntax of the language being evaluated is determined solely by the procedures that classify and extract pieces of expressions.

Here is the specification of the syntax of our language:

1. The only self-evaluating items are numbers and strings:

#+begin_src scheme
(define (self-evaluating? exp)
  (cond ((number? exp) true)
        ((string? exp) true)
        (else false)))
#+end_src

2. Variables are represented by symbols:

#+begin_src scheme
(define (variable? exp) (symbol? exp))
#+end_src

3. Quotations have the form '(quote <TEXT-OF-QUOTATION>)':[fn:213]

#+begin_src scheme
(define (quoted? exp)
  (tagged-list? exp 'quote))

(define (text-of-quotation exp) (cadr exp))
#+end_src

~quoted?~ is defined in terms of the procedure ~tagged-list?~, which identifies lists beginning with a designated symbol:

#+begin_src scheme
(define (tagged-list? exp tag)
  (if (pair? exp)
      (eq? (car exp) tag)
      false))
#+end_src

4. Assignments have the form '(set! <VAR> <VALUE>)':

#+begin_src scheme
(define (assignment? exp)
  (tagged-list? exp 'set!))

(define (assignment-variable exp) (cadr exp))

(define (assignment-value exp) (caddr exp))
#+end_src

5. Definitions have the form

#+begin_src scheme
(define <VAR> <VALUE>)
#+end_src

or the form

#+begin_src scheme
(define (<VAR> <PARAMETER_1> ... <PARAMETER_N>)
  <BODY>)
#+end_src

The latter form (standard procedure definition) is syntactic sugar for

#+begin_src scheme
(define <VAR>
  (lambda (<PARAMETER_1> ... <PARAMETER_N>)
    <BODY>))
#+end_src

The corresponding syntax procedures are the following:

#+begin_src scheme
(define (definition? exp)
  (tagged-list? exp 'define))

(define (definition-variable exp)
  (if (symbol? (cadr exp))
      (cadr exp)
      (caadr exp)))

(define (definition-value exp)
  (if (symbol? (cadr exp))
      (caddr exp)
      (make-lambda (cdadr exp)          ; formal parameters
                   (cddr exp)))) ; body
#+end_src

6. ~lambda~ expressions are lists that begin with the symbol ~lambda~:

#+begin_src scheme
(define (lambda? exp) (tagged-list? exp 'lambda))

(define (lambda-parameters exp) (cadr exp))

(define (lambda-body exp) (cddr exp))
#+end_src

We also provide a constructor for ~lambda~ expressions, which is used by ~definition-value~, above:

#+begin_src scheme
(define (make-lambda parameters body)
  (cons 'lambda (cons parameters body)))
#+end_src

7. Conditionals begin with ~if~ and have a predicate, a consequent, and an (optional) alternative.  If the expression has no alternative part, we provide ~false~ as the alternative.[fn:214]

#+begin_src scheme
(define (if? exp) (tagged-list? exp 'if))

(define (if-predicate exp) (cadr exp))

(define (if-consequent exp) (caddr exp))

(define (if-alternative exp)
  (if (not (null? (cdddr exp)))
      (cadddr exp)
      'false))
#+end_src

We also provide a constructor for ~if~ expressions, to be used by ~cond->if~ to transform ~cond~ expressions into ~if~ expressions:

#+begin_src scheme
(define (make-if predicate consequent alternative)
  (list 'if predicate consequent alternative))
#+end_src

8. ~begin~ packages a sequence of expressions into a single expression. We include syntax operations on ~begin~ expressions to extract the actual sequence from the ~begin~ expression, as well as selectors that return the first expression and the rest of the expressions in the sequence.[fn:215]

#+begin_src scheme
(define (begin? exp) (tagged-list? exp 'begin))

(define (begin-actions exp) (cdr exp))

(define (last-exp? seq) (null? (cdr seq)))

(define (first-exp seq) (car seq))

(define (rest-exps seq) (cdr seq))
#+end_src

We also include a constructor ~sequence->exp~ (for use by ~cond->if~) that transforms a sequence into a single expression, using ~begin~ if necessary:

#+begin_src scheme
(define (sequence->exp seq)
  (cond ((null? seq) seq)
        ((last-exp? seq) (first-exp seq))
        (else (make-begin seq))))

(define (make-begin seq) (cons 'begin seq))
#+end_src

9. A procedure application is any compound expression that is not one of the above expression types.  The ~car~ of the expression is the operator, and the ~cdr~ is the list of operands:

#+begin_src scheme
(define (application? exp) (pair? exp))

(define (operator exp) (car exp))

(define (operands exp) (cdr exp))

(define (no-operands? ops) (null? ops))

(define (first-operand ops) (car ops))

(define (rest-operands ops) (cdr ops))
#+end_src

*Derived expressions*

Some special forms in our language can be defined in terms of expressions involving other special forms, rather than being implemented directly.  One example is ~cond~, which can be implemented as a nest of ~if~ expressions.  For example, we can reduce the problem of evaluating the expression

#+begin_src scheme
(cond ((> x 0) x)
      ((= x 0) (display 'zero) 0)
      (else (- x)))
#+end_src

to the problem of evaluating the following expression involving ~if~ and ~begin~ expressions:

#+begin_src scheme
(if (> x 0)
    x
    (if (= x 0)
        (begin (display 'zero)
               0)
        (- x)))
#+end_src

Implementing the evaluation of ~cond~ in this way simplifies the evaluator because it reduces the number of special forms for which the evaluation process must be explicitly specified.

We include syntax procedures that extract the parts of a ~cond~ expression, and a procedure ~cond->if~ that transforms ~cond~ expressions into ~if~ expressions.  A case analysis begins with ~cond~ and has a list of predicate-action clauses.  A clause is an ~else~ clause if its predicate is the symbol ~else~.[fn:216]

#+begin_src scheme
(define (cond? exp) (tagged-list? exp 'cond))

(define (cond-clauses exp) (cdr exp))

(define (cond-else-clause? clause)
  (eq? (cond-predicate clause) 'else))

(define (cond-predicate clause) (car clause))

(define (cond-actions clause) (cdr clause))

(define (cond->if exp)
  (expand-clauses (cond-clauses exp)))

(define (expand-clauses clauses)
  (if (null? clauses)
      'false                            ; no ~else~ clause
      (let ((first (car clauses))
            (rest (cdr clauses)))
        (if (cond-else-clause? first)
            (if (null? rest)
                (sequence->exp (cond-actions first))
                (error "ELSE clause isn't last - COND->IF"
                       clauses))
            (make-if (cond-predicate first)
                     (sequence->exp (cond-actions first))
                     (expand-clauses rest))))))
#+end_src

Expressions (such as ~cond~) that we choose to implement as syntactic transformations are called <<i112>> derived expressions.  ~let~ expressions are also derived expressions (see [[#exercise-4.6][Exercise 4.6]]).[fn:217]

**** Exercise 4.2
:properties:
:custom_id: exercise-4.2
:end:

Louis Reasoner plans to reorder the ~cond~ clauses in ~eval~ so that the clause for procedure applications appears before the clause for assignments.  He argues that this will make the interpreter more efficient: Since programs usually contain more applications than assignments, definitions, and so on, his modified ~eval~ will usually check fewer clauses than the original ~eval~ before identifying the type of an expression.

a. What is wrong with Louis's plan?  (Hint: What will Louis's evaluator do with the expression ~(define x 3)~?)

b. Louis is upset that his plan didn't work.  He is willing to go to any lengths to make his evaluator recognize procedure applications before it checks for most other kinds of expressions.  Help him by changing the syntax of the evaluated language so that procedure applications start with ~call~.  For example, instead of ~(factorial 3)~ we will now have to write ~(call factorial 3)~ and instead of ~(+ 1 2)~ we will have to write ~(call
   + 1 2)~.

**** Exercise 4.3
:properties:
:custom_id: exercise-4.3
:end:

Rewrite ~eval~ so that the dispatch is done in data-directed style.  Compare this with the data-directed differentiation procedure of [[#exercise-2.73][Exercise 2.73]].  (You may use the ~car~ of a compound expression as the type of the expression, as is appropriate for the syntax implemented in this section.)

**** Exercise 4.4
:properties:
:custom_id: exercise-4.4
:end:

Recall the definitions of the special forms ~and~ and ~or~ from [[#section-1][Chapter 1]]:

- ~and~: The expressions are evaluated from left to right.  If any expression evaluates to false, false is returned; any remaining expressions are not evaluated.  If all the expressions evaluate to true values, the value of the last expression is returned.  If there are no expressions then true is returned.

- ~or~: The expressions are evaluated from left to right.  If any expression evaluates to a true value, that value is returned; any remaining expressions are not evaluated.  If all expressions evaluate to false, or if there are no expressions, then false is returned.

Install ~and~ and ~or~ as new special forms for the evaluator by defining appropriate syntax procedures and evaluation procedures ~eval-and~ and ~eval-or~.  Alternatively, show how to implement ~and~ and ~or~ as derived expressions.

**** Exercise 4.5
:properties:
:custom_id: exercise-4.5
:end:

Scheme allows an additional syntax for ~cond~ clauses, '(<TEST> => <RECIPIENT>)'.  If <TEST> evaluates to a true value, then <RECIPIENT> is evaluated.  Its value must be a procedure of one argument; this procedure is then invoked on the value of the <TEST>, and the result is returned as the value of the ~cond~ expression.  For example

#+begin_src scheme
(cond ((assoc 'b '((a 1) (b 2))) => cadr)
      (else false))
#+end_src

returns 2.  Modify the handling of ~cond~ so that it supports this extended syntax.

**** Exercise 4.6
:properties:
:custom_id: exercise-4.6
:end:

~let~ expressions are derived expressions, because

#+begin_src scheme
(let ((<VAR_1> <EXP_1>) ... (<VAR_N> <EXP_N>))
  <BODY>)
#+end_src

is equivalent to

#+begin_src scheme
((lambda (<VAR_1> ... <VAR_N>)
   <BODY>)
 <EXP_1>
 ...
 <EXP_N>)
#+end_src

Implement a syntactic transformation ~let->combination~ that reduces evaluating ~let~ expressions to evaluating combinations of the type shown above, and add the appropriate clause to ~eval~ to handle ~let~ expressions.

**** Exercise 4.7
:properties:
:custom_id: exercise-4.7
:end:

~let*~ is similar to ~let~, except that the bindings of the ~let~ variables are performed sequentially from left to right, and each binding is made in an environment in which all of the preceding bindings are visible.  For example

#+begin_src scheme
(let* ((x 3)
       (y (+ x 2))
       (z (+ x y 5)))
  (* x z))
#+end_src

returns 39.  Explain how a ~let*~ expression can be rewritten as a set of nested ~let~ expressions, and write a procedure ~let*->nested-lets~ that performs this transformation.  If we have already implemented ~let~ ([[#exercise-4.6][Exercise 4.6]]) and we want to extend the evaluator to handle ~let*~, is it sufficient to add a clause to ~eval~ whose action is

#+begin_src scheme
(eval (let*->nested-lets exp) env)
#+end_src

or must we explicitly expand ~let*~ in terms of non-derived expressions?

**** Exercise 4.8
:properties:
:custom_id: exercise-4.8
:end:

"Named ~let~" is a variant of ~let~ that has the form

#+begin_src scheme
(let <VAR> <BINDINGS> <BODY>)
#+end_src

The ~<BINDINGS>~ and ~<BODY>~ are just as in ordinary ~let~, except that ~<VAR>~ is bound within ~<BODY>~ to a procedure whose body is ~<BODY>~ and whose parameters are the variables in the ~<BINDINGS>~.  Thus, one can repeatedly execute the ~<BODY>~ by invoking the procedure named ~<VAR>~.  For example, the iterative Fibonacci procedure (section [[#section-1.2.2][1.2.2]]) can be rewritten using named ~let~ as follows:

#+begin_src scheme
(define (fib n)
  (let fib-iter ((a 1)
                 (b 0)
                 (count n))
    (if (= count 0)
        b
        (fib-iter (+ a b) a (- count 1)))))
#+end_src

Modify ~let->combination~ of [[#exercise-4.6][Exercise 4.6]] to also support named ~let~.

**** Exercise 4.9
:properties:
:custom_id: exercise-4.9
:end:

Many languages support a variety of iteration constructs, such as ~do~, ~for~, ~while~, and ~until~.  In Scheme, iterative processes can be expressed in terms of ordinary procedure calls, so special iteration constructs provide no essential gain in computational power.  On the other hand, such constructs are often convenient.  Design some iteration constructs, give examples of their use, and show how to implement them as derived expressions.

**** Exercise 4.10
:properties:
:custom_id: exercise-4.10
:end:

By using data abstraction, we were able to write an ~eval~ procedure that is independent of the particular syntax of the language to be evaluated.  To illustrate this, design and implement a new syntax for Scheme by modifying the procedures in this section, without changing ~eval~ or ~apply~.

*** 4.1.3 Evaluator Data Structures
:properties:
:custom_id: section-4.1.3
:end:

In addition to defining the external syntax of expressions, the evaluator implementation must also define the data structures that the evaluator manipulates internally, as part of the execution of a program, such as the representation of procedures and environments and the representation of true and false.

*Testing of predicates*

For conditionals, we accept anything to be true that is not the explicit ~false~ object.

#+begin_src scheme
(define (true? x)
  (not (eq? x false)))

(define (false? x)
  (eq? x false))
#+end_src

*Representing procedures*

To handle primitives, we assume that we have available the following procedures:

- ~(apply-primitive-procedure <PROC> <ARGS>)~

  applies the given primitive procedure to the argument values in the list <ARGS> and returns the result of the application.

- ~(primitive-procedure? <PROC>)~

tests whether ~<PROC>~ is a primitive procedure.

These mechanisms for handling primitives are further described in section [[#section-4.1.4][4.1.4]].

Compound procedures are constructed from parameters, procedure bodies, and environments using the constructor ~make-procedure~:

#+begin_src scheme
(define (make-procedure parameters body env)
  (list 'procedure parameters body env))

(define (compound-procedure? p)
  (tagged-list? p 'procedure))

(define (procedure-parameters p) (cadr p))

(define (procedure-body p) (caddr p))

(define (procedure-environment p) (cadddr p))
#+end_src

*Operations on Environments*

The evaluator needs operations for manipulating environments.  As explained in section [[#section-3.2][3.2]], an environment is a sequence of frames, where each frame is a table of bindings that associate variables with their corresponding values.  We use the following operations for manipulating environments:

- ~(lookup-variable-value <VAR> <ENV>)~

returns the value that is bound to the symbol ~<VAR>~ in the environment ~<ENV>~, or signals an error if the variable is unbound.

- ~(extend-environment <VARIABLES> <VALUES> <BASE-ENV>)~

returns a new environment, consisting of a new frame in which the symbols in the list ~<VARIABLES>~ are bound to the corresponding elements in the list ~<VALUES>~, where the enclosing environment is the environment ~<BASE-ENV>~.

- ~(define-variable! <VAR> <VALUE> <ENV>)~

adds to the first frame in the environment ~<ENV>~ a new binding that associates the variable ~<VAR>~ with the value ~<VALUE>~.

- ~(set-variable-value! <VAR> <VALUE> <ENV>)~

changes the binding of the variable ~<VAR>~ in the environment ~<ENV>~ so that the variable is now bound to the value ~<VALUE>~, or signals an error if the variable is unbound.

To implement these operations we represent an environment as a list of frames.  The enclosing environment of an environment is the ~cdr~ of the list.  The empty environment is simply the empty list.

#+begin_src scheme
(define (enclosing-environment env) (cdr env))

(define (first-frame env) (car env))

(define the-empty-environment '())
#+end_src

Each frame of an environment is represented as a pair of lists: a list of the variables bound in that frame and a list of the associated values.[fn:218]

#+begin_src scheme
(define (make-frame variables values)
  (cons variables values))

(define (frame-variables frame) (car frame))

(define (frame-values frame) (cdr frame))

(define (add-binding-to-frame! var val frame)
  (set-car! frame (cons var (car frame)))
  (set-cdr! frame (cons val (cdr frame))))
#+end_src

To extend an environment by a new frame that associates variables with values, we make a frame consisting of the list of variables and the list of values, and we adjoin this to the environment.  We signal an error if the number of variables does not match the number of values.

#+begin_src scheme
(define (extend-environment vars vals base-env)
  (if (= (length vars) (length vals))
      (cons (make-frame vars vals) base-env)
      (if (< (length vars) (length vals))
          (error "Too many arguments supplied" vars vals)
          (error "Too few arguments supplied" vars vals))))
#+end_src

To look up a variable in an environment, we scan the list of variables in the first frame.  If we find the desired variable, we return the corresponding element in the list of values.  If we do not find the variable in the current frame, we search the enclosing environment, and so on.  If we reach the empty environment, we signal an "unbound variable" error.

#+begin_src scheme
(define (lookup-variable-value var env)
  (define (env-loop env)
    (define (scan vars vals)
      (cond ((null? vars)
             (env-loop (enclosing-environment env)))
            ((eq? var (car vars))
             (car vals))
            (else (scan (cdr vars) (cdr vals)))))
    (if (eq? env the-empty-environment)
        (error "Unbound variable" var)
        (let ((frame (first-frame env)))
          (scan (frame-variables frame)
                (frame-values frame)))))
  (env-loop env))
#+end_src

To set a variable to a new value in a specified environment, we scan for the variable, just as in ~lookup-variable-value~, and change the corresponding value when we find it.

#+begin_src scheme
(define (set-variable-value! var val env)
  (define (env-loop env)
    (define (scan vars vals)
      (cond ((null? vars)
             (env-loop (enclosing-environment env)))
            ((eq? var (car vars))
             (set-car! vals val))
            (else (scan (cdr vars) (cdr vals)))))
    (if (eq? env the-empty-environment)
        (error "Unbound variable - SET!" var)
        (let ((frame (first-frame env)))
          (scan (frame-variables frame)
                (frame-values frame)))))
  (env-loop env))
#+end_src

To define a variable, we search the first frame for a binding for the variable, and change the binding if it exists (just as in ~set-variable-value!~).  If no such binding exists, we adjoin one to the first frame.

#+begin_src scheme
(define (define-variable! var val env)
  (let ((frame (first-frame env)))
    (define (scan vars vals)
      (cond ((null? vars)
             (add-binding-to-frame! var val frame))
            ((eq? var (car vars))
             (set-car! vals val))
            (else (scan (cdr vars) (cdr vals)))))
    (scan (frame-variables frame)
          (frame-values frame))))
#+end_src

The method described here is only one of many plausible ways to represent environments.  Since we used data abstraction to isolate the rest of the evaluator from the detailed choice of representation, we could change the environment representation if we wanted to.  (See [[#exercise-4.11][Exercise 4.11]].)  In a production-quality Lisp system, the speed of the evaluator's environment operations--especially that of variable lookup--has a major impact on the performance of the system.  The representation described here, although conceptually simple, is not efficient and would not ordinarily be used in a production system.[fn:219]

**** Exercise 4.11
:properties:
:custom_id: exercise-4.11
:end:

Instead of representing a frame as a pair of lists, we can represent a frame as a list of bindings, where each binding is a name-value pair.  Rewrite the environment operations to use this alternative representation.

**** Exercise 4.12
:properties:
:custom_id: exercise-4.12
:end:

The procedures ~set-variable-value!~, ~define-variable!~, and ~lookup-variable-value~ can be expressed in terms of more abstract procedures for traversing the environment structure.  Define abstractions that capture the common patterns and redefine the three procedures in terms of these abstractions.

**** Exercise 4.13
:properties:
:custom_id: exercise-4.13
:end:

Scheme allows us to create new bindings for variables by means of ~define~, but provides no way to get rid of bindings.  Implement for the evaluator a special form ~make-unbound!~ that removes the binding of a given symbol from the environment in which the ~make-unbound!~ expression is evaluated.  This problem is not completely specified.  For example, should we remove only the binding in the first frame of the environment?  Complete the specification and justify any choices you make.

*** 4.1.4 Running the Evaluator as a Program
:properties:
:custom_id: section-4.1.4
:end:

Given the evaluator, we have in our hands a description (expressed in Lisp) of the process by which Lisp expressions are evaluated.  One advantage of expressing the evaluator as a program is that we can run the program.  This gives us, running within Lisp, a working model of how Lisp itself evaluates expressions.  This can serve as a framework for experimenting with evaluation rules, as we shall do later in this chapter.

Our evaluator program reduces expressions ultimately to the application of primitive procedures.  Therefore, all that we need to run the evaluator is to create a mechanism that calls on the underlying Lisp system to model the application of primitive procedures.

There must be a binding for each primitive procedure name, so that when ~eval~ evaluates the operator of an application of a primitive, it will find an object to pass to ~apply~.  We thus set up a global environment that associates unique objects with the names of the primitive procedures that can appear in the expressions we will be evaluating.  The global environment also includes bindings for the symbols ~true~ and ~false~, so that they can be used as variables in expressions to be evaluated.

#+begin_src scheme
(define (setup-environment)
  (let ((initial-env
         (extend-environment (primitive-procedure-names)
                             (primitive-procedure-objects)
                             the-empty-environment)))
    (define-variable! 'true true initial-env)
    (define-variable! 'false false initial-env)
    initial-env))

(define the-global-environment (setup-environment))
#+end_src

It does not matter how we represent the primitive procedure objects, so long as ~apply~ can identify and apply them by using the procedures ~primitive-procedure?~ and ~apply-primitive-procedure~.  We have chosen to represent a primitive procedure as a list beginning with the symbol ~primitive~ and containing a procedure in the underlying Lisp that implements that primitive.

#+begin_src scheme
(define (primitive-procedure? proc)
  (tagged-list? proc 'primitive))

(define (primitive-implementation proc) (cadr proc))
#+end_src

~setup-environment~ will get the primitive names and implementation procedures from a list:[fn:220]

#+begin_src scheme
(define primitive-procedures
  (list (list 'car car)
        (list 'cdr cdr)
        (list 'cons cons)
        (list 'null? null?)
        <MORE PRIMITIVES>
        ))

(define (primitive-procedure-names)
  (map car
       primitive-procedures))

(define (primitive-procedure-objects)
  (map (lambda (proc) (list 'primitive (cadr proc)))
       primitive-procedures))
#+end_src

To apply a primitive procedure, we simply apply the implementation procedure to the arguments, using the underlying Lisp system:[fn:221]

#+begin_src scheme
(define (apply-primitive-procedure proc args)
  (apply-in-underlying-scheme
   (primitive-implementation proc) args))
#+end_src

For convenience in running the metacircular evaluator, we provide a <<i117>> driver loop that models the read-eval-print loop of the underlying Lisp system.  It prints a <<i307>> prompt, reads an input expression, evaluates this expression in the global environment, and prints the result.  We precede each printed result by an <<i275>> output prompt so as to distinguish the value of the expression from other output that may be printed.[fn:222]

#+begin_src scheme
(define input-prompt ";;; M-Eval input:")
(define output-prompt ";;; M-Eval value:")

(define (driver-loop)
  (prompt-for-input input-prompt)
  (let ((input (read)))
    (let ((output (eval input the-global-environment)))
      (announce-output output-prompt)
      (user-print output)))
  (driver-loop))

(define (prompt-for-input string)
  (newline) (newline) (display string) (newline))

(define (announce-output string)
  (newline) (display string) (newline))
#+end_src

We use a special printing procedure, ~user-print~, to avoid printing the environment part of a compound procedure, which may be a very long list (or may even contain cycles).

#+begin_src scheme
(define (user-print object)
  (if (compound-procedure? object)
      (display (list 'compound-procedure
                     (procedure-parameters object)
                     (procedure-body object)
                     '<procedure-env>))
      (display object)))
#+end_src

Now all we need to do to run the evaluator is to initialize the global environment and start the driver loop.  Here is a sample interaction:

#+begin_src scheme
(define the-global-environment (setup-environment))

(driver-loop)

;;; M-Eval input:
(define (append x y)
  (if (null? x)
      y
      (cons (car x)
            (append (cdr x) y))))
;;; M-Eval value:
ok

;;; M-Eval input:
(append '(a b c) '(d e f))
;;; M-Eval value:
(a b c d e f)
#+end_src

**** Exercise 4.14
:properties:
:custom_id: exercise-4.14
:end:

Eva Lu Ator and Louis Reasoner are each experimenting with the metacircular evaluator.  Eva types in the definition of ~map~, and runs some test programs that use it.  They work fine.  Louis, in contrast, has installed the system version of ~map~ as a primitive for the metacircular evaluator.  When he tries it, things go terribly wrong.  Explain why Louis's ~map~ fails even though Eva's works.

*** 4.1.5 Data as Programs
:properties:
:custom_id: section-4.1.5
:end:

In thinking about a Lisp program that evaluates Lisp expressions, an analogy might be helpful.  One operational view of the meaning of a program is that a program is a description of an abstract (perhaps infinitely large) machine.  For example, consider the familiar program to compute factorials:

#+begin_src scheme
(define (factorial n)
  (if (= n 1)
      1
      (* (factorial (- n 1)) n)))
#+end_src

We may regard this program as the description of a machine containing parts that decrement, multiply, and test for equality, together with a two-position switch and another factorial machine.  (The factorial machine is infinite because it contains another factorial machine within it.)  [[figure-4.2][Figure 4.2]] is a flow diagram for the factorial machine, showing how the parts are wired together.

<<figure-4.2>> The factorial program, viewed as an abstract machine.

#+begin_example
     +-----------------------------------+
     | factorial                   |1    |
     |              |1             V     |
     |              |           +-----+  |
     |              V           | #   |  |
     |           +-----+        |     |  |
 6 --------*-----|  =  |------->|   #-+-----> 720
     |     |     +-----+        |  /  |  |
     |     |                    | #   |  |
     |     |                    +-----+  |
     |     |                       ^     |
     |     |                       |     |
     |     |                    +--+--+  |
     |     *------------------->|  *  |  |
     |     |                    +-----+  |
     |     V                       ^     |
     |  +-----+    +-----------+   |     |
     |  |  -  +--->| factorial +---+     |
     |  +-----+    +-----------+         |
     |     ^                             |
     |     |1                            |
     +-----------------------------------+
#+end_example

In a similar way, we can regard the evaluator as a very special machine that takes as input a description of a machine.  Given this input, the evaluator configures itself to emulate the machine described.  For example, if we feed our evaluator the definition of ~factorial~, as shown in [[figure-4.3][Figure 4.3]], the evaluator will be able to compute factorials.

<<figure-4.3>> The evaluator emulating a factorial machine.

#+begin_example
                 +--------+
          6 ---->|  eval  |----> 720
                 +--------+
                     /
           . . .    /  . . .
     . . .       ../. .      .
   .                           ..
  .   (define (factorial n)      . . .
 .      (if (= n 1)                   . .
  .         1                            .
  .         (* (factorial (- n 1)) n)))   .
    . .                       . .        .
        . .  . .      . . . .     . . . .
                 . ..
#+end_example

From this perspective, our evaluator is seen to be a <<i415>> universal machine.  It mimics other machines when these are described as Lisp programs.[fn:223] This is striking.  Try to imagine an analogous evaluator for electrical circuits.  This would be a circuit that takes as input a signal encoding the plans for some other circuit, such as a filter.  Given this input, the circuit evaluator would then behave like a filter with the same description.  Such a universal electrical circuit is almost unimaginably complex.  It is remarkable that the program evaluator is a rather simple program.[fn:224]

Another striking aspect of the evaluator is that it acts as a bridge between the data objects that are manipulated by our programming language and the programming language itself.  Imagine that the evaluator program (implemented in Lisp) is running, and that a user is typing expressions to the evaluator and observing the results.  From the perspective of the user, an input expression such as ~(* x x)~ is an expression in the programming language, which the evaluator should execute.  From the perspective of the evaluator, however, the expression is simply a list (in this case, a list of three symbols: ~*~, ~x~, and ~x~) that is to be manipulated according to a well-defined set of rules.

That the user's programs are the evaluator's data need not be a source of confusion.  In fact, it is sometimes convenient to ignore this distinction, and to give the user the ability to explicitly evaluate a data object as a Lisp expression, by making ~eval~ available for use in programs.  Many Lisp dialects provide a primitive ~eval~ procedure that takes as arguments an expression and an environment and evaluates the expression relative to the environment.[fn:225] Thus,

#+begin_src scheme
(eval '(* 5 5) user-initial-environment)
#+end_src

and

#+begin_src scheme
(eval (cons '* (list 5 5)) user-initial-environment)
#+end_src

will both return 25.[fn:226]

**** Exercise 4.15
:properties:
:custom_id: exercise-4.15
:end:

Given a one-argument procedure ~p~ and an object ~a~, ~p~ is said to "halt" on ~a~ if evaluating the expression ~(p a)~ returns a value (as opposed to terminating with an error message or running forever).  Show that it is impossible to write a procedure ~halts?~ that correctly determines whether ~p~ halts on ~a~ for any procedure ~p~ and object ~a~.  Use the following reasoning: If you had such a procedure ~halts?~, you could implement the following program:

#+begin_src scheme
(define (run-forever) (run-forever))

(define (try p)
  (if (halts? p p)
      (run-forever)
      'halted))
#+end_src

Now consider evaluating the expression ~(try try)~ and show that any possible outcome (either halting or running forever) violates the intended behavior of ~halts?~.[fn:227]

*** 4.1.6 Internal Definitions
:properties:
:custom_id: section-4.1.6
:end:

Our environment model of evaluation and our metacircular evaluator execute definitions in sequence, extending the environment frame one definition at a time.  This is particularly convenient for interactive program development, in which the programmer needs to freely mix the application of procedures with the definition of new procedures.  However, if we think carefully about the internal definitions used to implement block structure (introduced in section [[#section-1.1.8][1.1.8]]), we will find that name-by-name extension of the environment may not be the best way to define local variables.

Consider a procedure with internal definitions, such as

#+begin_src scheme
(define (f x)
  (define (even? n)
    (if (= n 0)
        true
        (odd? (- n 1))))
  (define (odd? n)
    (if (= n 0)
        false
        (even? (- n 1))))
  <REST OF BODY OF ~F~>)
#+end_src

Our intention here is that the name ~odd?~ in the body of the procedure ~even?~ should refer to the procedure ~odd?~ that is defined after ~even?~.  The scope of the name ~odd?~ is the entire body of ~f~, not just the portion of the body of ~f~ starting at the point where the ~define~ for ~odd?~ occurs.  Indeed, when we consider that ~odd?~ is itself defined in terms of ~even?~--so that ~even?~ and ~odd?~ are mutually recursive procedures--we see that the only satisfactory interpretation of the two ~define~s is to regard them as if the names ~even?~ and ~odd?~ were being added to the environment simultaneously.  More generally, in block structure, the scope of a local name is the entire procedure body in which the ~define~ is evaluated.

As it happens, our interpreter will evaluate calls to ~f~ correctly, but for an "accidental" reason: Since the definitions of the internal procedures come first, no calls to these procedures will be evaluated until all of them have been defined.  Hence, ~odd?~ will have been defined by the time ~even?~ is executed.  In fact, our sequential evaluation mechanism will give the same result as a mechanism that directly implements simultaneous definition for any procedure in which the internal definitions come first in a body and evaluation of the value expressions for the defined variables doesn't actually use any of the defined variables.  (For an example of a procedure that doesn't obey these restrictions, so that sequential definition isn't equivalent to simultaneous definition, see [[#exercise-4.19][Exercise 4.19]].)[fn:228]

There is, however, a simple way to treat definitions so that internally defined names have truly simultaneous scope--just create all local variables that will be in the current environment before evaluating any of the value expressions.  One way to do this is by a syntax transformation on ~lambda~ expressions.  Before evaluating the body of a ~lambda~ expression, we "scan out" and eliminate all the internal definitions in the body.  The internally defined variables will be created with a ~let~ and then set to their values by assignment.  For example, the procedure

#+begin_src scheme
(lambda <VARS>
  (define u <E1>)
  (define v <E2>)
  <E3>)
#+end_src

would be transformed into

#+begin_src scheme
(lambda <VARS>
  (let ((u '*unassigned*)
        (v '*unassigned*))
    (set! u <E1>)
    (set! v <E2>)
    <E3>))
#+end_src

where ~*unassigned*~ is a special symbol that causes looking up a variable to signal an error if an attempt is made to use the value of the not-yet-assigned variable.

An alternative strategy for scanning out internal definitions is shown in [[#exercise-4.18][Exercise 4.18]].  Unlike the transformation shown above, this enforces the restriction that the defined variables' values can be evaluated without using any of the variables' values.[fn:229]

**** Exercise 4.16
:properties:
:custom_id: exercise-4.16
:end:

In this exercise we implement the method just described for interpreting internal definitions.  We assume that the evaluator supports ~let~ (see [[#exercise-4.6][Exercise 4.6]]).

a. Change ~lookup-variable-value~ (section [[#section-4.1.3][4.1.3]]) to signal an error if the value it finds is the symbol ~*unassigned*~.

b. Write a procedure ~scan-out-defines~ that takes a procedure body and returns an equivalent one that has no internal definitions, by making the transformation described above.

c. Install ~scan-out-defines~ in the interpreter, either in ~make-procedure~ or in ~procedure-body~ (see section [[#section-4.1.3][4.1.3]]).  Which place is better?  Why?

**** Exercise 4.17
:properties:
:custom_id: exercise-4.17
:end:

Draw diagrams of the environment in effect when evaluating the expression <E3> in the procedure in the text, comparing how this will be structured when definitions are interpreted sequentially with how it will be structured if definitions are scanned out as described.  Why is there an extra frame in the transformed program?  Explain why this difference in environment structure can never make a difference in the behavior of a correct program.  Design a way to make the interpreter implement the "simultaneous" scope rule for internal definitions without constructing the extra frame.

**** Exercise 4.18
:properties:
:custom_id: exercise-4.18
:end:

Consider an alternative strategy for scanning out definitions that translates the example in the text to

#+begin_src scheme
(lambda <VARS>
  (let ((u '*unassigned*)
        (v '*unassigned*))
    (let ((a <E1>)
          (b <E2>))
      (set! u a)
      (set! v b))
    <E3>))
#+end_src

Here ~a~ and ~b~ are meant to represent new variable names, created by the interpreter, that do not appear in the user's program.  Consider the ~solve~ procedure from section [[#section-3.5.4][3.5.4]]:

#+begin_src scheme
(define (solve f y0 dt)
  (define y (integral (delay dy) y0 dt))
  (define dy (stream-map f y))
  y)
#+end_src

Will this procedure work if internal definitions are scanned out as shown in this exercise?  What if they are scanned out as shown in the text?  Explain.

**** Exercise 4.19
:properties:
:custom_id: exercise-4.19
:end:

Ben Bitdiddle, Alyssa P. Hacker, and Eva Lu Ator are arguing about the desired result of evaluating the expression

#+begin_src scheme
(let ((a 1))
  (define (f x)
    (define b (+ a x))
    (define a 5)
    (+ a b))
  (f 10))
#+end_src

Ben asserts that the result should be obtained using the sequential rule for ~define~: ~b~ is defined to be 11, then ~a~ is defined to be 5, so the result is 16.  Alyssa objects that mutual recursion requires the simultaneous scope rule for internal procedure definitions, and that it is unreasonable to treat procedure names differently from other names.  Thus, she argues for the mechanism implemented in [[#exercise-4.16][Exercise 4.16]].  This would lead to ~a~ being unassigned at the time that the value for ~b~ is to be computed.  Hence, in Alyssa's view the procedure should produce an error.  Eva has a third opinion.  She says that if the definitions of ~a~ and ~b~ are truly meant to be simultaneous, then the value 5 for ~a~ should be used in evaluating ~b~.  Hence, in Eva's view ~a~ should be 5, ~b~ should be 15, and the result should be 20.  Which (if any) of these viewpoints do you support?  Can you devise a way to implement internal definitions so that they behave as Eva prefers?[fn:230]

**** Exercise 4.20
:properties:
:custom_id: exercise-4.20
:end:

Because internal definitions look sequential but are actually simultaneous, some people prefer to avoid them entirely, and use the special form ~letrec~ instead.  ~letrec~ looks like ~let~, so it is not surprising that the variables it binds are bound simultaneously and have the same scope as each other.  The sample procedure ~f~ above can be written without internal definitions, but with exactly the same meaning, as

#+begin_src scheme
(define (f x)
  (letrec ((even?
            (lambda (n)
              (if (= n 0)
                  true
                  (odd? (- n 1)))))
           (odd?
            (lambda (n)
              (if (= n 0)
                  false
                  (even? (- n 1))))))
    <REST OF BODY OF ~F~>))
#+end_src

~letrec~ expressions, which have the form

#+begin_src scheme
(letrec ((<VAR_1> <EXP_1>) ... (<VAR_N> <EXP_N>))
  <BODY>)
#+end_src

are a variation on ~let~ in which the expressions <EXP_K> that provide the initial values for the variables <VAR_K> are evaluated in an environment that includes all the ~letrec~ bindings.  This permits recursion in the bindings, such as the mutual recursion of ~even?~ and ~odd?~ in the example above, or the evaluation of 10 factorial with

#+begin_src scheme
(letrec ((fact
          (lambda (n)
            (if (= n 1)
                1
                (* n (fact (- n 1)))))))
  (fact 10))
#+end_src

a. Implement ~letrec~ as a derived expression, by transforming a ~letrec~ expression into a ~let~ expression as shown in the text above or in [[#exercise-4.18][Exercise 4.18]].  That is, the ~letrec~ variables should be created with a ~let~ and then be assigned their values with ~set!~.

b. Louis Reasoner is confused by all this fuss about internal definitions.  The way he sees it, if you don't like to use ~define~ inside a procedure, you can just use ~let~.  Illustrate what is loose about his reasoning by drawing an environment diagram that shows the environment in which the <REST OF BODY OF F> is evaluated during evaluation of the expression ~(f 5)~, with ~f~ defined as in this exercise.  Draw an environment diagram for the same evaluation, but with ~let~ in place of ~letrec~ in the definition of ~f~.

**** Exercise 4.21
:properties:
:custom_id: exercise-4.21
:end:

Amazingly, Louis's intuition in [[#exercise-4.20][Exercise 4.20]] is correct.  It is indeed possible to specify recursive procedures without using ~letrec~ (or even ~define~), although the method for accomplishing this is much more subtle than Louis imagined.  The following expression computes 10 factorial by applying a recursive factorial procedure:[fn:231]

#+begin_src scheme
((lambda (n)
   ((lambda (fact)
      (fact fact n))
    (lambda (ft k)
      (if (= k 1)
          1
          (* k (ft ft (- k 1)))))))
 10)
#+end_src

a. Check (by evaluating the expression) that this really does compute factorials.  Devise an analogous expression for computing Fibonacci numbers.

b. Consider the following procedure, which includes mutually recursive internal definitions:

#+begin_src scheme
(define (f x)
  (define (even? n)
    (if (= n 0)
        true
        (odd? (- n 1))))
  (define (odd? n)
    (if (= n 0)
        false
        (even? (- n 1))))
  (even? x))
#+end_src

Fill in the missing expressions to complete an alternative definition of ~f~, which uses neither internal definitions nor ~letrec~:

#+begin_src scheme
(define (f x)
  ((lambda (even? odd?)
     (even? even? odd? x))
   (lambda (ev? od? n)
     (if (= n 0) true (od? <??> <??> <??>)))
   (lambda (ev? od? n)
     (if (= n 0) false (ev? <??> <??> <??>)))))
#+end_src

*** 4.1.7 Separating Syntactic Analysis from Execution
:properties:
:custom_id: section-4.1.7
:end:

The evaluator implemented above is simple, but it is very inefficient, because the syntactic analysis of expressions is interleaved with their execution.  Thus if a program is executed many times, its syntax is analyzed many times.  Consider, for example, evaluating ~(factorial 4)~ using the following definition of ~factorial~:

#+begin_src scheme
(define (factorial n)
  (if (= n 1)
      1
      (* (factorial (- n 1)) n)))
#+end_src

Each time ~factorial~ is called, the evaluator must determine that the body is an ~if~ expression and extract the predicate.  Only then can it evaluate the predicate and dispatch on its value.  Each time it evaluates the expression ~(* (factorial (- n 1)) n)~, or the subexpressions ~(factorial (- n 1))~ and ~(- n 1)~, the evaluator must perform the case analysis in ~eval~ to determine that the expression is an application, and must extract its operator and operands.  This analysis is expensive.  Performing it repeatedly is wasteful.

We can transform the evaluator to be significantly more efficient by arranging things so that syntactic analysis is performed only once.[fn:232] We split ~eval~, which takes an expression and an environment, into two parts.  The procedure ~analyze~ takes only the expression.  It performs the syntactic analysis and returns a new procedure, the <<i132>> execution procedure, that encapsulates the work to be done in executing the analyzed expression.  The execution procedure takes an environment as its argument and completes the evaluation.  This saves work because ~analyze~ will be called only once on an expression, while the execution procedure may be called many times.

With the separation into analysis and execution, ~eval~ now becomes

#+begin_src scheme
(define (eval exp env)
  ((analyze exp) env))
#+end_src

The result of calling ~analyze~ is the execution procedure to be applied to the environment.  The ~analyze~ procedure is the same case analysis as performed by the original ~eval~ of section [[#section-4.1.1][4.1.1]], except that the procedures to which we dispatch perform only analysis, not full evaluation:

#+begin_src scheme
(define (analyze exp)
  (cond ((self-evaluating? exp)
         (analyze-self-evaluating exp))
        ((quoted? exp) (analyze-quoted exp))
        ((variable? exp) (analyze-variable exp))
        ((assignment? exp) (analyze-assignment exp))
        ((definition? exp) (analyze-definition exp))
        ((if? exp) (analyze-if exp))
        ((lambda? exp) (analyze-lambda exp))
        ((begin? exp) (analyze-sequence (begin-actions exp)))
        ((cond? exp) (analyze (cond->if exp)))
        ((application? exp) (analyze-application exp))
        (else
         (error "Unknown expression type - ANALYZE" exp))))
#+end_src

Here is the simplest syntactic analysis procedure, which handles self-evaluating expressions.  It returns an execution procedure that ignores its environment argument and just returns the expression:

#+begin_src scheme
(define (analyze-self-evaluating exp)
  (lambda (env) exp))
#+end_src

For a quoted expression, we can gain a little efficiency by extracting the text of the quotation only once, in the analysis phase, rather than in the execution phase.

#+begin_src scheme
(define (analyze-quoted exp)
  (let ((qval (text-of-quotation exp)))
    (lambda (env) qval)))
#+end_src

Looking up a variable value must still be done in the execution phase, since this depends upon knowing the environment.[fn:233]

#+begin_src scheme
(define (analyze-variable exp)
  (lambda (env) (lookup-variable-value exp env)))
#+end_src

~analyze-assignment~ also must defer actually setting the variable until the execution, when the environment has been supplied.  However, the fact that the ~assignment-value~ expression can be analyzed (recursively) during analysis is a major gain in efficiency, because the ~assignment-value~ expression will now be analyzed only once.  The same holds true for definitions.

#+begin_src scheme
(define (analyze-assignment exp)
  (let ((var (assignment-variable exp))
        (vproc (analyze (assignment-value exp))))
    (lambda (env)
      (set-variable-value! var (vproc env) env)
      'ok)))

(define (analyze-definition exp)
  (let ((var (definition-variable exp))
        (vproc (analyze (definition-value exp))))
    (lambda (env)
      (define-variable! var (vproc env) env)
      'ok)))
#+end_src

For ~if~ expressions, we extract and analyze the predicate, consequent, and alternative at analysis time.

#+begin_src scheme
(define (analyze-if exp)
  (let ((pproc (analyze (if-predicate exp)))
        (cproc (analyze (if-consequent exp)))
        (aproc (analyze (if-alternative exp))))
    (lambda (env)
      (if (true? (pproc env))
          (cproc env)
          (aproc env)))))
#+end_src

Analyzing a ~lambda~ expression also achieves a major gain in efficiency: We analyze the ~lambda~ body only once, even though procedures resulting from evaluation of the ~lambda~ may be applied many times.

#+begin_src scheme
(define (analyze-lambda exp)
  (let ((vars (lambda-parameters exp))
        (bproc (analyze-sequence (lambda-body exp))))
    (lambda (env) (make-procedure vars bproc env))))
#+end_src

Analysis of a sequence of expressions (as in a ~begin~ or the body of a ~lambda~ expression) is more involved.[fn:234] Each expression in the sequence is analyzed, yielding an execution procedure.  These execution procedures are combined to produce an execution procedure that takes an environment as argument and sequentially calls each individual execution procedure with the environment as argument.

#+begin_src scheme
(define (analyze-sequence exps)
  (define (sequentially proc1 proc2)
    (lambda (env) (proc1 env) (proc2 env)))
  (define (loop first-proc rest-procs)
    (if (null? rest-procs)
        first-proc
        (loop (sequentially first-proc (car rest-procs))
              (cdr rest-procs))))
  (let ((procs (map analyze exps)))
    (if (null? procs)
        (error "Empty sequence - ANALYZE"))
    (loop (car procs) (cdr procs))))
#+end_src

To analyze an application, we analyze the operator and operands and construct an execution procedure that calls the operator execution procedure (to obtain the actual procedure to be applied) and the operand execution procedures (to obtain the actual arguments).  We then pass these to ~execute-application~, which is the analog of ~apply~ in section [[#section-4.1.1][4.1.1]].  ~execute-application~ differs from ~apply~ in that the procedure body for a compound procedure has already been analyzed, so there is no need to do further analysis.  Instead, we just call the execution procedure for the body on the extended environment.

#+begin_src scheme
(define (analyze-application exp)
  (let ((fproc (analyze (operator exp)))
        (aprocs (map analyze (operands exp))))
    (lambda (env)
      (execute-application (fproc env)
                           (map (lambda (aproc) (aproc env))
                                aprocs)))))

(define (execute-application proc args)
  (cond ((primitive-procedure? proc)
         (apply-primitive-procedure proc args))
        ((compound-procedure? proc)
         ((procedure-body proc)
          (extend-environment (procedure-parameters proc)
                              args
                              (procedure-environment proc))))
        (else
         (error
          "Unknown procedure type - EXECUTE-APPLICATION"
          proc))))
#+end_src

Our new evaluator uses the same data structures, syntax procedures, and run-time support procedures as in sections [[#section-4.1.2][4.1.2]], [[#section-4.1.3][4.1.3]], and [[#section-4.1.4][4.1.4]].

**** Exercise 4.22
:properties:
:custom_id: exercise-4.22
:end:

Extend the evaluator in this section to support the special form ~let~.  (See [[#exercise-4.6][Exercise 4.6]].)

**** Exercise 4.23
:properties:
:custom_id: exercise-4.23
:end:

Alyssa P. Hacker doesn't understand why ~analyze-sequence~ needs to be so complicated.  All the other analysis procedures are straightforward transformations of the corresponding evaluation procedures (or ~eval~ clauses) in section [[#section-4.1.1][4.1.1]].  She expected ~analyze-sequence~ to look like this:

#+begin_src scheme
(define (analyze-sequence exps)
  (define (execute-sequence procs env)
    (cond ((null? (cdr procs)) ((car procs) env))
          (else ((car procs) env)
                (execute-sequence (cdr procs) env))))
  (let ((procs (map analyze exps)))
    (if (null? procs)
        (error "Empty sequence - ANALYZE"))
    (lambda (env) (execute-sequence procs env))))
#+end_src

Eva Lu Ator explains to Alyssa that the version in the text does more of the work of evaluating a sequence at analysis time.  Alyssa's sequence-execution procedure, rather than having the calls to the individual execution procedures built in, loops through the procedures in order to call them: In effect, although the individual expressions in the sequence have been analyzed, the sequence itself has not been.

Compare the two versions of ~analyze-sequence~.  For example, consider the common case (typical of procedure bodies) where the sequence has just one expression.  What work will the execution procedure produced by Alyssa's program do?  What about the execution procedure produced by the program in the text above?  How do the two versions compare for a sequence with two expressions?

**** Exercise 4.24
:properties:
:custom_id: exercise-4.24
:end:

Design and carry out some experiments to compare the speed of the original metacircular evaluator with the version in this section.  Use your results to estimate the fraction of time that is spent in analysis versus execution for various procedures.

** 4.2 Variations on a Scheme -- Lazy Evaluation
:properties:
:custom_id: section-4.2
:end:

Now that we have an evaluator expressed as a Lisp program, we can experiment with alternative choices in language design simply by modifying the evaluator.  Indeed, new languages are often invented by first writing an evaluator that embeds the new language within an existing high-level language.  For example, if we wish to discuss some aspect of a proposed modification to Lisp with another member of the Lisp community, we can supply an evaluator that embodies the change.  The recipient can then experiment with the new evaluator and send back comments as further modifications.  Not only does the high-level implementation base make it easier to test and debug the evaluator; in addition, the embedding enables the designer to snarf[fn:235] features from the underlying language, just as our embedded Lisp evaluator uses primitives and control structure from the underlying Lisp.  Only later (if ever) need the designer go to the trouble of building a complete implementation in a low-level language or in hardware.  In this section and the next we explore some variations on Scheme that provide significant additional expressive power.

*** 4.2.1 Normal Order and Applicative Order
:properties:
:custom_id: section-4.2.1
:end:

In section [[#section-1.1][1.1]], where we began our discussion of models of evaluation, we noted that Scheme is an <<i18>> applicative-order language, namely, that all the arguments to Scheme procedures are evaluated when the procedure is applied.  In contrast, <<i262>> normal-order languages delay evaluation of procedure arguments until the actual argument values are needed.  Delaying evaluation of procedure arguments until the last possible moment (e.g., until they are required by a primitive operation) is called <<i203>> lazy evaluation.[fn:236] Consider the procedure

#+begin_src scheme
(define (try a b)
  (if (= a 0) 1 b))
#+end_src

Evaluating ~(try 0 (/ 1 0))~ generates an error in Scheme.  With lazy evaluation, there would be no error.  Evaluating the expression would return 1, because the argument ~(/ 1 0)~ would never be evaluated.

An example that exploits lazy evaluation is the definition of a procedure ~unless~

#+begin_src scheme
(define (unless condition usual-value exceptional-value)
  (if condition exceptional-value usual-value))
#+end_src

that can be used in expressions such as

#+begin_src scheme
(unless (= b 0)
  (/ a b)
  (begin (display "exception: returning 0")
         0))
#+end_src

This won't work in an applicative-order language because both the usual value and the exceptional value will be evaluated before ~unless~ is called (compare [[#exercise-1.6][Exercise 1.6]]).  An advantage of lazy evaluation is that some procedures, such as ~unless~, can do useful computation even if evaluation of some of their arguments would produce errors or would not terminate.

If the body of a procedure is entered before an argument has been evaluated we say that the procedure is <<i257>> non-strict in that argument.  If the argument is evaluated before the body of the procedure is entered we say that the procedure is <<i372>> strict in that argument.[fn:237] In a purely applicative-order language, all procedures are strict in each argument.  In a purely normal-order language, all compound procedures are non-strict in each argument, and primitive procedures may be either strict or non-strict.  There are also languages (see [[#exercise-4.31][Exercise 4.31]]) that give programmers detailed control over the strictness of the procedures they define.

A striking example of a procedure that can usefully be made non-strict is ~cons~ (or, in general, almost any constructor for data structures).  One can do useful computation, combining elements to form data structures and operating on the resulting data structures, even if the values of the elements are not known.  It makes perfect sense, for instance, to compute the length of a list without knowing the values of the individual elements in the list.  We will exploit this idea in section [[#section-4.2.3][4.2.3]] to implement the streams of [[#section-3][Chapter 3]] as lists formed of non-strict ~cons~ pairs.

**** Exercise 4.25
:properties:
:custom_id: exercise-4.25
:end:

Suppose that (in ordinary applicative-order Scheme) we define ~unless~ as shown above and then define ~factorial~ in terms of ~unless~ as

#+begin_src scheme
(define (factorial n)
  (unless (= n 1)
    (* n (factorial (- n 1)))
    1))
#+end_src

What happens if we attempt to evaluate ~(factorial 5)~?  Will our definitions work in a normal-order language?

**** Exercise 4.26
:properties:
:custom_id: exercise-4.26
:end:

Ben Bitdiddle and Alyssa P. Hacker disagree over the importance of lazy evaluation for implementing things such as ~unless~.  Ben points out that it's possible to implement ~unless~ in applicative order as a special form.  Alyssa counters that, if one did that, ~unless~ would be merely syntax, not a procedure that could be used in conjunction with higher-order procedures.  Fill in the details on both sides of the argument.  Show how to implement ~unless~ as a derived expression (like ~cond~ or ~let~), and give an example of a situation where it might be useful to have ~unless~ available as a procedure, rather than as a special form.

*** 4.2.2 An Interpreter with Lazy Evaluation
:properties:
:custom_id: section-4.2.2
:end:

In this section we will implement a normal-order language that is the same as Scheme except that compound procedures are non-strict in each argument.  Primitive procedures will still be strict.  It is not difficult to modify the evaluator of section [[#section-4.1.1][4.1.1]] so that the language it interprets behaves this way.  Almost all the required changes center around procedure application.

The basic idea is that, when applying a procedure, the interpreter must determine which arguments are to be evaluated and which are to be delayed.  The delayed arguments are not evaluated; instead, they are transformed into objects called <<i395>> thunks.[fn:238] The thunk must contain the information required to produce the value of the argument when it is needed, as if it had been evaluated at the time of the application.  Thus, the thunk must contain the argument expression and the environment in which the procedure application is being evaluated.

The process of evaluating the expression in a thunk is called <<i142>> forcing.[fn:239] In general, a thunk will be forced only when its value is needed: when it is passed to a primitive procedure that will use the value of the thunk; when it is the value of a predicate of a conditional; and when it is the value of an operator that is about to be applied as a procedure.  One design choice we have available is whether or not to <<i231>> memoize thunks, as we did with delayed objects in section [[#section-3.5.1][3.5.1]].  With memoization, the first time a thunk is forced, it stores the value that is computed.  Subsequent forcings simply return the stored value without repeating the computation.  We'll make our interpreter memoize, because this is more efficient for many applications.  There are tricky considerations here, however.[fn:240]

*Modifying the evaluator*

The main difference between the lazy evaluator and the one in section [[#section-4.1][4.1]] is in the handling of procedure applications in ~eval~ and ~apply~.

The ~application?~ clause of ~eval~ becomes

#+begin_src scheme
((application? exp)
 (apply (actual-value (operator exp) env)
        (operands exp)
        env))
#+end_src

This is almost the same as the ~application?~ clause of ~eval~ in section [[#section-4.1.1][4.1.1]].  For lazy evaluation, however, we call ~apply~ with the operand expressions, rather than the arguments produced by evaluating them.  Since we will need the environment to construct thunks if the arguments are to be delayed, we must pass this as well.  We still evaluate the operator, because ~apply~ needs the actual procedure to be applied in order to dispatch on its type (primitive versus compound) and apply it.

Whenever we need the actual value of an expression, we use

#+begin_src scheme
(define (actual-value exp env)
  (force-it (eval exp env)))
#+end_src

instead of just ~eval~, so that if the expression's value is a thunk, it will be forced.

Our new version of ~apply~ is also almost the same as the version in section [[#section-4.1.1][4.1.1]].  The difference is that ~eval~ has passed in unevaluated operand expressions: For primitive procedures (which are strict), we evaluate all the arguments before applying the primitive; for compound procedures (which are non-strict) we delay all the arguments before applying the procedure.

#+begin_src scheme
(define (apply procedure arguments env)
  (cond ((primitive-procedure? procedure)
         (apply-primitive-procedure
          procedure
          (list-of-arg-values arguments env))) ; changed
        ((compound-procedure? procedure)
         (eval-sequence
          (procedure-body procedure)
          (extend-environment
           (procedure-parameters procedure)
           (list-of-delayed-args arguments env) ; changed
           (procedure-environment procedure))))
        (else
         (error
          "Unknown procedure type - APPLY" procedure))))
#+end_src

The procedures that process the arguments are just like ~list-of-values~ from section [[#section-4.1.1][4.1.1]], except that ~list-of-delayed-args~ delays the arguments instead of evaluating them, and ~list-of-arg-values~ uses ~actual-value~ instead of ~eval~:

#+begin_src scheme
(define (list-of-arg-values exps env)
  (if (no-operands? exps)
      '()
      (cons (actual-value (first-operand exps) env)
            (list-of-arg-values (rest-operands exps)
                                env))))

(define (list-of-delayed-args exps env)
  (if (no-operands? exps)
      '()
      (cons (delay-it (first-operand exps) env)
            (list-of-delayed-args (rest-operands exps)
                                  env))))
#+end_src

The other place we must change the evaluator is in the handling of ~if~, where we must use ~actual-value~ instead of ~eval~ to get the value of the predicate expression before testing whether it is true or false:

#+begin_src scheme
(define (eval-if exp env)
  (if (true? (actual-value (if-predicate exp) env))
      (eval (if-consequent exp) env)
      (eval (if-alternative exp) env)))
#+end_src

Finally, we must change the ~driver-loop~ procedure (section [[#section-4.1.4][4.1.4]]) to use ~actual-value~ instead of ~eval~, so that if a delayed value is propagated back to the read-eval-print loop, it will be forced before being printed.  We also change the prompts to indicate that this is the lazy evaluator:

#+begin_src scheme
(define input-prompt ";;; L-Eval input:")
(define output-prompt ";;; L-Eval value:")

(define (driver-loop)
  (prompt-for-input input-prompt)
  (let ((input (read)))
    (let ((output
           (actual-value input the-global-environment)))
      (announce-output output-prompt)
      (user-print output)))
  (driver-loop))
#+end_src

With these changes made, we can start the evaluator and test it.  The successful evaluation of the ~try~ expression discussed in section [[#section-4.2.1][4.2.1]] indicates that the interpreter is performing lazy evaluation:

#+begin_src scheme
(define the-global-environment (setup-environment))

(driver-loop)

;;; L-Eval input:
(define (try a b)
  (if (= a 0) 1 b))
;;; L-Eval value:
ok

;;; L-Eval input:
(try 0 (/ 1 0))
;;; L-Eval value:
1
#+end_src

*Representing thunks*

Our evaluator must arrange to create thunks when procedures are applied to arguments and to force these thunks later.  A thunk must package an expression together with the environment, so that the argument can be produced later.  To force the thunk, we simply extract the expression and environment from the thunk and evaluate the expression in the environment.  We use ~actual-value~ rather than ~eval~ so that in case the value of the expression is itself a thunk, we will force that, and so on, until we reach something that is not a thunk:

#+begin_src scheme
(define (force-it obj)
  (if (thunk? obj)
      (actual-value (thunk-exp obj) (thunk-env obj))
      obj))
#+end_src

One easy way to package an expression with an environment is to make a list containing the expression and the environment.  Thus, we create a thunk as follows:

#+begin_src scheme
(define (delay-it exp env)
  (list 'thunk exp env))

(define (thunk? obj)
  (tagged-list? obj 'thunk))

(define (thunk-exp thunk) (cadr thunk))

(define (thunk-env thunk) (caddr thunk))
#+end_src

Actually, what we want for our interpreter is not quite this, but rather thunks that have been memoized.  When a thunk is forced, we will turn it into an evaluated thunk by replacing the stored expression with its value and changing the ~thunk~ tag so that it can be recognized as already evaluated.[fn:241]

#+begin_src scheme
(define (evaluated-thunk? obj)
  (tagged-list? obj 'evaluated-thunk))

(define (thunk-value evaluated-thunk) (cadr evaluated-thunk))

(define (force-it obj)
  (cond ((thunk? obj)
         (let ((result (actual-value
                        (thunk-exp obj)
                        (thunk-env obj))))
           (set-car! obj 'evaluated-thunk)
           (set-car! (cdr obj) result)  ; replace ~exp~ with its value
           (set-cdr! (cdr obj) '())     ; forget unneeded ~env~
           result))
        ((evaluated-thunk? obj)
         (thunk-value obj))
        (else obj)))
#+end_src

Notice that the same ~delay-it~ procedure works both with and without memoization.

**** Exercise 4.27
:properties:
:custom_id: exercise-4.27
:end:

Suppose we type in the following definitions to the lazy evaluator:

#+begin_src scheme
(define count 0)

(define (id x)
  (set! count (+ count 1))
  x)
#+end_src

Give the missing values in the following sequence of interactions, and explain your answers.[fn:242]

#+begin_src scheme
(define w (id (id 10)))

;;; L-Eval input:
count
;;; L-Eval value:
<RESPONSE>

;;; L-Eval input:
w
;;; L-Eval value:
<RESPONSE>

;;; L-Eval input:
count
;;; L-Eval value:
<RESPONSE>
#+end_src

**** Exercise 4.28
:properties:
:custom_id: exercise-4.28
:end:

~eval~ uses ~actual-value~ rather than ~eval~ to evaluate the operator before passing it to ~apply~, in order to force the value of the operator.  Give an example that demonstrates the need for this forcing.

**** Exercise 4.29
:properties:
:custom_id: exercise-4.29
:end:

Exhibit a program that you would expect to run much more slowly without memoization than with memoization.  Also, consider the following interaction, where the ~id~ procedure is defined as in [[#exercise-4.27][Exercise 4.27]] and ~count~ starts at 0:

#+begin_src scheme
(define (square x)
  (* x x))

;;; L-Eval input:
(square (id 10))
;;; L-Eval value:
<RESPONSE>

;;; L-Eval input:
count
;;; L-Eval value:
<RESPONSE>
#+end_src

Give the responses both when the evaluator memoizes and when it does not.

**** Exercise 4.30
:properties:
:custom_id: exercise-4.30
:end:

Cy D. Fect, a reformed C programmer, is worried that some side effects may never take place, because the lazy evaluator doesn't force the expressions in a sequence.  Since the value of an expression in a sequence other than the last one is not used (the expression is there only for its effect, such as assigning to a variable or printing), there can be no subsequent use of this value (e.g., as an argument to a primitive procedure) that will cause it to be forced.  Cy thus thinks that when evaluating sequences, we must force all expressions in the sequence except the final one.  He proposes to modify ~eval-sequence~ from section [[#section-4.1.1][4.1.1]] to use ~actual-value~ rather than ~eval~:

#+begin_src scheme
(define (eval-sequence exps env)
  (cond ((last-exp? exps) (eval (first-exp exps) env))
        (else (actual-value (first-exp exps) env)
              (eval-sequence (rest-exps exps) env))))
#+end_src

a. Ben Bitdiddle thinks Cy is wrong.  He shows Cy the ~for-each~ procedure described in [[#exercise-2.23][Exercise 2.23]], which gives an important example of a sequence with side effects:

#+begin_src scheme
(define (for-each proc items)
  (if (null? items)
      'done
      (begin (proc (car items))
             (for-each proc (cdr items)))))
#+end_src

He claims that the evaluator in the text (with the original ~eval-sequence~) handles this correctly:

#+begin_src scheme
;;; L-Eval input:
(for-each (lambda (x) (newline) (display x))
          (list 57 321 88))
57
321
88
;;; L-Eval value:
done
#+end_src

Explain why Ben is right about the behavior of ~for-each~.

b. Cy agrees that Ben is right about the ~for-each~ example, but says that that's not the kind of program he was thinking about when he proposed his change to ~eval-sequence~.  He defines the following two procedures in the lazy evaluator:

#+begin_src scheme
(define (p1 x)
  (set! x (cons x '(2)))
  x)

(define (p2 x)
  (define (p e)
    e
    x)
  (p (set! x (cons x '(2)))))
#+end_src

What are the values of ~(p1 1)~ and ~(p2 1)~ with the original ~eval-sequence~?  What would the values be with Cy's proposed change to ~eval-sequence~?

c. Cy also points out that changing ~eval-sequence~ as he proposes does not affect the behavior of the example in part a.  Explain why this is true.

d. How do you think sequences ought to be treated in the lazy evaluator?  Do you like Cy's approach, the approach in the text, or some other approach?

**** Exercise 4.31
:properties:
:custom_id: exercise-4.31
:end:

The approach taken in this section is somewhat unpleasant, because it makes an incompatible change to Scheme.  It might be nicer to implement lazy evaluation as an <<i416>> upward-compatible extension, that is, so that ordinary Scheme programs will work as before.  We can do this by extending the syntax of procedure declarations to let the user control whether or not arguments are to be delayed.  While we're at it, we may as well also give the user the choice between delaying with and without memoization.  For example, the definition

#+begin_src scheme
(define (f a (b lazy) c (d lazy-memo))
  ...)
#+end_src

would define ~f~ to be a procedure of four arguments, where the first and third arguments are evaluated when the procedure is called, the second argument is delayed, and the fourth argument is both delayed and memoized.  Thus, ordinary procedure definitions will produce the same behavior as ordinary Scheme, while adding the ~lazy-memo~ declaration to each parameter of every compound procedure will produce the behavior of the lazy evaluator defined in this section.  Design and implement the changes required to produce such an extension to Scheme.  You will have to implement new syntax procedures to handle the new syntax for ~define~.  You must also arrange for ~eval~ or ~apply~ to determine when arguments are to be delayed, and to force or delay arguments accordingly, and you must arrange for forcing to memoize or not, as appropriate.

*** 4.2.3 Streams as Lazy Lists
:properties:
:custom_id: section-4.2.3
:end:

In section [[#section-3.5.1][3.5.1]], we showed how to implement streams as delayed lists.  We introduced special forms ~delay~ and ~cons-stream~, which allowed us to construct a "promise" to compute the ~cdr~ of a stream, without actually fulfilling that promise until later.  We could use this general technique of introducing special forms whenever we need more control over the evaluation process, but this is awkward.  For one thing, a special form is not a first-class object like a procedure, so we cannot use it together with higher-order procedures.[fn:243] Additionally, we were forced to create streams as a new kind of data object similar but not identical to lists, and this required us to reimplement many ordinary list operations (~map~, ~append~, and so on) for use with streams.

With lazy evaluation, streams and lists can be identical, so there is no need for special forms or for separate list and stream operations.  All we need to do is to arrange matters so that ~cons~ is non-strict.  One way to accomplish this is to extend the lazy evaluator to allow for non-strict primitives, and to implement ~cons~ as one of these.  An easier way is to recall (section [[#section-2.1.3][2.1.3]]) that there is no fundamental need to implement ~cons~ as a primitive at all.  Instead, we can represent pairs as procedures:[fn:244]

#+begin_src scheme
(define (cons x y)
  (lambda (m) (m x y)))

(define (car z)
  (z (lambda (p q) p)))

(define (cdr z)
  (z (lambda (p q) q)))
#+end_src

In terms of these basic operations, the standard definitions of the list operations will work with infinite lists (streams) as well as finite ones, and the stream operations can be implemented as list operations.  Here are some examples:

#+begin_src scheme
(define (list-ref items n)
  (if (= n 0)
      (car items)
      (list-ref (cdr items) (- n 1))))

(define (map proc items)
  (if (null? items)
      '()
      (cons (proc (car items))
            (map proc (cdr items)))))

(define (scale-list items factor)
  (map (lambda (x) (* x factor))
       items))

(define (add-lists list1 list2)
  (cond ((null? list1) list2)
        ((null? list2) list1)
        (else (cons (+ (car list1) (car list2))
                    (add-lists (cdr list1) (cdr list2))))))

(define ones (cons 1 ones))

(define integers (cons 1 (add-lists ones integers)))

;;; L-Eval input:
(list-ref integers 17)
;;; L-Eval value:
18
#+end_src

Note that these lazy lists are even lazier than the streams of [[#section-3][Chapter 3]]: The ~car~ of the list, as well as the ~cdr~, is delayed.[fn:245] In fact, even accessing the ~car~ or ~cdr~ of a lazy pair need not force the value of a list element.  The value will be forced only when it is really needed - e.g., for use as the argument of a primitive, or to be printed as an answer.

Lazy pairs also help with the problem that arose with streams in section [[#section-3.5.4][3.5.4]], where we found that formulating stream models of systems with loops may require us to sprinkle our programs with explicit ~delay~ operations, beyond the ones supplied by ~cons-stream~.  With lazy evaluation, all arguments to procedures are delayed uniformly.  For instance, we can implement procedures to integrate lists and solve differential equations as we originally intended in section [[#section-3.5.4][3.5.4]]:

#+begin_src scheme
(define (integral integrand initial-value dt)
  (define int
    (cons initial-value
          (add-lists (scale-list integrand dt)
                     int)))
  int)

(define (solve f y0 dt)
  (define y (integral dy y0 dt))
  (define dy (map f y))
  y)

;;; L-Eval input:
(list-ref (solve (lambda (x) x) 1 0.001) 1000)
;;; L-Eval value:
2.716924
#+end_src

**** Exercise 4.32
:properties:
:custom_id: exercise-4.32
:end:

Give some examples that illustrate the difference between the streams of [[#section-3][Chapter 3]] and the "lazier" lazy lists described in this section.  How can you take advantage of this extra laziness?

**** Exercise 4.33
:properties:
:custom_id: exercise-4.33
:end:

Ben Bitdiddle tests the lazy list implementation given above by evaluating the expression

#+begin_src scheme
(car '(a b c))
#+end_src

To his surprise, this produces an error.  After some thought, he realizes that the "lists" obtained by reading in quoted expressions are different from the lists manipulated by the new definitions of ~cons~, ~car~, and ~cdr~.  Modify the evaluator's treatment of quoted expressions so that quoted lists typed at the driver loop will produce true lazy lists.

**** Exercise 4.34
:properties:
:custom_id: exercise-4.34
:end:

Modify the driver loop for the evaluator so that lazy pairs and lists will print in some reasonable way.  (What are you going to do about infinite lists?)  You may also need to modify the representation of lazy pairs so that the evaluator can identify them in order to print them.

** 4.3 Variations on a Scheme -- Nondeterministic Computing
:properties:
:custom_id: section-4.3
:end:

In this section, we extend the Scheme evaluator to support a programming paradigm called <<i261>> nondeterministic computing by building into the evaluator a facility to support automatic search.  This is a much more profound change to the language than the introduction of lazy evaluation in section [[#section-4.2][4.2]].

Nondeterministic computing, like stream processing, is useful for "generate and test" applications.  Consider the task of starting with two lists of positive integers and finding a pair of integers--one from the first list and one from the second list--whose sum is prime.  We saw how to handle this with finite sequence operations in section [[#section-2.2.3][2.2.3]] and with infinite streams in section [[#section-3.5.3][3.5.3]].  Our approach was to generate the sequence of all possible pairs and filter these to select the pairs whose sum is prime.  Whether we actually generate the entire sequence of pairs first as in [[#section-2][Chapter 2]], or interleave the generating and filtering as in [[#section-3][Chapter 3]], is immaterial to the essential image of how the computation is organized.

The nondeterministic approach evokes a different image.  Imagine simply that we choose (in some way) a number from the first list and a number from the second list and require (using some mechanism) that their sum be prime.  This is expressed by following procedure:

#+begin_src scheme
(define (prime-sum-pair list1 list2)
  (let ((a (an-element-of list1))
        (b (an-element-of list2)))
    (require (prime? (+ a b)))
    (list a b)))
#+end_src

It might seem as if this procedure merely restates the problem, rather than specifying a way to solve it.  Nevertheless, this is a legitimate nondeterministic program.[fn:246]

The key idea here is that expressions in a nondeterministic language can have more than one possible value.  For instance, ~an-element-of~ might return any element of the given list.  Our nondeterministic program evaluator will work by automatically choosing a possible value and keeping track of the choice.  If a subsequent requirement is not met, the evaluator will try a different choice, and it will keep trying new choices until the evaluation succeeds, or until we run out of choices.  Just as the lazy evaluator freed the programmer from the details of how values are delayed and forced, the nondeterministic program evaluator will free the programmer from the details of how choices are made.

It is instructive to contrast the different images of time evoked by nondeterministic evaluation and stream processing.  Stream processing uses lazy evaluation to decouple the time when the stream of possible answers is assembled from the time when the actual stream elements are produced.  The evaluator supports the illusion that all the possible answers are laid out before us in a timeless sequence.  With nondeterministic evaluation, an expression represents the exploration of a set of possible worlds, each determined by a set of choices.  Some of the possible worlds lead to dead ends, while others have useful values.  The nondeterministic program evaluator supports the illusion that time branches, and that our programs have different possible execution histories.  When we reach a dead end, we can revisit a previous choice point and proceed along a different branch.

The nondeterministic program evaluator implemented below is called the ~amb~ evaluator because it is based on a new special form called ~amb~.  We can type the above definition of ~prime-sum-pair~ at the ~amb~ evaluator driver loop (along with definitions of ~prime?~, ~an-element-of~, and ~require~) and run the procedure as follows:

#+begin_src scheme
;;; Amb-Eval input:
(prime-sum-pair '(1 3 5 8) '(20 35 110))
;;; Starting a new problem
;;; Amb-Eval value:
(3 20)
#+end_src

The value returned was obtained after the evaluator repeatedly chose elements from each of the lists, until a successful choice was made.

Section [[#section-4.3.1][4.3.1]] introduces ~amb~ and explains how it supports nondeterminism through the evaluator's automatic search mechanism.  Section [[#section-4.3.2][4.3.2]] presents examples of nondeterministic programs, and section [[#section-4.3.3][4.3.3]] gives the details of how to implement the ~amb~ evaluator by modifying the ordinary Scheme evaluator.

*** 4.3.1 Amb and Search
:properties:
:custom_id: section-4.3.1
:end:

To extend Scheme to support nondeterminism, we introduce a new special form called ~amb~.[fn:247] The expression

#+begin_src scheme
(amb <E_1> <E_2> ... <E_N>)
#+end_src

returns the value of one of the n expressions <E_I> "ambiguously."  For example, the expression

#+begin_src scheme
(list (amb 1 2 3) (amb 'a 'b))
#+end_src

can have six possible values:

#+begin_example
(1 a)
(1 b)
(2 a)
(2 b)
(3 a)
(3 b)
#+end_example

~amb~ with a single choice produces an ordinary (single) value.

~amb~ with no choices--the expression ~(amb)~--is an expression with no acceptable values.  Operationally, we can think of ~(amb)~ as an expression that when evaluated causes the computation to "fail": The computation aborts and no value is produced.  Using this idea, we can express the requirement that a particular predicate expression ~p~ must be true as follows:

#+begin_src scheme
(define (require p)
  (if (not p) (amb)))
#+end_src

With ~amb~ and ~require~, we can implement the ~an-element-of~ procedure used above:

#+begin_src scheme
(define (an-element-of items)
  (require (not (null? items)))
  (amb (car items) (an-element-of (cdr items))))
#+end_src

~an-element-of~ fails if the list is empty.  Otherwise it ambiguously returns either the first element of the list or an element chosen from the rest of the list.

We can also express infinite ranges of choices.  The following procedure potentially returns any integer greater than or equal to some given n:

#+begin_src scheme
(define (an-integer-starting-from n)
  (amb n (an-integer-starting-from (+ n 1))))
#+end_src

This is like the stream procedure ~integers-starting-from~ described in section [[#section-3.5.2][3.5.2]], but with an important difference: The stream procedure returns an object that represents the sequence of all integers beginning with n, whereas the ~amb~ procedure returns a single integer.[fn:248]

Abstractly, we can imagine that evaluating an ~amb~ expression causes time to split into branches, where the computation continues on each branch with one of the possible values of the expression.  We say that ~amb~ represents a <<i259>> nondeterministic choice point.  If we had a machine with a sufficient number of processors that could be dynamically allocated, we could implement the search in a straightforward way.  Execution would proceed as in a sequential machine, until an ~amb~ expression is encountered.  At this point, more processors would be allocated and initialized to continue all of the parallel executions implied by the choice.  Each processor would proceed sequentially as if it were the only choice, until it either terminates by encountering a failure, or it further subdivides, or it finishes.[fn:249]

On the other hand, if we have a machine that can execute only one process (or a few concurrent processes), we must consider the alternatives sequentially.  One could imagine modifying an evaluator to pick at random a branch to follow whenever it encounters a choice point.  Random choice, however, can easily lead to failing values.  We might try running the evaluator over and over, making random choices and hoping to find a non-failing value, but it is better to <<i384>> systematically search all possible execution paths.  The ~amb~ evaluator that we will develop and work with in this section implements a systematic search as follows: When the evaluator encounters an application of ~amb~, it initially selects the first alternative.  This selection may itself lead to a further choice.  The evaluator will always initially choose the first alternative at each choice point.  If a choice results in a failure, then the evaluator automagically[fn:250] <<i31>> backtracks to the most recent choice point and tries the next alternative.  If it runs out of alternatives at any choice point, the evaluator will back up to the previous choice point and resume from there.  This process leads to a search strategy known as <<i110>> depth-first search or <<i57>> chronological backtracking.[fn:251]

*Driver loop*

The driver loop for the ~amb~ evaluator has some unusual properties.  It reads an expression and prints the value of the first non-failing execution, as in the ~prime-sum-pair~ example shown above.  If we want to see the value of the next successful execution, we can ask the interpreter to backtrack and attempt to generate a second non-failing execution.  This is signaled by typing the symbol ~try-again~.  If any expression except ~try-again~ is given, the interpreter will start a new problem, discarding the unexplored alternatives in the previous problem.  Here is a sample interaction:

#+begin_src scheme
;;; Amb-Eval input:
(prime-sum-pair '(1 3 5 8) '(20 35 110))
;;; Starting a new problem
;;; Amb-Eval value:
(3 20)

;;; Amb-Eval input:
try-again
;;; Amb-Eval value:
(3 110)

;;; Amb-Eval input:
try-again
;;; Amb-Eval value:
(8 35)

;;; Amb-Eval input:
try-again
;;; There are no more values of
(prime-sum-pair (quote (1 3 5 8)) (quote (20 35 110)))

;;; Amb-Eval input:
(prime-sum-pair '(19 27 30) '(11 36 58))
;;; Starting a new problem
;;; Amb-Eval value:
(30 11)
#+end_src

**** Exercise 4.35
:properties:
:custom_id: exercise-4.35
:end:

Write a procedure ~an-integer-between~ that returns an integer between two given bounds.  This can be used to implement a procedure that finds Pythagorean triples, i.e., triples of integers (i,j,k) between the given bounds such that i <= j and i^2 + j^2 = k^2, as follows:

#+begin_src scheme
(define (a-pythagorean-triple-between low high)
  (let ((i (an-integer-between low high)))
    (let ((j (an-integer-between i high)))
      (let ((k (an-integer-between j high)))
        (require (= (+ (* i i) (* j j)) (* k k)))
        (list i j k)))))
#+end_src

**** Exercise 4.36
:properties:
:custom_id: exercise-4.36
:end:

[[#exercise-3.69][Exercise 3.69]] discussed how to generate the stream of /all/ Pythagorean triples, with no upper bound on the size of the integers to be searched.  Explain why simply replacing ~an-integer-between~ by ~an-integer-starting-from~ in the procedure in [[#exercise-4.35][Exercise 4.35]] is not an adequate way to generate arbitrary Pythagorean triples.  Write a procedure that actually will accomplish this.  (That is, write a procedure for which repeatedly typing ~try-again~ would in principle eventually generate all Pythagorean triples.)

**** Exercise 4.37
:properties:
:custom_id: exercise-4.37
:end:

Ben Bitdiddle claims that the following method for generating Pythagorean triples is more efficient than the one in [[#exercise-4.35][Exercise 4.35]].  Is he correct?  (Hint: Consider the number of possibilities that must be explored.)

#+begin_src scheme
(define (a-pythagorean-triple-between low high)
  (let ((i (an-integer-between low high))
        (hsq (* high high)))
    (let ((j (an-integer-between i high)))
      (let ((ksq (+ (* i i) (* j j))))
        (require (>= hsq ksq))
        (let ((k (sqrt ksq)))
          (require (integer? k))
          (list i j k))))))
#+end_src

*** 4.3.2 Examples of Nondeterministic Programs
:properties:
:custom_id: section-4.3.2
:end:

Section [[#section-4.3.3][4.3.3]] describes the implementation of the ~amb~ evaluator.  First, however, we give some examples of how it can be used.  The advantage of nondeterministic programming is that we can suppress the details of how search is carried out, thereby expressing our programs at a higher level of abstraction.

*Logic Puzzles*

The following puzzle (taken from Dinesman 1968) is typical of a large class of simple logic puzzles:

#+begin_quote
Baker, Cooper, Fletcher, Miller, and Smith live on different floors of an apartment house that contains only five floors.  Baker does not live on the top floor.  Cooper does not live on the bottom floor.  Fletcher does not live on either the top or the bottom floor.  Miller lives on a higher floor than does Cooper.  Smith does not live on a floor adjacent to Fletcher's.  Fletcher does not live on a floor adjacent to Cooper's.  Where does everyone live?
#+end_quote

We can determine who lives on each floor in a straightforward way by enumerating all the possibilities and imposing the given restrictions:[fn:252]

#+begin_src scheme
(define (multiple-dwelling)
  (let ((baker (amb 1 2 3 4 5))
        (cooper (amb 1 2 3 4 5))
        (fletcher (amb 1 2 3 4 5))
        (miller (amb 1 2 3 4 5))
        (smith (amb 1 2 3 4 5)))
    (require
     (distinct? (list baker cooper fletcher miller smith)))
    (require (not (= baker 5)))
    (require (not (= cooper 1)))
    (require (not (= fletcher 5)))
    (require (not (= fletcher 1)))
    (require (> miller cooper))
    (require (not (= (abs (- smith fletcher)) 1)))
    (require (not (= (abs (- fletcher cooper)) 1)))
    (list (list 'baker baker)
          (list 'cooper cooper)
          (list 'fletcher fletcher)
          (list 'miller miller)
          (list 'smith smith))))
#+end_src

Evaluating the expression ~(multiple-dwelling)~ produces the result

#+begin_src scheme
((baker 3) (cooper 2) (fletcher 4) (miller 5) (smith 1))
#+end_src

Although this simple procedure works, it is very slow.  [[#exercise-4.39][Exercise 4.39]] and [[#exercise-4.40][Exercise 4.40]] discuss some possible improvements.

**** Exercise 4.38
:properties:
:custom_id: exercise-4.38
:end:

Modify the multiple-dwelling procedure to omit the requirement that Smith and Fletcher do not live on adjacent floors.  How many solutions are there to this modified puzzle?

**** Exercise 4.39
:properties:
:custom_id: exercise-4.39
:end:

Does the order of the restrictions in the multiple-dwelling procedure affect the answer?  Does it affect the time to find an answer?  If you think it matters, demonstrate a faster program obtained from the given one by reordering the restrictions.  If you think it does not matter, argue your case.

**** Exercise 4.40
:properties:
:custom_id: exercise-4.40
:end:

In the multiple dwelling problem, how many sets of assignments are there of people to floors, both before and after the requirement that floor assignments be distinct?  It is very inefficient to generate all possible assignments of people to floors and then leave it to backtracking to eliminate them.  For example, most of the restrictions depend on only one or two of the person-floor variables, and can thus be imposed before floors have been selected for all the people.  Write and demonstrate a much more efficient nondeterministic procedure that solves this problem based upon generating only those possibilities that are not already ruled out by previous restrictions.  (Hint: This will require a nest of ~let~ expressions.)

**** Exercise 4.41
:properties:
:custom_id: exercise-4.41
:end:

Write an ordinary Scheme program to solve the multiple dwelling puzzle.

**** Exercise 4.42
:properties:
:custom_id: exercise-4.42
:end:

Solve the following "Liars" puzzle (from Phillips 1934):

Five schoolgirls sat for an examination.  Their parents--so they thought--showed an undue degree of interest in the result.  They therefore agreed that, in writing home about the examination, each girl should make one true statement and one untrue one.  The following are the relevant passages from their letters:

- Betty: "Kitty was second in the examination.  I was only third."
- Ethel: "You'll be glad to hear that I was on top.  Joan was second."
- Joan: "I was third, and poor old Ethel was bottom."
- Kitty: "I came out second.  Mary was only fourth."
- Mary: "I was fourth.  Top place was taken by Betty."

What in fact was the order in which the five girls were placed?

**** Exercise 4.43
:properties:
:custom_id: exercise-4.43
:end:

Use the ~amb~ evaluator to solve the following puzzle:[fn:253]

Mary Ann Moore's father has a yacht and so has each of his four friends: Colonel Downing, Mr.  Hall, Sir Barnacle Hood, and Dr.  Parker.  Each of the five also has one daughter and each has named his yacht after a daughter of one of the others.  Sir Barnacle's yacht is the Gabrielle, Mr.  Moore owns the Lorna; Mr.  Hall the Rosalind.  The Melissa, owned by Colonel Downing, is named after Sir Barnacle's daughter.  Gabrielle's father owns the yacht that is named after Dr.  Parker's daughter.  Who is Lorna's father?

Try to write the program so that it runs efficiently (see [[#exercise-4.40][Exercise 4.40]]).  Also determine how many solutions there are if we are not told that Mary Ann's last name is Moore.

**** Exercise 4.44
:properties:
:custom_id: exercise-4.44
:end:

[[#exercise-2.42][Exercise 2.42]] described the "eight-queens puzzle" of placing queens on a chessboard so that no two attack each other.  Write a nondeterministic program to solve this puzzle.

*Parsing natural language*

Programs designed to accept natural language as input usually start by attempting to <<i280>> parse the input, that is, to match the input against some grammatical structure.  For example, we might try to recognize simple sentences consisting of an article followed by a noun followed by a verb, such as "The cat eats."  To accomplish such an analysis, we must be able to identify the parts of speech of individual words.  We could start with some lists that classify various words:[fn:254]

#+begin_src scheme
(define nouns '(noun student professor cat class))

(define verbs '(verb studies lectures eats sleeps))

(define articles '(article the a))
#+end_src

We also need a <<i169>> grammar, that is, a set of rules describing how grammatical elements are composed from simpler elements.  A very simple grammar might stipulate that a sentence always consists of two pieces--a noun phrase followed by a verb--and that a noun phrase consists of an article followed by a noun.  With this grammar, the sentence "The cat eats" is parsed as follows:

#+begin_src scheme
(sentence (noun-phrase (article the) (noun cat))
          (verb eats))
#+end_src

We can generate such a parse with a simple program that has separate procedures for each of the grammatical rules.  To parse a sentence, we identify its two constituent pieces and return a list of these two elements, tagged with the symbol ~sentence~:

#+begin_src scheme
(define (parse-sentence)
  (list 'sentence
        (parse-noun-phrase)
        (parse-word verbs)))
#+end_src

A noun phrase, similarly, is parsed by finding an article followed by a noun:

#+begin_src scheme
(define (parse-noun-phrase)
  (list 'noun-phrase
        (parse-word articles)
        (parse-word nouns)))
#+end_src

At the lowest level, parsing boils down to repeatedly checking that the next unparsed word is a member of the list of words for the required part of speech.  To implement this, we maintain a global variable ~*unparsed*~, which is the input that has not yet been parsed.  Each time we check a word, we require that ~*unparsed*~ must be non-empty and that it should begin with a word from the designated list.  If so, we remove that word from ~*unparsed*~ and return the word together with its part of speech (which is found at the head of the list):[fn:255]

#+begin_src scheme
(define (parse-word word-list)
  (require (not (null? *unparsed*)))
  (require (memq (car *unparsed*) (cdr word-list)))
  (let ((found-word (car *unparsed*)))
    (set! *unparsed* (cdr *unparsed*))
    (list (car word-list) found-word)))
#+end_src

To start the parsing, all we need to do is set ~*unparsed*~ to be the entire input, try to parse a sentence, and check that nothing is left over:

#+begin_src scheme
(define *unparsed* '())

(define (parse input)
  (set! *unparsed* input)
  (let ((sent (parse-sentence)))
    (require (null? *unparsed*))
    sent))
#+end_src

We can now try the parser and verify that it works for our simple test sentence:

#+begin_src scheme
;;; Amb-Eval input:
(parse '(the cat eats))
;;; Starting a new problem
;;; Amb-Eval value:
(sentence (noun-phrase (article the) (noun cat)) (verb eats))
#+end_src

The ~amb~ evaluator is useful here because it is convenient to express the parsing constraints with the aid of ~require~.  Automatic search and backtracking really pay off, however, when we consider more complex grammars where there are choices for how the units can be decomposed.

Let's add to our grammar a list of prepositions:

#+begin_src scheme
(define prepositions '(prep for to in by with))
#+end_src

and define a prepositional phrase (e.g., "for the cat") to be a preposition followed by a noun phrase:

#+begin_src scheme
(define (parse-prepositional-phrase)
  (list 'prep-phrase
        (parse-word prepositions)
        (parse-noun-phrase)))
#+end_src

Now we can define a sentence to be a noun phrase followed by a verb phrase, where a verb phrase can be either a verb or a verb phrase extended by a prepositional phrase:[fn:256]

#+begin_src scheme
(define (parse-sentence)
  (list 'sentence
        (parse-noun-phrase)
        (parse-verb-phrase)))

(define (parse-verb-phrase)
  (define (maybe-extend verb-phrase)
    (amb verb-phrase
         (maybe-extend (list 'verb-phrase
                             verb-phrase
                             (parse-prepositional-phrase)))))
  (maybe-extend (parse-word verbs)))
#+end_src

While we're at it, we can also elaborate the definition of noun phrases to permit such things as "a cat in the class."  What we used to call a noun phrase, we'll now call a simple noun phrase, and a noun phrase will now be either a simple noun phrase or a noun phrase extended by a prepositional phrase:

#+begin_src scheme
(define (parse-simple-noun-phrase)
  (list 'simple-noun-phrase
        (parse-word articles)
        (parse-word nouns)))

(define (parse-noun-phrase)
  (define (maybe-extend noun-phrase)
    (amb noun-phrase
         (maybe-extend (list 'noun-phrase
                             noun-phrase
                             (parse-prepositional-phrase)))))
  (maybe-extend (parse-simple-noun-phrase)))
#+end_src

Our new grammar lets us parse more complex sentences.  For example

#+begin_src scheme
(parse '(the student with the cat sleeps in the class))
#+end_src

produces

#+begin_src scheme
(sentence
 (noun-phrase
  (simple-noun-phrase (article the) (noun student))
  (prep-phrase (prep with)
               (simple-noun-phrase
                (article the) (noun cat))))
 (verb-phrase
  (verb sleeps)
  (prep-phrase (prep in)
               (simple-noun-phrase
                (article the) (noun class)))))
#+end_src

Observe that a given input may have more than one legal parse.  In the sentence "The professor lectures to the student with the cat," it may be that the professor is lecturing with the cat, or that the student has the cat.  Our nondeterministic program finds both possibilities:

#+begin_src scheme
(parse '(the professor lectures to the student with the cat))
#+end_src

produces

#+begin_src scheme
(sentence
 (simple-noun-phrase (article the) (noun professor))
 (verb-phrase
  (verb-phrase
   (verb lectures)
   (prep-phrase (prep to)
                (simple-noun-phrase
                 (article the) (noun student))))
  (prep-phrase (prep with)
               (simple-noun-phrase
                (article the) (noun cat)))))
#+end_src

Asking the evaluator to try again yields

#+begin_src scheme
(sentence
 (simple-noun-phrase (article the) (noun professor))
 (verb-phrase
  (verb lectures)
  (prep-phrase (prep to)
               (noun-phrase
                (simple-noun-phrase
                 (article the) (noun student))
                (prep-phrase (prep with)
                             (simple-noun-phrase
                              (article the) (noun cat)))))))
#+end_src

**** Exercise 4.45
:properties:
:custom_id: exercise-4.45
:end:

With the grammar given above, the following sentence can be parsed in five different ways: "The professor lectures to the student in the class with the cat."  Give the five parses and explain the differences in shades of meaning among them.

**** Exercise 4.46
:properties:
:custom_id: exercise-4.46
:end:

The evaluators in sections [[#section-4.1][4.1]] and [[#section-4.2][4.2]] do not determine what order operands are evaluated in.  We will see that the ~amb~ evaluator evaluates them from left to right.  Explain why our parsing program wouldn't work if the operands were evaluated in some other order.

**** Exercise 4.47
:properties:
:custom_id: exercise-4.47
:end:

Louis Reasoner suggests that, since a verb phrase is either a verb or a verb phrase followed by a prepositional phrase, it would be much more straightforward to define the procedure ~parse-verb-phrase~ as follows (and similarly for noun phrases):

#+begin_src scheme
(define (parse-verb-phrase)
  (amb (parse-word verbs)
       (list 'verb-phrase
             (parse-verb-phrase)
             (parse-prepositional-phrase))))
#+end_src

Does this work?  Does the program's behavior change if we interchange the order of expressions in the ~amb~

**** Exercise 4.48
:properties:
:custom_id: exercise-4.48
:end:

Extend the grammar given above to handle more complex sentences.  For example, you could extend noun phrases and verb phrases to include adjectives and adverbs, or you could handle compound sentences.[fn:257]

**** Exercise 4.49
:properties:
:custom_id: exercise-4.49
:end:

Alyssa P. Hacker is more interested in generating interesting sentences than in parsing them.  She reasons that by simply changing the procedure ~parse-word~ so that it ignores the "input sentence" and instead always succeeds and generates an appropriate word, we can use the programs we had built for parsing to do generation instead.  Implement Alyssa's idea, and show the first half-dozen or so sentences generated.[fn:258]

*** 4.3.3 Implementing the ~amb~ Evaluator
:properties:
:custom_id: section-4.3.3
:end:

The evaluation of an ordinary Scheme expression may return a value, may never terminate, or may signal an error.  In nondeterministic Scheme the evaluation of an expression may in addition result in the discovery of a dead end, in which case evaluation must backtrack to a previous choice point.  The interpretation of nondeterministic Scheme is complicated by this extra case.

We will construct the ~amb~ evaluator for nondeterministic Scheme by modifying the analyzing evaluator of section [[#section-4.1.7][4.1.7]].[fn:259] As in the analyzing evaluator, evaluation of an expression is accomplished by calling an execution procedure produced by analysis of that expression.  The difference between the interpretation of ordinary Scheme and the interpretation of nondeterministic Scheme will be entirely in the execution procedures.

*Execution procedures and continuations*

Recall that the execution procedures for the ordinary evaluator take one argument: the environment of execution.  In contrast, the execution procedures in the ~amb~ evaluator take three arguments: the environment, and two procedures called <<i84>> continuation procedures.  The evaluation of an expression will finish by calling one of these two continuations: If the evaluation results in a value, the <<i377>> success continuation is called with that value; if the evaluation results in the discovery of a dead end, the <<i135>> failure continuation is called.  Constructing and calling appropriate continuations is the mechanism by which the nondeterministic evaluator implements backtracking.

It is the job of the success continuation to receive a value and proceed with the computation.  Along with that value, the success continuation is passed another failure continuation, which is to be called subsequently if the use of that value leads to a dead end.

It is the job of the failure continuation to try another branch of the nondeterministic process.  The essence of the nondeterministic language is in the fact that expressions may represent choices among alternatives.  The evaluation of such an expression must proceed with one of the indicated alternative choices, even though it is not known in advance which choices will lead to acceptable results.  To deal with this, the evaluator picks one of the alternatives and passes this value to the success continuation.  Together with this value, the evaluator constructs and passes along a failure continuation that can be called later to choose a different alternative.

A failure is triggered during evaluation (that is, a failure continuation is called) when a user program explicitly rejects the current line of attack (for example, a call to ~require~ may result in execution of ~(amb)~, an expression that always fails--see section [[#section-4.3.1][4.3.1]]).  The failure continuation in hand at that point will cause the most recent choice point to choose another alternative.  If there are no more alternatives to be considered at that choice point, a failure at an earlier choice point is triggered, and so on.  Failure continuations are also invoked by the driver loop in response to a ~try-again~ request, to find another value of the expression.

In addition, if a side-effect operation (such as assignment to a variable) occurs on a branch of the process resulting from a choice, it may be necessary, when the process finds a dead end, to undo the side effect before making a new choice.  This is accomplished by having the side-effect operation produce a failure continuation that undoes the side effect and propagates the failure.

In summary, failure continuations are constructed by

- ~amb~ expressions--to provide a mechanism to make alternative choices if the current choice made by the ~amb~ expression leads to a dead end;

- the top-level driver--to provide a mechanism to report failure when the choices are exhausted;

- assignments--to intercept failures and undo assignments during backtracking.

Failures are initiated only when a dead end is encountered.  This occurs

- if the user program executes ~(amb)~;
- if the user types ~try-again~ at the top-level driver.

Failure continuations are also called during processing of a failure:

- When the failure continuation created by an assignment finishes undoing a side effect, it calls the failure continuation it intercepted, in order to propagate the failure back to the choice point that led to this assignment or to the top level.

- When the failure continuation for an ~amb~ runs out of choices, it calls the failure continuation that was originally given to the ~amb~, in order to propagate the failure back to the previous choice point or to the top level.

*Structure of the evaluator*

The syntax- and data-representation procedures for the ~amb~ evaluator, and also the basic ~analyze~ procedure, are identical to those in the evaluator of section [[#section-4.1.7][4.1.7]], except for the fact that we need additional syntax procedures to recognize the ~amb~ special form:[fn:260]

#+begin_src scheme
(define (amb? exp) (tagged-list? exp 'amb))

(define (amb-choices exp) (cdr exp))
#+end_src

We must also add to the dispatch in ~analyze~ a clause that will recognize this special form and generate an appropriate execution procedure:

#+begin_src scheme
((amb? exp) (analyze-amb exp))
#+end_src

The top-level procedure ~ambeval~ (similar to the version of ~eval~ given in section [[#section-4.1.7][4.1.7]]) analyzes the given expression and applies the resulting execution procedure to the given environment, together with two given continuations:

#+begin_src scheme
(define (ambeval exp env succeed fail)
  ((analyze exp) env succeed fail))
#+end_src

A success continuation is a procedure of two arguments: the value just obtained and another failure continuation to be used if that value leads to a subsequent failure.  A failure continuation is a procedure of no arguments.  So the general form of an execution procedure is

#+begin_src scheme
(lambda (env succeed fail)
  ;; ~succeed~ is '(lambda (value fail) ...)'
  ;; ~fail~ is '(lambda () ...)'
  ...)
#+end_src

For example, executing

#+begin_src scheme
(ambeval <EXP>
         the-global-environment
         (lambda (value fail) value)
         (lambda () 'failed))
#+end_src

will attempt to evaluate the given expression and will return either the expression's value (if the evaluation succeeds) or the symbol ~failed~ (if the evaluation fails).  The call to ~ambeval~ in the driver loop shown below uses much more complicated continuation procedures, which continue the loop and support the ~try-again~ request.

Most of the complexity of the ~amb~ evaluator results from the mechanics of passing the continuations around as the execution procedures call each other.  In going through the following code, you should compare each of the execution procedures with the corresponding procedure for the ordinary evaluator given in section [[#section-4.1.7][4.1.7]].

*Simple expressions*

The execution procedures for the simplest kinds of expressions are essentially the same as those for the ordinary evaluator, except for the need to manage the continuations.  The execution procedures simply succeed with the value of the expression, passing along the failure continuation that was passed to them.

#+begin_src scheme
(define (analyze-self-evaluating exp)
  (lambda (env succeed fail)
    (succeed exp fail)))

(define (analyze-quoted exp)
  (let ((qval (text-of-quotation exp)))
    (lambda (env succeed fail)
      (succeed qval fail))))

(define (analyze-variable exp)
  (lambda (env succeed fail)
    (succeed (lookup-variable-value exp env)
             fail)))

(define (analyze-lambda exp)
  (let ((vars (lambda-parameters exp))
        (bproc (analyze-sequence (lambda-body exp))))
    (lambda (env succeed fail)
      (succeed (make-procedure vars bproc env)
               fail))))
#+end_src

Notice that looking up a variable always "succeeds."  If ~lookup-variable-value~ fails to find the variable, it signals an error, as usual.  Such a "failure" indicates a program bug--a reference to an unbound variable; it is not an indication that we should try another nondeterministic choice instead of the one that is currently being tried.

*Conditionals and sequences*

Conditionals are also handled in a similar way as in the ordinary evaluator.  The execution procedure generated by ~analyze-if~ invokes the predicate execution procedure ~pproc~ with a success continuation that checks whether the predicate value is true and goes on to execute either the consequent or the alternative.  If the execution of ~pproc~ fails, the original failure continuation for the ~if~ expression is called.

#+begin_src scheme
(define (analyze-if exp)
  (let ((pproc (analyze (if-predicate exp)))
        (cproc (analyze (if-consequent exp)))
        (aproc (analyze (if-alternative exp))))
    (lambda (env succeed fail)
      (pproc env
             ;; success continuation for evaluating the predicate
             ;; to obtain ~pred-value~
             (lambda (pred-value fail2)
               (if (true? pred-value)
                   (cproc env succeed fail2)
                   (aproc env succeed fail2)))
             ;; failure continuation for evaluating the predicate
             fail))))
#+end_src

Sequences are also handled in the same way as in the previous evaluator, except for the machinations in the subprocedure ~sequentially~ that are required for passing the continuations.  Namely, to sequentially execute ~a~ and then ~b~, we call ~a~ with a success continuation that calls ~b~.

#+begin_src scheme
(define (analyze-sequence exps)
  (define (sequentially a b)
    (lambda (env succeed fail)
      (a env
         ;; success continuation for calling ~a~
         (lambda (a-value fail2)
           (b env succeed fail2))
         ;; failure continuation for calling ~a~
         fail)))
  (define (loop first-proc rest-procs)
    (if (null? rest-procs)
        first-proc
        (loop (sequentially first-proc (car rest-procs))
              (cdr rest-procs))))
  (let ((procs (map analyze exps)))
    (if (null? procs)
        (error "Empty sequence - ANALYZE"))
    (loop (car procs) (cdr procs))))
#+end_src

*Definitions and assignments*

Definitions are another case where we must go to some trouble to manage the continuations, because it is necessary to evaluate the definition-value expression before actually defining the new variable.  To accomplish this, the definition-value execution procedure ~vproc~ is called with the environment, a success continuation, and the failure continuation.  If the execution of ~vproc~ succeeds, obtaining a value ~val~ for the defined variable, the variable is defined and the success is propagated:

#+begin_src scheme
(define (analyze-definition exp)
  (let ((var (definition-variable exp))
        (vproc (analyze (definition-value exp))))
    (lambda (env succeed fail)
      (vproc env
             (lambda (val fail2)
               (define-variable! var val env)
               (succeed 'ok fail2))
             fail))))
#+end_src

Assignments are more interesting.  This is the first place where we really use the continuations, rather than just passing them around.  The execution procedure for assignments starts out like the one for definitions.  It first attempts to obtain the new value to be assigned to the variable.  If this evaluation of ~vproc~ fails, the assignment fails.

If ~vproc~ succeeds, however, and we go on to make the assignment, we must consider the possibility that this branch of the computation might later fail, which will require us to backtrack out of the assignment.  Thus, we must arrange to undo the assignment as part of the backtracking process.[fn:261]

This is accomplished by giving ~vproc~ a success continuation (marked with the comment "*1*" below) that saves the old value of the variable before assigning the new value to the variable and proceeding from the assignment.  The failure continuation that is passed along with the value of the assignment (marked with the comment "*2*" below) restores the old value of the variable before continuing the failure.  That is, a successful assignment provides a failure continuation that will intercept a subsequent failure; whatever failure would otherwise have called ~fail2~ calls this procedure instead, to undo the assignment before actually calling ~fail2~.

#+begin_src scheme
(define (analyze-assignment exp)
  (let ((var (assignment-variable exp))
        (vproc (analyze (assignment-value exp))))
    (lambda (env succeed fail)
      (vproc env
             (lambda (val fail2)        ; *1*
               (let ((old-value
                      (lookup-variable-value var env)))
                 (set-variable-value! var val env)
                 (succeed 'ok
                          (lambda ()    ; *2*
                            (set-variable-value! var
                                                 old-value
                                                 env)
                            (fail2)))))
             fail))))
#+end_src

*Procedure applications*

The execution procedure for applications contains no new ideas except for the technical complexity of managing the continuations.  This complexity arises in ~analyze-application~, due to the need to keep track of the success and failure continuations as we evaluate the operands.  We use a procedure ~get-args~ to evaluate the list of operands, rather than a simple ~map~ as in the ordinary evaluator.

#+begin_src scheme
(define (analyze-application exp)
  (let ((fproc (analyze (operator exp)))
        (aprocs (map analyze (operands exp))))
    (lambda (env succeed fail)
      (fproc env
             (lambda (proc fail2)
               (get-args aprocs
                         env
                         (lambda (args fail3)
                           (execute-application
                            proc args succeed fail3))
                         fail2))
             fail))))
#+end_src

In ~get-args~, notice how 'cdr'ing down the list of ~aproc~ execution procedures and 'cons'ing up the resulting list of ~args~ is accomplished by calling each ~aproc~ in the list with a success continuation that recursively calls ~get-args~.  Each of these recursive calls to ~get-args~ has a success continuation whose value is the ~cons~ of the newly obtained argument onto the list of accumulated arguments:

#+begin_src scheme
(define (get-args aprocs env succeed fail)
  (if (null? aprocs)
      (succeed '() fail)
      ((car aprocs) env
       ;; success continuation for this ~aproc~
       (lambda (arg fail2)
         (get-args (cdr aprocs)
                   env
                   ;; success continuation for recursive
                   ;; call to ~get-args~
                   (lambda (args fail3)
                     (succeed (cons arg args)
                              fail3))
                   fail2))
       fail)))
#+end_src

The actual procedure application, which is performed by ~execute-application~, is accomplished in the same way as for the ordinary evaluator, except for the need to manage the continuations.

#+begin_src scheme
(define (execute-application proc args succeed fail)
  (cond ((primitive-procedure? proc)
         (succeed (apply-primitive-procedure proc args)
                  fail))
        ((compound-procedure? proc)
         ((procedure-body proc)
          (extend-environment (procedure-parameters proc)
                              args
                              (procedure-environment proc))
          succeed
          fail))
        (else
         (error
          "Unknown procedure type - EXECUTE-APPLICATION"
          proc))))
#+end_src

*Evaluating ~amb~ expressions*

The ~amb~ special form is the key element in the nondeterministic language.  Here we see the essence of the interpretation process and the reason for keeping track of the continuations.  The execution procedure for ~amb~ defines a loop ~try-next~ that cycles through the execution procedures for all the possible values of the ~amb~ expression.  Each execution procedure is called with a failure continuation that will try the next one.  When there are no more alternatives to try, the entire ~amb~ expression fails.

#+begin_src scheme
(define (analyze-amb exp)
  (let ((cprocs (map analyze (amb-choices exp))))
    (lambda (env succeed fail)
      (define (try-next choices)
        (if (null? choices)
            (fail)
            ((car choices) env
             succeed
             (lambda ()
               (try-next (cdr choices))))))
      (try-next cprocs))))
#+end_src

*Driver loop*

The driver loop for the ~amb~ evaluator is complex, due to the mechanism that permits the user to try again in evaluating an expression.  The driver uses a procedure called ~internal-loop~, which takes as argument a procedure ~try-again~.  The intent is that calling ~try-again~ should go on to the next untried alternative in the nondeterministic evaluation.  ~internal-loop~ either calls ~try-again~ in response to the user typing ~try-again~ at the driver loop, or else starts a new evaluation by calling ~ambeval~.

The failure continuation for this call to ~ambeval~ informs the user that there are no more values and re-invokes the driver loop.

The success continuation for the call to ~ambeval~ is more subtle.  We print the obtained value and then invoke the internal loop again with a ~try-again~ procedure that will be able to try the next alternative.  This ~next-alternative~ procedure is the second argument that was passed to the success continuation.  Ordinarily, we think of this second argument as a failure continuation to be used if the current evaluation branch later fails.  In this case, however, we have completed a successful evaluation, so we can invoke the "failure" alternative branch in order to search for additional successful evaluations.

#+begin_src scheme
(define input-prompt ";;; Amb-Eval input:")
(define output-prompt ";;; Amb-Eval value:")

(define (driver-loop)
  (define (internal-loop try-again)
    (prompt-for-input input-prompt)
    (let ((input (read)))
      (if (eq? input 'try-again)
          (try-again)
          (begin
            (newline)
            (display ";;; Starting a new problem ")
            (ambeval input
                     the-global-environment
                     ;; ~ambeval~ success
                     (lambda (val next-alternative)
                       (announce-output output-prompt)
                       (user-print val)
                       (internal-loop next-alternative))
                     ;; ~ambeval~ failure
                     (lambda ()
                       (announce-output
                        ";;; There are no more values of")
                       (user-print input)
                       (driver-loop)))))))
  (internal-loop
   (lambda ()
     (newline)
     (display ";;; There is no current problem")
     (driver-loop))))
#+end_src

The initial call to ~internal-loop~ uses a ~try-again~ procedure that complains that there is no current problem and restarts the driver loop.  This is the behavior that will happen if the user types ~try-again~ when there is no evaluation in progress.

**** Exercise 4.50
:properties:
:custom_id: exercise-4.50
:end:

Implement a new special form ~ramb~ that is like ~amb~ except that it searches alternatives in a random order, rather than from left to right.  Show how this can help with Alyssa's problem in [[#exercise-4.49][Exercise 4.49]].

**** Exercise 4.51
:properties:
:custom_id: exercise-4.51
:end:

Implement a new kind of assignment called ~permanent-set!~ that is not undone upon failure.  For example, we can choose two distinct elements from a list and count the number of trials required to make a successful choice as follows:

#+begin_src scheme
(define count 0)

(let ((x (an-element-of '(a b c)))
      (y (an-element-of '(a b c))))
  (permanent-set! count (+ count 1))
  (require (not (eq? x y)))
  (list x y count))
;;; Starting a new problem
;;; Amb-Eval value:
(a b 2)

;;; Amb-Eval input:
try-again
;;; Amb-Eval value:
(a c 3)
#+end_src

What values would have been displayed if we had used ~set!~ here rather than ~permanent-set!~ ?

**** Exercise 4.52
:properties:
:custom_id: exercise-4.52
:end:

Implement a new construct called ~if-fail~ that permits the user to catch the failure of an expression.  ~if-fail~ takes two expressions.  It evaluates the first expression as usual and returns as usual if the evaluation succeeds.  If the evaluation fails, however, the value of the second expression is returned, as in the following example:

#+begin_src scheme
;;; Amb-Eval input:
(if-fail (let ((x (an-element-of '(1 3 5))))
           (require (even? x))
           x)
         'all-odd)
;;; Starting a new problem
;;; Amb-Eval value:
all-odd

;;; Amb-Eval input:
(if-fail (let ((x (an-element-of '(1 3 5 8))))
           (require (even? x))
           x)
         'all-odd)
;;; Starting a new problem
;;; Amb-Eval value:
8
#+end_src

**** Exercise 4.53
:properties:
:custom_id: exercise-4.53
:end:

With ~permanent-set!~ as described in [[#exercise-4.51][Exercise 4.51]] and ~if-fail~ as in [[#exercise-4.52][Exercise 4.52]], what will be the result of evaluating

#+begin_src scheme
(let ((pairs '()))
  (if-fail (let ((p (prime-sum-pair '(1 3 5 8) '(20 35 110))))
             (permanent-set! pairs (cons p pairs))
             (amb))
           pairs))
#+end_src

**** Exercise 4.54
:properties:
:custom_id: exercise-4.54
:end:

If we had not realized that ~require~ could be implemented as an ordinary procedure that uses ~amb~, to be defined by the user as part of a nondeterministic program, we would have had to implement it as a special form.  This would require syntax procedures

#+begin_src scheme
(define (require? exp) (tagged-list? exp 'require))

(define (require-predicate exp) (cadr exp))
#+end_src

and a new clause in the dispatch in ~analyze~

#+begin_src scheme
((require? exp) (analyze-require exp))
#+end_src

as well the procedure ~analyze-require~ that handles ~require~ expressions.  Complete the following definition of ~analyze-require~.

#+begin_src scheme
(define (analyze-require exp)
  (let ((pproc (analyze (require-predicate exp))))
    (lambda (env succeed fail)
      (pproc env
             (lambda (pred-value fail2)
               (if <??>
                   <??>
                   (succeed 'ok fail2)))
             fail))))
#+end_src

** 4.4 Logic Programming
:properties:
:custom_id: section-4.4
:end:

In [[#section-1][Chapter 1]] we stressed that computer science deals with imperative (how to) knowledge, whereas mathematics deals with declarative (what is) knowledge.  Indeed, programming languages require that the programmer express knowledge in a form that indicates the step-by-step methods for solving particular problems.  On the other hand, high-level languages provide, as part of the language implementation, a substantial amount of methodological knowledge that frees the user from concern with numerous details of how a specified computation will progress.

Most programming languages, including Lisp, are organized around computing the values of mathematical functions.  Expression-oriented languages (such as Lisp, Fortran, and Algol) capitalize on the "pun" that an expression that describes the value of a function may also be interpreted as a means of computing that value.  Because of this, most programming languages are strongly biased toward unidirectional computations (computations with well-defined inputs and outputs).  There are, however, radically different programming languages that relax this bias.  We saw one such example in section [[#section-3.3.5][3.3.5]], where the objects of computation were arithmetic constraints.  In a constraint system the direction and the order of computation are not so well specified; in carrying out a computation the system must therefore provide more detailed "how to" knowledge than would be the case with an ordinary arithmetic computation.  This does not mean, however, that the user is released altogether from the responsibility of providing imperative knowledge.  There are many constraint networks that implement the same set of constraints, and the user must choose from the set of mathematically equivalent networks a suitable network to specify a particular computation.

The nondeterministic program evaluator of section [[#section-4.3][4.3]] also moves away from the view that programming is about constructing algorithms for computing unidirectional functions.  In a nondeterministic language, expressions can have more than one value, and, as a result, the computation is dealing with relations rather than with single-valued functions.  Logic programming extends this idea by combining a relational vision of programming with a powerful kind of symbolic pattern matching called <<i410>> unification.[fn:262]

This approach, when it works, can be a very powerful way to write programs.  Part of the power comes from the fact that a single "what is" fact can be used to solve a number of different problems that would have different "how to" components.  As an example, consider the ~append~ operation, which takes two lists as arguments and combines their elements to form a single list.  In a procedural language such as Lisp, we could define ~append~ in terms of the basic list constructor ~cons~, as we did in section [[#section-2.2.1][2.2.1]]:

#+begin_src scheme
(define (append x y)
  (if (null? x)
      y
      (cons (car x) (append (cdr x) y))))
#+end_src

This procedure can be regarded as a translation into Lisp of the following two rules, the first of which covers the case where the first list is empty and the second of which handles the case of a nonempty list, which is a ~cons~ of two parts:

- For any list ~y~, the empty list and ~y~ ~append~ to form ~y~.

- For any ~u~, ~v~, ~y~, and ~z~, ~(cons u v)~ and ~y~ ~append~ to form ~(cons u z)~ if ~v~ and ~y~ ~append~ to form ~z~.[fn:263]

Using the ~append~ procedure, we can answer questions such as

#+begin_example
Find the ~append~ of ~(a b)~ and ~(c d)~.
#+end_example

But the same two rules are also sufficient for answering the following sorts of questions, which the procedure can't answer:

#+begin_example
Find a list ~y~ that ~append~s with ~(a b)~ to produce ~(a
b c d)~.

Find all ~x~ and ~y~ that ~append~ to form ~(a b c d)~.
#+end_example

In a logic programming language, the programmer writes an ~append~ "procedure" by stating the two rules about ~append~ given above.  "How to" knowledge is provided automatically by the interpreter to allow this single pair of rules to be used to answer all three types of questions about ~append~.[fn:264]

Contemporary logic programming languages (including the one we implement here) have substantial deficiencies, in that their general "how to" methods can lead them into spurious infinite loops or other undesirable behavior.  Logic programming is an active field of research in computer science.[fn:265]

Earlier in this chapter we explored the technology of implementing interpreters and described the elements that are essential to an interpreter for a Lisp-like language (indeed, to an interpreter for any conventional language).  Now we will apply these ideas to discuss an interpreter for a logic programming language.  We call this language the <<i313>> query language, because it is very useful for retrieving information from data bases by formulating <<i312>> queries, or questions, expressed in the language.  Even though the query language is very different from Lisp, we will find it convenient to describe the language in terms of the same general framework we have been using all along: as a collection of primitive elements, together with means of combination that enable us to combine simple elements to create more complex elements and means of abstraction that enable us to regard complex elements as single conceptual units.  An interpreter for a logic programming language is considerably more complex than an interpreter for a language like Lisp.  Nevertheless, we will see that our query-language interpreter contains many of the same elements found in the interpreter of section [[#section-4.1][4.1]].  In particular, there will be an "eval" part that classifies expressions according to type and an "apply" part that implements the language's abstraction mechanism (procedures in the case of Lisp, and <<i339>> rules in the case of logic programming).  Also, a central role is played in the implementation by a frame data structure, which determines the correspondence between symbols and their associated values.  One additional interesting aspect of our query-language implementation is that we make substantial use of streams, which were introduced in [[#section-3][Chapter 3]].

*** 4.4.1 Deductive Information Retrieval
:properties:
:custom_id: section-4.4.1
:end:

Logic programming excels in providing interfaces to data bases for information retrieval.  The query language we shall implement in this chapter is designed to be used in this way.

In order to illustrate what the query system does, we will show how it can be used to manage the data base of personnel records for Microshaft, a thriving high-technology company in the Boston area.  The language provides pattern-directed access to personnel information and can also take advantage of general rules in order to make logical deductions.

*A sample data base*

The personnel data base for Microshaft contains <<i23>> assertions about company personnel.  Here is the information about Ben Bitdiddle, the resident computer wizard:

#+begin_src scheme
(address (Bitdiddle Ben) (Slumerville (Ridge Road) 10))
(job (Bitdiddle Ben) (computer wizard))
(salary (Bitdiddle Ben) 60000)
#+end_src

Each assertion is a list (in this case a triple) whose elements can themselves be lists.

As resident wizard, Ben is in charge of the company's computer division, and he supervises two programmers and one technician.  Here is the information about them:

#+begin_src scheme
(address (Hacker Alyssa P) (Cambridge (Mass Ave) 78))
(job (Hacker Alyssa P) (computer programmer))
(salary (Hacker Alyssa P) 40000)
(supervisor (Hacker Alyssa P) (Bitdiddle Ben))

(address (Fect Cy D) (Cambridge (Ames Street) 3))
(job (Fect Cy D) (computer programmer))
(salary (Fect Cy D) 35000)
(supervisor (Fect Cy D) (Bitdiddle Ben))

(address (Tweakit Lem E) (Boston (Bay State Road) 22))
(job (Tweakit Lem E) (computer technician))
(salary (Tweakit Lem E) 25000)
(supervisor (Tweakit Lem E) (Bitdiddle Ben))
#+end_src

There is also a programmer trainee, who is supervised by Alyssa:

#+begin_src scheme
(address (Reasoner Louis) (Slumerville (Pine Tree Road) 80))
(job (Reasoner Louis) (computer programmer trainee))
(salary (Reasoner Louis) 30000)
(supervisor (Reasoner Louis) (Hacker Alyssa P))
#+end_src

All of these people are in the computer division, as indicated by the word ~computer~ as the first item in their job descriptions.

Ben is a high-level employee.  His supervisor is the company's big wheel himself:

#+begin_src scheme
(supervisor (Bitdiddle Ben) (Warbucks Oliver))

(address (Warbucks Oliver) (Swellesley (Top Heap Road)))
(job (Warbucks Oliver) (administration big wheel))
(salary (Warbucks Oliver) 150000)
#+end_src

Besides the computer division supervised by Ben, the company has an accounting division, consisting of a chief accountant and his assistant:

#+begin_src scheme
(address (Scrooge Eben) (Weston (Shady Lane) 10))
(job (Scrooge Eben) (accounting chief accountant))
(salary (Scrooge Eben) 75000)
(supervisor (Scrooge Eben) (Warbucks Oliver))

(address (Cratchet Robert) (Allston (N Harvard Street) 16))
(job (Cratchet Robert) (accounting scrivener))
(salary (Cratchet Robert) 18000)
(supervisor (Cratchet Robert) (Scrooge Eben))
#+end_src

There is also a secretary for the big wheel:

#+begin_src scheme
(address (Aull DeWitt) (Slumerville (Onion Square) 5))
(job (Aull DeWitt) (administration secretary))
(salary (Aull DeWitt) 25000)
(supervisor (Aull DeWitt) (Warbucks Oliver))
#+end_src

The data base also contains assertions about which kinds of jobs can be done by people holding other kinds of jobs.  For instance, a computer wizard can do the jobs of both a computer programmer and a computer technician:

#+begin_src scheme
(can-do-job (computer wizard) (computer programmer))
(can-do-job (computer wizard) (computer technician))
#+end_src

A computer programmer could fill in for a trainee:

#+begin_src scheme
(can-do-job (computer programmer)
            (computer programmer trainee))
#+end_src

Also, as is well known,

#+begin_src scheme
(can-do-job (administration secretary)
            (administration big wheel))
#+end_src

*Simple queries*

The query language allows users to retrieve information from the data base by posing queries in response to the system's prompt.  For example, to find all computer programmers one can say

#+begin_src scheme
;;; Query input:
(job ?x (computer programmer))
#+end_src

The system will respond with the following items:

#+begin_src scheme
;;; Query results:
(job (Hacker Alyssa P) (computer programmer))
(job (Fect Cy D) (computer programmer))
#+end_src

The input query specifies that we are looking for entries in the data base that match a certain <<i282>> pattern.  In this example, the pattern specifies entries consisting of three items, of which the first is the literal symbol ~job~, the second can be anything, and the third is the literal list ~(computer programmer)~.  The "anything" that can be the second item in the matching list is specified by a <<i285>> pattern variable, ~?x~.  The general form of a pattern variable is a symbol, taken to be the name of the variable, preceded by a question mark.  We will see below why it is useful to specify names for pattern variables rather than just putting ~?~ into patterns to represent "anything."  The system responds to a simple query by showing all entries in the data base that match the specified pattern.

A pattern can have more than one variable.  For example, the query

#+begin_src scheme
(address ?x ?y)
#+end_src

will list all the employees' addresses.

A pattern can have no variables, in which case the query simply determines whether that pattern is an entry in the data base.  If so, there will be one match; if not, there will be no matches.

The same pattern variable can appear more than once in a query, specifying that the same "anything" must appear in each position.  This is why variables have names.  For example,

#+begin_src scheme
(supervisor ?x ?x)
#+end_src

finds all people who supervise themselves (though there are no such assertions in our sample data base).

The query

#+begin_src scheme
(job ?x (computer ?type))
#+end_src

matches all job entries whose third item is a two-element list whose first item is ~computer~:

#+begin_src scheme
(job (Bitdiddle Ben) (computer wizard))
(job (Hacker Alyssa P) (computer programmer))
(job (Fect Cy D) (computer programmer))
(job (Tweakit Lem E) (computer technician))
#+end_src

This same pattern does /not/ match

#+begin_src scheme
(job (Reasoner Louis) (computer programmer trainee))
#+end_src

because the third item in the entry is a list of three elements, and the pattern's third item specifies that there should be two elements.  If we wanted to change the pattern so that the third item could be any list beginning with ~computer~, we could specify[fn:266]

#+begin_src scheme
(job ?x (computer . ?type))
#+end_src

For example,

#+begin_src scheme
(computer . ?type)
#+end_src

matches the data

#+begin_src scheme
(computer programmer trainee)
#+end_src

with ~?type~ as the list ~(programmer trainee)~.  It also matches the data

#+begin_src scheme
(computer programmer)
#+end_src

with ~?type~ as the list ~(programmer)~, and matches the data

#+begin_src scheme
(computer)
#+end_src

with ~?type~ as the empty list ~()~.

We can describe the query language's processing of simple queries as follows:

- The system finds all assignments to variables in the query pattern that <<i341>> satisfy the pattern--that is, all sets of values for the variables such that if the pattern variables are <<i183>> instantiated with (replaced by) the values, the result is in the data base.

- The system responds to the query by listing all instantiations of the query pattern with the variable assignments that satisfy it.

Note that if the pattern has no variables, the query reduces to a determination of whether that pattern is in the data base.  If so, the empty assignment, which assigns no values to variables, satisfies that pattern for that data base.

**** Exercise 4.55
:properties:
:custom_id: exercise-4.55
:end:

Give simple queries that retrieve the following information from the data base:

1. all people supervised by Ben Bitdiddle;
2. the names and jobs of all people in the accounting division;
3. the names and addresses of all people who live in Slumerville.

*Compound queries*

Simple queries form the primitive operations of the query language.  In order to form compound operations, the query language provides means of combination.  One thing that makes the query language a logic programming language is that the means of combination mirror the means of combination used in forming logical expressions: ~and~, ~or~, and ~not~.  (Here ~and~, ~or~, and ~not~ are not the Lisp primitives, but rather operations built into the query language.)

We can use ~and~ as follows to find the addresses of all the computer programmers:

#+begin_src scheme
(and (job ?person (computer programmer))
     (address ?person ?where))
#+end_src

The resulting output is

#+begin_src scheme
(and (job (Hacker Alyssa P) (computer programmer))
     (address (Hacker Alyssa P) (Cambridge (Mass Ave) 78)))

(and (job (Fect Cy D) (computer programmer))
     (address (Fect Cy D) (Cambridge (Ames Street) 3)))
#+end_src

In general,

#+begin_src scheme
(and <QUERY_1> <QUERY_2> ... <QUERY_N>)
#+end_src

is satisfied by all sets of values for the pattern variables that simultaneously satisfy <QUERY_1> ... <QUERY_N>.

As for simple queries, the system processes a compound query by finding all assignments to the pattern variables that satisfy the query, then displaying instantiations of the query with those values.

Another means of constructing compound queries is through ~or~.  For example,

#+begin_src scheme
(or (supervisor ?x (Bitdiddle Ben))
    (supervisor ?x (Hacker Alyssa P)))
#+end_src

will find all employees supervised by Ben Bitdiddle or Alyssa P. Hacker:

#+begin_src scheme
(or (supervisor (Hacker Alyssa P) (Bitdiddle Ben))
    (supervisor (Hacker Alyssa P) (Hacker Alyssa P)))

(or (supervisor (Fect Cy D) (Bitdiddle Ben))
    (supervisor (Fect Cy D) (Hacker Alyssa P)))

(or (supervisor (Tweakit Lem E) (Bitdiddle Ben))
    (supervisor (Tweakit Lem E) (Hacker Alyssa P)))

(or (supervisor (Reasoner Louis) (Bitdiddle Ben))
    (supervisor (Reasoner Louis) (Hacker Alyssa P)))
#+end_src

In general,

#+begin_src scheme
(or <QUERY_1> <QUERY_2> ... <QUERY_N>)
#+end_src

is satisfied by all sets of values for the pattern variables that satisfy at least one of <QUERY_1> ... <QUERY_N>.

Compound queries can also be formed with ~not~.  For example,

#+begin_src scheme
(and (supervisor ?x (Bitdiddle Ben))
     (not (job ?x (computer programmer))))
#+end_src

finds all people supervised by Ben Bitdiddle who are not computer programmers.  In general,

#+begin_src scheme
(not <QUERY_1>)
#+end_src

is satisfied by all assignments to the pattern variables that do not satisfy <QUERY_1>.[fn:267]

The final combining form is called ~lisp-value~.  When ~lisp-value~ is the first element of a pattern, it specifies that the next element is a Lisp predicate to be applied to the rest of the (instantiated) elements as arguments.  In general,

#+begin_src scheme
(lisp-value <PREDICATE> <ARG_1> ... <ARG_N>)
#+end_src

will be satisfied by assignments to the pattern variables for which the <PREDICATE> applied to the instantiated <ARG_1> ... <ARG_N> is true.  For example, to find all people whose salary is greater than $30,000 we could write[fn:268]

#+begin_src scheme
(and (salary ?person ?amount)
     (lisp-value > ?amount 30000))
#+end_src

**** Exercise 4.56
:properties:
:custom_id: exercise-4.56
:end:

Formulate compound queries that retrieve the following information:

a. the names of all people who are supervised by Ben Bitdiddle, together with their addresses;

b. all people whose salary is less than Ben Bitdiddle's, together with their salary and Ben Bitdiddle's salary;

c. all people who are supervised by someone who is not in the computer division, together with the supervisor's name and job.

*Rules*

In addition to primitive queries and compound queries, the query language provides means for abstracting queries.  These are given by <<i340>> rules.  The rule

#+begin_src scheme
(rule (lives-near ?person-1 ?person-2)
      (and (address ?person-1 (?town . ?rest-1))
           (address ?person-2 (?town . ?rest-2))
           (not (same ?person-1 ?person-2))))
#+end_src

specifies that two people live near each other if they live in the same town.  The final ~not~ clause prevents the rule from saying that all people live near themselves.  The ~same~ relation is defined by a very simple rule:[fn:269]

#+begin_src scheme
(rule (same ?x ?x))
#+end_src

The following rule declares that a person is a "wheel" in an organization if he supervises someone who is in turn a supervisor:

#+begin_src scheme
(rule (wheel ?person)
      (and (supervisor ?middle-manager ?person)
           (supervisor ?x ?middle-manager)))
#+end_src

The general form of a rule is

#+begin_src scheme
(rule <CONCLUSION> <BODY>)
#+end_src

where <CONCLUSION> is a pattern and <BODY> is any query.[fn:270] We can think of a rule as representing a large (even infinite) set of assertions, namely all instantiations of the rule conclusion with variable assignments that satisfy the rule body.  When we described simple queries (patterns), we said that an assignment to variables satisfies a pattern if the instantiated pattern is in the data base.  But the pattern needn't be explicitly in the data base as an assertion.  It can be an implicit assertion implied by a rule.  For example, the query

#+begin_src scheme
(lives-near ?x (Bitdiddle Ben))
#+end_src

results in

#+begin_src scheme
(lives-near (Reasoner Louis) (Bitdiddle Ben))
(lives-near (Aull DeWitt) (Bitdiddle Ben))
#+end_src

To find all computer programmers who live near Ben Bitdiddle, we can ask

#+begin_src scheme
(and (job ?x (computer programmer))
     (lives-near ?x (Bitdiddle Ben)))
#+end_src

As in the case of compound procedures, rules can be used as parts of other rules (as we saw with the ~lives-near~ rule above) or even be defined recursively.  For instance, the rule

#+begin_src scheme
(rule (outranked-by ?staff-person ?boss)
      (or (supervisor ?staff-person ?boss)
          (and (supervisor ?staff-person ?middle-manager)
               (outranked-by ?middle-manager ?boss))))
#+end_src

says that a staff person is outranked by a boss in the organization if the boss is the person's supervisor or (recursively) if the person's supervisor is outranked by the boss.

**** Exercise 4.57
:properties:
:custom_id: exercise-4.57
:end:

Define a rule that says that person 1 can replace person 2 if either person 1 does the same job as person 2 or someone who does person 1's job can also do person 2's job, and if person 1 and person 2 are not the same person.  Using your rule, give queries that find the following:

a. all people who can replace Cy D. Fect;

b. all people who can replace someone who is being paid more than they are, together with the two salaries.

**** Exercise 4.58
:properties:
:custom_id: exercise-4.58
:end:

Define a rule that says that a person is a "big shot" in a division if the person works in the division but does not have a supervisor who works in the division.

**** Exercise 4.59
:properties:
:custom_id: exercise-4.59
:end:

Ben Bitdiddle has missed one meeting too many.  Fearing that his habit of forgetting meetings could cost him his job, Ben decides to do something about it.  He adds all the weekly meetings of the firm to the Microshaft data base by asserting the following:

#+begin_src scheme
(meeting accounting (Monday 9am))
(meeting administration (Monday 10am))
(meeting computer (Wednesday 3pm))
(meeting administration (Friday 1pm))
#+end_src

Each of the above assertions is for a meeting of an entire division.  Ben also adds an entry for the company-wide meeting that spans all the divisions.  All of the company's employees attend this meeting.

#+begin_src scheme
(meeting whole-company (Wednesday 4pm))
#+end_src

a. On Friday morning, Ben wants to query the data base for all the meetings that occur that day.  What query should he use?

b. Alyssa P. Hacker is unimpressed.  She thinks it would be much more useful to be able to ask for her meetings by specifying her name.  So she designs a rule that says that a person's meetings include all ~whole-company~ meetings plus all meetings of that person's division.  Fill in the body of Alyssa's rule.

#+begin_src scheme
(rule (meeting-time ?person ?day-and-time)
      <RULE-BODY>)
#+end_src

c. Alyssa arrives at work on Wednesday morning and wonders what meetings she has to attend that day.  Having defined the above rule, what query should she make to find this out?

**** Exercise 4.60
:properties:
:custom_id: exercise-4.60
:end:

By giving the query

#+begin_src scheme
(lives-near ?person (Hacker Alyssa P))
#+end_src

Alyssa P. Hacker is able to find people who live near her, with whom she can ride to work.  On the other hand, when she tries to find all pairs of people who live near each other by querying

#+begin_src scheme
(lives-near ?person-1 ?person-2)
#+end_src

she notices that each pair of people who live near each other is listed twice; for example,

#+begin_src scheme
(lives-near (Hacker Alyssa P) (Fect Cy D))
(lives-near (Fect Cy D) (Hacker Alyssa P))
#+end_src

Why does this happen?  Is there a way to find a list of people who live near each other, in which each pair appears only once?  Explain.

*Logic as programs*

We can regard a rule as a kind of logical implication: /If/ an assignment of values to pattern variables satisfies the body, /then/ it satisfies the conclusion.  Consequently, we can regard the query language as having the ability to perform <<i221>> logical deductions based upon the rules.  As an example, consider the ~append~ operation described at the beginning of section [[#section-4.4][4.4]].  As we said, ~append~ can be characterized by the following two rules:

- For any list ~y~, the empty list and ~y~ ~append~ to form ~y~.

- For any ~u~, ~v~, ~y~, and ~z~, ~(cons u v)~ and ~y~ ~append~ to form ~(cons u z)~ if ~v~ and ~y~ ~append~ to form ~z~.

To express this in our query language, we define two rules for a relation

#+begin_src scheme
(append-to-form x y z)
#+end_src

which we can interpret to mean "~x~ and ~y~ ~append~ to form ~z~":

#+begin_src scheme
(rule (append-to-form () ?y ?y))

(rule (append-to-form (?u . ?v) ?y (?u . ?z))
      (append-to-form ?v ?y ?z))
#+end_src

The first rule has no body, which means that the conclusion holds for any value of ~?y~.  Note how the second rule makes use of dotted-tail notation to name the ~car~ and ~cdr~ of a list.

Given these two rules, we can formulate queries that compute the ~append~ of two lists:

#+begin_src scheme
;;; Query input:
(append-to-form (a b) (c d) ?z)
;;; Query results:
(append-to-form (a b) (c d) (a b c d))
#+end_src

What is more striking, we can use the same rules to ask the question "Which list, when 'append'ed to ~(a b)~, yields ~(a b c d)~?"  This is done as follows:

#+begin_src scheme
;;; Query input:
(append-to-form (a b) ?y (a b c d))
;;; Query results:
(append-to-form (a b) (c d) (a b c d))
#+end_src

We can also ask for all pairs of lists that ~append~ to form ~(a b c d)~:

#+begin_src scheme
;;; Query input:
(append-to-form ?x ?y (a b c d))
;;; Query results:
(append-to-form () (a b c d) (a b c d))
(append-to-form (a) (b c d) (a b c d))
(append-to-form (a b) (c d) (a b c d))
(append-to-form (a b c) (d) (a b c d))
(append-to-form (a b c d) () (a b c d))
#+end_src

The query system may seem to exhibit quite a bit of intelligence in using the rules to deduce the answers to the queries above.  Actually, as we will see in the next section, the system is following a well-determined algorithm in unraveling the rules.  Unfortunately, although the system works impressively in the ~append~ case, the general methods may break down in more complex cases, as we will see in section [[#section-4.4.3][4.4.3]].

**** Exercise 4.61
:properties:
:custom_id: exercise-4.61
:end:

The following rules implement a ~next-to~ relation that finds adjacent elements of a list:

#+begin_src scheme
(rule (?x next-to ?y in (?x ?y . ?u)))

(rule (?x next-to ?y in (?v . ?z))
      (?x next-to ?y in ?z))
#+end_src

What will the response be to the following queries?

#+begin_src scheme
(?x next-to ?y in (1 (2 3) 4))

(?x next-to 1 in (2 1 3 1))
#+end_src

**** Exercise 4.62
:properties:
:custom_id: exercise-4.62
:end:

Define rules to implement the ~last-pair~ operation of [[#exercise-2.17][Exercise 2.17]], which returns a list containing the last element of a nonempty list.  Check your rules on queries such as ~(last-pair (3) ?x)~, ~(last-pair (1 2 3) ?x)~, and ~(last-pair (2 ?x) (3))~.  Do your rules work correctly on queries such as ~(last-pair ?x (3))~ ?

**** Exercise 4.63
:properties:
:custom_id: exercise-4.63
:end:

The following data base (see Genesis 4) traces the genealogy of the descendants of Ada back to Adam, by way of Cain:

#+begin_src scheme
(son Adam Cain)
(son Cain Enoch)
(son Enoch Irad)
(son Irad Mehujael)
(son Mehujael Methushael)
(son Methushael Lamech)
(wife Lamech Ada)
(son Ada Jabal)
(son Ada Jubal)
#+end_src

Formulate rules such as "If S is the son of f, and f is the son of G, then S is the grandson of G" and "If W is the wife of M, and S is the son of W, then S is the son of M" (which was supposedly more true in biblical times than today) that will enable the query system to find the grandson of Cain; the sons of Lamech; the grandsons of Methushael.  (See [[#exercise-4.69][Exercise 4.69]] for some rules to deduce more complicated relationships.)

*** 4.4.2 How the Query System Works
:properties:
:custom_id: section-4.4.2
:end:

In section [[#section-4.4.4][4.4.4]] we will present an implementation of the query interpreter as a collection of procedures.  In this section we give an overview that explains the general structure of the system independent of low-level implementation details.  After describing the implementation of the interpreter, we will be in a position to understand some of its limitations and some of the subtle ways in which the query language's logical operations differ from the operations of mathematical logic.

It should be apparent that the query evaluator must perform some kind of search in order to match queries against facts and rules in the data base.  One way to do this would be to implement the query system as a nondeterministic program, using the ~amb~ evaluator of section [[#section-4.3][4.3]] (see [[#exercise-4.78][Exercise 4.78]]).  Another possibility is to manage the search with the aid of streams.  Our implementation follows this second approach.

The query system is organized around two central operations called <<i284>> pattern matching and <<i411>> unification.  We first describe pattern matching and explain how this operation, together with the organization of information in terms of streams of frames, enables us to implement both simple and compound queries.  We next discuss unification, a generalization of pattern matching needed to implement rules.  Finally, we show how the entire query interpreter fits together through a procedure that classifies expressions in a manner analogous to the way ~eval~ classifies expressions for the interpreter described in section [[#section-4.1][4.1]].

*Pattern matching*

A <<i283>> pattern matcher is a program that tests whether some datum fits a specified pattern.  For example, the data list ~((a b) c (a b))~ matches the pattern ~(?x c ?x)~ with the pattern variable ~?x~ bound to ~(a b)~.  The same data list matches the pattern ~(?x ?y ?z)~ with ~?x~ and ~?z~ both bound to ~(a b)~ and ~?y~ bound to ~c~.  It also matches the pattern ~((?x ?y) c (?x ?y))~ with ~?x~ bound to ~a~ and ~?y~ bound to ~b~.  However, it does not match the pattern ~(?x a ?y)~, since that pattern specifies a list whose second element is the symbol ~a~.

The pattern matcher used by the query system takes as inputs a pattern, a datum, and a <<i144>> frame that specifies bindings for various pattern variables.  It checks whether the datum matches the pattern in a way that is consistent with the bindings already in the frame.  If so, it returns the given frame augmented by any bindings that may have been determined by the match.  Otherwise, it indicates that the match has failed.

For example, using the pattern ~(?x ?y ?x)~ to match ~(a b a)~ given an empty frame will return a frame specifying that ~?x~ is bound to ~a~ and ~?y~ is bound to ~b~.  Trying the match with the same pattern, the same datum, and a frame specifying that ~?y~ is bound to ~a~ will fail.  Trying the match with the same pattern, the same datum, and a frame in which ~?y~ is bound to ~b~ and ~?x~ is unbound will return the given frame augmented by a binding of ~?x~ to ~a~.

The pattern matcher is all the mechanism that is needed to process simple queries that don't involve rules.  For instance, to process the query

#+begin_src scheme
(job ?x (computer programmer))
#+end_src

we scan through all assertions in the data base and select those that match the pattern with respect to an initially empty frame.  For each match we find, we use the frame returned by the match to instantiate the pattern with a value for ~?x~.

*Streams of frames*

The testing of patterns against frames is organized through the use of streams.  Given a single frame, the matching process runs through the data-base entries one by one.  For each data-base entry, the matcher generates either a special symbol indicating that the match has failed or an extension to the frame.  The results for all the data-base entries are collected into a stream, which is passed through a filter to weed out the failures.  The result is a stream of all the frames that extend the given frame via a match to some assertion in the data base.[fn:271]

In our system, a query takes an input stream of frames and performs the above matching operation for every frame in the stream, as indicated in [[figure-4.4][Figure 4.4]].  That is, for each frame in the input stream, the query generates a new stream consisting of all extensions to that frame by matches to assertions in the data base.  All these streams are then combined to form one huge stream, which contains all possible extensions of every frame in the input stream.  This stream is the output of the query.

<<figure-4.4>> A query processes a stream of frames.

#+begin_example
                                   output stream
   input stream   +-------------+  of frames,
   of frames      |    query    |  filtered and extended
 ---------------->|             +------------------------->
                  | (job ?x ?y) |
                  +-------------+
                         ^
                         |
                stream of assertions
                   from data base
#+end_example

To answer a simple query, we use the query with an input stream consisting of a single empty frame.  The resulting output stream contains all extensions to the empty frame (that is, all answers to our query).  This stream of frames is then used to generate a stream of copies of the original query pattern with the variables instantiated by the values in each frame, and this is the stream that is finally printed.

*Compound queries*

The real elegance of the stream-of-frames implementation is evident when we deal with compound queries.  The processing of compound queries makes use of the ability of our matcher to demand that a match be consistent with a specified frame.  For example, to handle the ~and~ of two queries, such as

#+begin_src scheme
(and (can-do-job ?x (computer programmer trainee))
     (job ?person ?x))
#+end_src

(informally, "Find all people who can do the job of a computer programmer trainee"), we first find all entries that match the pattern

#+begin_src scheme
(can-do-job ?x (computer programmer trainee))
#+end_src

This produces a stream of frames, each of which contains a binding for ~?x~.  Then for each frame in the stream we find all entries that match

#+begin_src scheme
(job ?person ?x)
#+end_src

in a way that is consistent with the given binding for ~?x~.  Each such match will produce a frame containing bindings for ~?x~ and ~?person~.  The ~and~ of two queries can be viewed as a series combination of the two component queries, as shown in [[figure-4.5][Figure 4.5]].  The frames that pass through the first query filter are filtered and further extended by the second query.

<<figure-4.5>> The ~and~ combination of two queries is produced by operating on the stream of frames in series.

#+begin_example
                 +----------------------+
                 |       (and A B)      |
   input stream  |                      |  output stream
   of frames     |   +---+       +---+  |  of frames
 ------------------->| A +------>| B +-------------------->
                 |   +---+       +---+  |
                 |     ^           ^    |
                 |     |           |    |
                 |     +-----*-----+    |
                 +-----------|----------+
                             |
                         data base
#+end_example

[[figure-4.6][Figure 4.6]] shows the analogous method for computing the ~or~ of two queries as a parallel combination of the two component queries.  The input stream of frames is extended separately by each query.  The two resulting streams are then merged to produce the final output stream.

<<figure-4.6>> The ~or~ combination of two queries is produced by operating on the stream of frames in parallel and merging the results.

#+begin_example
            +---------------------------+
            |          (or A B)         |
            |    +---+                  |
 input      | +->| A |------------+     |  output
 stream of  | |  +---+            V     |  stream of
 frames     | |    ^          +-------+ |  frames
 -------------*    |          | merge +--------------->
            | |    |          +-------+ |
            | |    |              ^     |
            | |    |   +---+      |     |
            | +------->| B +------+     |
            |      |   +---+            |
            |      |     ^              |
            |      |     |              |
            |      +--*--+              |
            +---------|-----------------+
                      |
                  data base
#+end_example

Even from this high-level description, it is apparent that the processing of compound queries can be slow.  For example, since a query may produce more than one output frame for each input frame, and each query in an ~and~ gets its input frames from the previous query, an ~and~ query could, in the worst case, have to perform a number of matches that is exponential in the number of queries (see [[#exercise-4.76][Exercise 4.76]]).[fn:272] Though systems for handling only simple queries are quite practical, dealing with complex queries is extremely difficult.[fn:273]

From the stream-of-frames viewpoint, the ~not~ of some query acts as a filter that removes all frames for which the query can be satisfied.  For instance, given the pattern

#+begin_src scheme
(not (job ?x (computer programmer)))
#+end_src

we attempt, for each frame in the input stream, to produce extension frames that satisfy ~(job ?x (computer programmer))~.  We remove from the input stream all frames for which such extensions exist.  The result is a stream consisting of only those frames in which the binding for ~?x~ does not satisfy ~(job ?x (computer programmer))~.  For example, in processing the query

#+begin_src scheme
(and (supervisor ?x ?y)
     (not (job ?x (computer programmer))))
#+end_src

the first clause will generate frames with bindings for ~?x~ and ~?y~.  The ~not~ clause will then filter these by removing all frames in which the binding for ~?x~ satisfies the restriction that ~?x~ is a computer programmer.[fn:274]

The ~lisp-value~ special form is implemented as a similar filter on frame streams.  We use each frame in the stream to instantiate any variables in the pattern, then apply the Lisp predicate.  We remove from the input stream all frames for which the predicate fails.

*Unification*

In order to handle rules in the query language, we must be able to find the rules whose conclusions match a given query pattern.  Rule conclusions are like assertions except that they can contain variables, so we will need a generalization of pattern matching--called <<i412>> unification--in which both the "pattern" and the "datum" may contain variables.

A unifier takes two patterns, each containing constants and variables, and determines whether it is possible to assign values to the variables that will make the two patterns equal.  If so, it returns a frame containing these bindings.  For example, unifying ~(?x a ?y)~ and ~(?y ?z a)~ will specify a frame in which ~?x~, ~?y~, and ~?z~ must all be bound to ~a~.  On the other hand, unifying ~(?x ?y a)~ and ~(?x b ?y)~ will fail, because there is no value for ~?y~ that can make the two patterns equal.  (For the second elements of the patterns to be equal, ~?y~ would have to be ~b~; however, for the third elements to be equal, ~?y~ would have to be ~a~.)  The unifier used in the query system, like the pattern matcher, takes a frame as input and performs unifications that are consistent with this frame.

The unification algorithm is the most technically difficult part of the query system.  With complex patterns, performing unification may seem to require deduction.  To unify ~(?x ?x)~ and ~((a ?y c) (a b ?z))~, for example, the algorithm must infer that ~?x~ should be ~(a b c)~, ~?y~ should be ~b~, and ~?z~ should be ~c~.  We may think of this process as solving a set of equations among the pattern components.  In general, these are simultaneous equations, which may require substantial manipulation to solve.[fn:275] For example, unifying ~(?x ?x)~ and ~((a ?y c) (a b ?z))~ may be thought of as specifying the simultaneous equations

#+begin_src scheme
?x  =  (a ?y c)
?x  =  (a b ?z)
#+end_src

These equations imply that

#+begin_src scheme
(a ?y c)  =  (a b ?z)
#+end_src

which in turn implies that

#+begin_src scheme
a  =  a, ?y  =  b, c  =  ?z,
#+end_src

and hence that

#+begin_src scheme
?x  =  (a b c)
#+end_src

In a successful pattern match, all pattern variables become bound, and the values to which they are bound contain only constants.  This is also true of all the examples of unification we have seen so far.  In general, however, a successful unification may not completely determine the variable values; some variables may remain unbound and others may be bound to values that contain variables.

Consider the unification of ~(?x a)~ and ~((b ?y) ?z)~.  We can deduce that ~?x = (b ?y)~ and ~a = ?z~, but we cannot further solve for ~?x~ or ~?y~.  The unification doesn't fail, since it is certainly possible to make the two patterns equal by assigning values to ~?x~ and ~?y~.  Since this match in no way restricts the values ~?y~ can take on, no binding for ~?y~ is put into the result frame.  The match does, however, restrict the value of ~?x~.  Whatever value ~?y~ has, ~?x~ must be ~(b ?y)~.  A binding of ~?x~ to the pattern ~(b ?y)~ is thus put into the frame.  If a value for ~?y~ is later determined and added to the frame (by a pattern match or unification that is required to be consistent with this frame), the previously bound ~?x~ will refer to this value.[fn:276]

*Applying rules*

Unification is the key to the component of the query system that makes inferences from rules.  To see how this is accomplished, consider processing a query that involves applying a rule, such as

#+begin_src scheme
(lives-near ?x (Hacker Alyssa P))
#+end_src

To process this query, we first use the ordinary pattern-match procedure described above to see if there are any assertions in the data base that match this pattern.  (There will not be any in this case, since our data base includes no direct assertions about who lives near whom.)  The next step is to attempt to unify the query pattern with the conclusion of each rule.  We find that the pattern unifies with the conclusion of the rule

#+begin_src scheme
(rule (lives-near ?person-1 ?person-2)
      (and (address ?person-1 (?town . ?rest-1))
           (address ?person-2 (?town . ?rest-2))
           (not (same ?person-1 ?person-2))))
#+end_src

resulting in a frame specifying that ~?person-2~ is bound to ~(Hacker Alyssa P)~ and that ~?x~ should be bound to (have the same value as) ~?person-1~.  Now, relative to this frame, we evaluate the compound query given by the body of the rule.  Successful matches will extend this frame by providing a binding for ~?person-1~, and consequently a value for ~?x~, which we can use to instantiate the original query pattern.

In general, the query evaluator uses the following method to apply a rule when trying to establish a query pattern in a frame that specifies bindings for some of the pattern variables:

- Unify the query with the conclusion of the rule to form, if successful, an extension of the original frame.

- Relative to the extended frame, evaluate the query formed by the body of the rule.

Notice how similar this is to the method for applying a procedure in the ~eval~/~apply~ evaluator for Lisp:

- Bind the procedure's parameters to its arguments to form a frame that extends the original procedure environment.

- Relative to the extended environment, evaluate the expression formed by the body of the procedure.

The similarity between the two evaluators should come as no surprise.  Just as procedure definitions are the means of abstraction in Lisp, rule definitions are the means of abstraction in the query language.  In each case, we unwind the abstraction by creating appropriate bindings and evaluating the rule or procedure body relative to these.

*Simple queries*

We saw earlier in this section how to evaluate simple queries in the absence of rules.  Now that we have seen how to apply rules, we can describe how to evaluate simple queries by using both rules and assertions.

Given the query pattern and a stream of frames, we produce, for each frame in the input stream, two streams:

- a stream of extended frames obtained by matching the pattern against all assertions in the data base (using the pattern matcher), and

- a stream of extended frames obtained by applying all possible rules (using the unifier).[fn:277]

Appending these two streams produces a stream that consists of all the ways that the given pattern can be satisfied consistent with the original frame.  These streams (one for each frame in the input stream) are now all combined to form one large stream, which therefore consists of all the ways that any of the frames in the original input stream can be extended to produce a match with the given pattern.

*The query evaluator and the driver loop*

Despite the complexity of the underlying matching operations, the system is organized much like an evaluator for any language.  The procedure that coordinates the matching operations is called ~qeval~, and it plays a role analogous to that of the ~eval~ procedure for Lisp.  ~qeval~ takes as inputs a query and a stream of frames.  Its output is a stream of frames, corresponding to successful matches to the query pattern, that extend some frame in the input stream, as indicated in [[figure-4.4][Figure 4.4]].  Like ~eval~, ~qeval~ classifies the different types of expressions (queries) and dispatches to an appropriate procedure for each.  There is a procedure for each special form (~and~, ~or~, ~not~, and ~lisp-value~) and one for simple queries.

The driver loop, which is analogous to the ~driver-loop~ procedure for the other evaluators in this chapter, reads queries from the terminal.  For each query, it calls ~qeval~ with the query and a stream that consists of a single empty frame.  This will produce the stream of all possible matches (all possible extensions to the empty frame).  For each frame in the resulting stream, it instantiates the original query using the values of the variables found in the frame.  This stream of instantiated queries is then printed.[fn:278]

The driver also checks for the special command ~assert!~, which signals that the input is not a query but rather an assertion or rule to be added to the data base.  For instance,

#+begin_src scheme
(assert! (job (Bitdiddle Ben) (computer wizard)))

(assert! (rule (wheel ?person)
               (and (supervisor ?middle-manager ?person)
                    (supervisor ?x ?middle-manager))))
#+end_src

*** 4.4.3 Is Logic Programming Mathematical Logic?
:properties:
:custom_id: section-4.4.3
:end:

The means of combination used in the query language may at first seem identical to the operations ~and~, ~or~, and ~not~ of mathematical logic, and the application of query-language rules is in fact accomplished through a legitimate method of inference.[fn:279] This identification of the query language with mathematical logic is not really valid, though, because the query language provides a <<i86>> control structure that interprets the logical statements procedurally.  We can often take advantage of this control structure.  For example, to find all of the supervisors of programmers we could formulate a query in either of two logically equivalent forms:

#+begin_src scheme
(and (job ?x (computer programmer))
     (supervisor ?x ?y))
#+end_src

or

#+begin_src scheme
(and (supervisor ?x ?y)
     (job ?x (computer programmer)))
#+end_src

If a company has many more supervisors than programmers (the usual case), it is better to use the first form rather than the second because the data base must be scanned for each intermediate result (frame) produced by the first clause of the ~and~.

The aim of logic programming is to provide the programmer with techniques for decomposing a computational problem into two separate problems: "what" is to be computed, and "how" this should be computed.  This is accomplished by selecting a subset of the statements of mathematical logic that is powerful enough to be able to describe anything one might want to compute, yet weak enough to have a controllable procedural interpretation.  The intention here is that, on the one hand, a program specified in a logic programming language should be an effective program that can be carried out by a computer.  Control ("how" to compute) is effected by using the order of evaluation of the language.  We should be able to arrange the order of clauses and the order of subgoals within each clause so that the computation is done in an order deemed to be effective and efficient.  At the same time, we should be able to view the result of the computation ("what" to compute) as a simple consequence of the laws of logic.

Our query language can be regarded as just such a procedurally interpretable subset of mathematical logic.  An assertion represents a simple fact (an atomic proposition).  A rule represents the implication that the rule conclusion holds for those cases where the rule body holds.  A rule has a natural procedural interpretation: To establish the conclusion of the rule, establish the body of the rule.  Rules, therefore, specify computations.  However, because rules can also be regarded as statements of mathematical logic, we can justify any "inference" accomplished by a logic program by asserting that the same result could be obtained by working entirely within mathematical logic.[fn:280]

*Infinite loops*

A consequence of the procedural interpretation of logic programs is that it is possible to construct hopelessly inefficient programs for solving certain problems.  An extreme case of inefficiency occurs when the system falls into infinite loops in making deductions.  As a simple example, suppose we are setting up a data base of famous marriages, including

#+begin_src scheme
(assert! (married Minnie Mickey))
#+end_src

If we now ask

#+begin_src scheme
(married Mickey ?who)
#+end_src

we will get no response, because the system doesn't know that if A is married to B, then B is married to A. So we assert the rule

#+begin_src scheme
(assert! (rule (married ?x ?y)
               (married ?y ?x)))
#+end_src

and again query

#+begin_src scheme
(married Mickey ?who)
#+end_src

Unfortunately, this will drive the system into an infinite loop, as follows:

- The system finds that the ~married~ rule is applicable; that is, the rule conclusion ~(married ?x ?y)~ successfully unifies with the query pattern ~(married Mickey ?who)~ to produce a frame in which ~?x~ is bound to ~Mickey~ and ~?y~ is bound to ~?who~.  So the interpreter proceeds to evaluate the rule body ~(married ?y ?x)~ in this frame--in effect, to process the query ~(married ?who Mickey)~.

- One answer appears directly as an assertion in the data base: ~(married Minnie Mickey)~.

- The ~married~ rule is also applicable, so the interpreter again evaluates the rule body, which this time is equivalent to ~(married Mickey ?who)~.

The system is now in an infinite loop.  Indeed, whether the system will find the simple answer ~(married Minnie Mickey)~ before it goes into the loop depends on implementation details concerning the order in which the system checks the items in the data base.  This is a very simple example of the kinds of loops that can occur.  Collections of interrelated rules can lead to loops that are much harder to anticipate, and the appearance of a loop can depend on the order of clauses in an ~and~ (see [[#exercise-4.64][Exercise 4.64]]) or on low-level details concerning the order in which the system processes queries.[fn:281]

*Problems with 'not'*

Another quirk in the query system concerns ~not~.  Given the data base of section [[#section-4.4.1][4.4.1]], consider the following two queries:

#+begin_src scheme
(and (supervisor ?x ?y)
     (not (job ?x (computer programmer))))

(and (not (job ?x (computer programmer)))
     (supervisor ?x ?y))
#+end_src

These two queries do not produce the same result.  The first query begins by finding all entries in the data base that match ~(supervisor ?x ?y)~, and then filters the resulting frames by removing the ones in which the value of ~?x~ satisfies ~(job ?x (computer programmer))~.  The second query begins by filtering the incoming frames to remove those that can satisfy ~(job ?x (computer programmer))~.  Since the only incoming frame is empty, it checks the data base to see if there are any patterns that satisfy ~(job ?x (computer programmer))~.  Since there generally are entries of this form, the ~not~ clause filters out the empty frame and returns an empty stream of frames.  Consequently, the entire compound query returns an empty stream.

The trouble is that our implementation of ~not~ really is meant to serve as a filter on values for the variables.  If a ~not~ clause is processed with a frame in which some of the variables remain unbound (as does ~?x~ in the example above), the system will produce unexpected results.  Similar problems occur with the use of ~lisp-value~--the Lisp predicate can't work if some of its arguments are unbound.  See [[#exercise-4.77][Exercise 4.77]].

There is also a much more serious way in which the ~not~ of the query language differs from the ~not~ of mathematical logic.  In logic, we interpret the statement "not P" to mean that P is not true.  In the query system, however, "not P" means that P is not deducible from the knowledge in the data base.  For example, given the personnel data base of section [[#section-4.4.1][4.4.1]], the system would happily deduce all sorts of ~not~ statements, such as that Ben Bitdiddle is not a baseball fan, that it is not raining outside, and that 2 + 2 is not 4.[fn:282] In other words, the ~not~ of logic programming languages reflects the so-called <<i61>> closed world assumption that all relevant information has been included in the data base.[fn:283]

**** Exercise 4.64
:properties:
:custom_id: exercise-4.64
:end:

Louis Reasoner mistakenly deletes the ~outranked-by~ rule (section [[#section-4.4.1][4.4.1]]) from the data base.  When he realizes this, he quickly reinstalls it.  Unfortunately, he makes a slight change in the rule, and types it in as

#+begin_src scheme
(rule (outranked-by ?staff-person ?boss)
      (or (supervisor ?staff-person ?boss)
          (and (outranked-by ?middle-manager ?boss)
               (supervisor ?staff-person ?middle-manager))))
#+end_src

Just after Louis types this information into the system, DeWitt Aull comes by to find out who outranks Ben Bitdiddle.  He issues the query

#+begin_src scheme
(outranked-by (Bitdiddle Ben) ?who)
#+end_src

After answering, the system goes into an infinite loop.  Explain why.

**** Exercise 4.65
:properties:
:custom_id: exercise-4.65
:end:

Cy D. Fect, looking forward to the day when he will rise in the organization, gives a query to find all the wheels (using the ~wheel~ rule of section [[#section-4.4.1][4.4.1]]):

#+begin_src scheme
(wheel ?who)
#+end_src

To his surprise, the system responds

#+begin_src scheme
;;; Query results:
(wheel (Warbucks Oliver))
(wheel (Bitdiddle Ben))
(wheel (Warbucks Oliver))
(wheel (Warbucks Oliver))
(wheel (Warbucks Oliver))
#+end_src

Why is Oliver Warbucks listed four times?

**** Exercise 4.66
:properties:
:custom_id: exercise-4.66
:end:

Ben has been generalizing the query system to provide statistics about the company.  For example, to find the total salaries of all the computer programmers one will be able to say

#+begin_src scheme
(sum ?amount
     (and (job ?x (computer programmer))
          (salary ?x ?amount)))
#+end_src

In general, Ben's new system allows expressions of the form

#+begin_src scheme
(accumulation-function <VARIABLE>
                       <QUERY PATTERN>)
#+end_src

where ~accumulation-function~ can be things like ~sum~, ~average~, or ~maximum~.  Ben reasons that it should be a cinch to implement this.  He will simply feed the query pattern to ~qeval~.  This will produce a stream of frames.  He will then pass this stream through a mapping function that extracts the value of the designated variable from each frame in the stream and feed the resulting stream of values to the accumulation function.  Just as Ben completes the implementation and is about to try it out, Cy walks by, still puzzling over the ~wheel~ query result in exercise [[#exercise-4.65][Exercise 4.65]].  When Cy shows Ben the system's response, Ben groans, "Oh, no, my simple accumulation scheme won't work!"

What has Ben just realized?  Outline a method he can use to salvage the situation.

**** Exercise 4.67
:properties:
:custom_id: exercise-4.67
:end:

Devise a way to install a loop detector in the query system so as to avoid the kinds of simple loops illustrated in the text and in [[#exercise-4.64][Exercise 4.64]].  The general idea is that the system should maintain some sort of history of its current chain of deductions and should not begin processing a query that it is already working on.  Describe what kind of information (patterns and frames) is included in this history, and how the check should be made.  (After you study the details of the query-system implementation in section [[#section-4.4.4][4.4.4]], you may want to modify the system to include your loop detector.)

**** Exercise 4.68
:properties:
:custom_id: exercise-4.68
:end:

Define rules to implement the ~reverse~ operation of [[#exercise-2.18][Exercise 2.18]], which returns a list containing the same elements as a given list in reverse order.  (Hint: Use ~append-to-form~.)  Can your rules answer both ~(reverse (1 2 3) ?x)~ and ~(reverse ?x (1 2 3))~ ?

**** Exercise 4.69
:properties:
:custom_id: exercise-4.69
:end:

Beginning with the data base and the rules you formulated in [[#exercise-4.63][Exercise 4.63]], devise a rule for adding "greats" to a grandson relationship.  This should enable the system to deduce that Irad is the great-grandson of Adam, or that Jabal and Jubal are the great-great-great-great-great-grandsons of Adam.  (Hint: Represent the fact about Irad, for example, as ~((great grandson) Adam Irad)~.  Write rules that determine if a list ends in the word ~grandson~.  Use this to express a rule that allows one to derive the relationship ~((great .  ?rel) ?x ?y)~, where ~?rel~ is a list ending in ~grandson~.)  Check your rules on queries such as ~((great grandson) ?g ?ggs)~ and ~(?relationship Adam Irad)~.

*** 4.4.4 Implementing the Query System
:properties:
:custom_id: section-4.4.4
:end:

Section [[#section-4.4.2][4.4.2]] described how the query system works.  Now we fill in the details by presenting a complete implementation of the system.

**** 4.4.4.1 The Driver Loop and Instantiation
:properties:
:custom_id: section-4.4.4.1
:end:

The driver loop for the query system repeatedly reads input expressions.  If the expression is a rule or assertion to be added to the data base, then the information is added.  Otherwise the expression is assumed to be a query.  The driver passes this query to the evaluator ~qeval~ together with an initial frame stream consisting of a single empty frame.  The result of the evaluation is a stream of frames generated by satisfying the query with variable values found in the data base.  These frames are used to form a new stream consisting of copies of the original query in which the variables are instantiated with values supplied by the stream of frames, and this final stream is printed at the terminal:

#+begin_src scheme
(define input-prompt ";;; Query input:")
(define output-prompt ";;; Query results:")

(define (query-driver-loop)
  (prompt-for-input input-prompt)
  (let ((q (query-syntax-process (read))))
    (cond ((assertion-to-be-added? q)
           (add-rule-or-assertion! (add-assertion-body q))
           (newline)
           (display "Assertion added to data base.")
           (query-driver-loop))
          (else
           (newline)
           (display output-prompt)
           (display-stream
            (stream-map
             (lambda (frame)
               (instantiate q
                   frame
                 (lambda (v f)
                   (contract-question-mark v))))
             (qeval q (singleton-stream '()))))
           (query-driver-loop)))))
#+end_src

Here, as in the other evaluators in this chapter, we use an abstract syntax for the expressions of the query language.  The implementation of the expression syntax, including the predicate ~assertion-to-be-added?~ and the selector ~add-assertion-body~, is given in section [[#section-4.4.4.7][4.4.4.7]].  ~add-rule-or-assertion!~ is defined in section [[#section-4.4.4.5][4.4.4.5]].

Before doing any processing on an input expression, the driver loop transforms it syntactically into a form that makes the processing more efficient.  This involves changing the representation of pattern variables.  When the query is instantiated, any variables that remain unbound are transformed back to the input representation before being printed.  These transformations are performed by the two procedures ~query-syntax-process~ and ~contract-question-mark~ (section [[#section-4.4.4.7][4.4.4.7]]).

To instantiate an expression, we copy it, replacing any variables in the expression by their values in a given frame.  The values are themselves instantiated, since they could contain variables (for example, if ~?x~ in ~exp~ is bound to ~?y~ as the result of unification and ~?y~ is in turn bound to 5).  The action to take if a variable cannot be instantiated is given by a procedural argument to ~instantiate~.

#+begin_src scheme
(define (instantiate exp frame unbound-var-handler)
  (define (copy exp)
    (cond ((var? exp)
           (let ((binding (binding-in-frame exp frame)))
             (if binding
                 (copy (binding-value binding))
                 (unbound-var-handler exp frame))))
          ((pair? exp)
           (cons (copy (car exp)) (copy (cdr exp))))
          (else exp)))
  (copy exp))
#+end_src

The procedures that manipulate bindings are defined in section [[#section-4.4.4.8][4.4.4.8]].

**** 4.4.4.2 The Evaluator
:properties:
:custom_id: section-4.4.4.2
:end:

The ~qeval~ procedure, called by the ~query-driver-loop~, is the basic evaluator of the query system.  It takes as inputs a query and a stream of frames, and it returns a stream of extended frames.  It identifies special forms by a data-directed dispatch using ~get~ and ~put~, just as we did in implementing generic operations in [[#section-2][Chapter 2]].  Any query that is not identified as a special form is assumed to be a simple query, to be processed by ~simple-query~.

#+begin_src scheme
(define (qeval query frame-stream)
  (let ((qproc (get (type query) 'qeval)))
    (if qproc
        (qproc (contents query) frame-stream)
        (simple-query query frame-stream))))
#+end_src

~type~ and ~contents~, defined in section [[#section-4.4.4.7][4.4.4.7]], implement the abstract syntax of the special forms.

*Simple queries*

The ~simple-query~ procedure handles simple queries.  It takes as arguments a simple query (a pattern) together with a stream of frames, and it returns the stream formed by extending each frame by all data-base matches of the query.

#+begin_src scheme
(define (simple-query query-pattern frame-stream)
  (stream-flatmap
   (lambda (frame)
     (stream-append-delayed
      (find-assertions query-pattern frame)
      (delay (apply-rules query-pattern frame))))
   frame-stream))
#+end_src

For each frame in the input stream, we use ~find-assertions~ (section [[#section-4.4.4.3][4.4.4.3]]) to match the pattern against all assertions in the data base, producing a stream of extended frames, and we use ~apply-rules~ (section [[#section-4.4.4.4][4.4.4.4]]) to apply all possible rules, producing another stream of extended frames.  These two streams are combined (using ~stream-append-delayed~, section [[#section-4.4.4.6][4.4.4.6]]) to make a stream of all the ways that the given pattern can be satisfied consistent with the original frame (see [[#exercise-4.71][Exercise 4.71]]).  The streams for the individual input frames are combined using ~stream-flatmap~ (section [[#section-4.4.4.6][4.4.4.6]]) to form one large stream of all the ways that any of the frames in the original input stream can be extended to produce a match with the given pattern.

*Compound queries*

~and~ queries are handled as illustrated in [[figure-4.5][Figure 4.5]] by the ~conjoin~ procedure.  ~conjoin~ takes as inputs the conjuncts and the frame stream and returns the stream of extended frames.  First, ~conjoin~ processes the stream of frames to find the stream of all possible frame extensions that satisfy the first query in the conjunction.  Then, using this as the new frame stream, it recursively applies ~conjoin~ to the rest of the queries.

#+begin_src scheme
(define (conjoin conjuncts frame-stream)
  (if (empty-conjunction? conjuncts)
      frame-stream
      (conjoin (rest-conjuncts conjuncts)
               (qeval (first-conjunct conjuncts)
                      frame-stream))))
#+end_src

The expression

#+begin_src scheme
(put 'and 'qeval conjoin)
#+end_src

sets up ~qeval~ to dispatch to ~conjoin~ when an ~and~ form is encountered.

~or~ queries are handled similarly, as shown in [[figure-4.6][Figure 4.6]].  The output streams for the various disjuncts of the ~or~ are computed separately and merged using the ~interleave-delayed~ procedure from section [[#section-4.4.4.6][4.4.4.6]].  (See [[#exercise-4.71][Exercise 4.71]] and [[#exercise-4.72][Exercise 4.72]].)

#+begin_src scheme
(define (disjoin disjuncts frame-stream)
  (if (empty-disjunction? disjuncts)
      the-empty-stream
      (interleave-delayed
       (qeval (first-disjunct disjuncts) frame-stream)
       (delay (disjoin (rest-disjuncts disjuncts)
                       frame-stream)))))

(put 'or 'qeval disjoin)
#+end_src

The predicates and selectors for the syntax of conjuncts and disjuncts are given in section [[#section-4.4.4.7][4.4.4.7]].

*Filters*

~not~ is handled by the method outlined in section [[#section-4.4.2][4.4.2]].  We attempt to extend each frame in the input stream to satisfy the query being negated, and we include a given frame in the output stream only if it cannot be extended.

#+begin_src scheme
(define (negate operands frame-stream)
  (stream-flatmap
   (lambda (frame)
     (if (stream-null? (qeval (negated-query operands)
                              (singleton-stream frame)))
         (singleton-stream frame)
         the-empty-stream))
   frame-stream))

(put 'not 'qeval negate)
#+end_src

~lisp-value~ is a filter similar to ~not~.  Each frame in the stream is used to instantiate the variables in the pattern, the indicated predicate is applied, and the frames for which the predicate returns false are filtered out of the input stream.  An error results if there are unbound pattern variables.

#+begin_src scheme
(define (lisp-value call frame-stream)
  (stream-flatmap
   (lambda (frame)
     (if (execute
          (instantiate
              call
              frame
            (lambda (v f)
              (error "Unknown pat var - LISP-VALUE" v))))
         (singleton-stream frame)
         the-empty-stream))
   frame-stream))

(put 'lisp-value 'qeval lisp-value)
#+end_src

~execute~, which applies the predicate to the arguments, must ~eval~ the predicate expression to get the procedure to apply.  However, it must not evaluate the arguments, since they are already the actual arguments, not expressions whose evaluation (in Lisp) will produce the arguments.  Note that ~execute~ is implemented using ~eval~ and ~apply~ from the underlying Lisp system.

#+begin_src scheme
(define (execute exp)
  (apply (eval (predicate exp) user-initial-environment)
         (args exp)))
#+end_src

The ~always-true~ special form provides for a query that is always satisfied.  It ignores its contents (normally empty) and simply passes through all the frames in the input stream.  ~always-true~ is used by the ~rule-body~ selector (section [[#section-4.4.4.7][4.4.4.7]]) to provide bodies for rules that were defined without bodies (that is, rules whose conclusions are always satisfied).

#+begin_src scheme
(define (always-true ignore frame-stream) frame-stream)

(put 'always-true 'qeval always-true)
#+end_src

The selectors that define the syntax of ~not~ and ~lisp-value~ are given in section [[#section-4.4.4.7][4.4.4.7]].

**** 4.4.4.3 Finding Assertions by Pattern Matching
:properties:
:custom_id: section-4.4.4.3
:end:

~find-assertions~, called by ~simple-query~ (section [[#section-4.4.4.2][4.4.4.2]]), takes as input a pattern and a frame.  It returns a stream of frames, each extending the given one by a data-base match of the given pattern.  It uses ~fetch-assertions~ (section [[#section-4.4.4.5][4.4.4.5]]) to get a stream of all the assertions in the data base that should be checked for a match against the pattern and the frame.  The reason for ~fetch-assertions~ here is that we can often apply simple tests that will eliminate many of the entries in the data base from the pool of candidates for a successful match.  The system would still work if we eliminated ~fetch-assertions~ and simply checked a stream of all assertions in the data base, but the computation would be less efficient because we would need to make many more calls to the matcher.

#+begin_src scheme
(define (find-assertions pattern frame)
  (stream-flatmap (lambda (datum)
                    (check-an-assertion datum pattern frame))
                  (fetch-assertions pattern frame)))
#+end_src

~check-an-assertion~ takes as arguments a pattern, a data object (assertion), and a frame and returns either a one-element stream containing the extended frame or ~the-empty-stream~ if the match fails.

#+begin_src scheme
(define (check-an-assertion assertion query-pat query-frame)
  (let ((match-result
         (pattern-match query-pat assertion query-frame)))
    (if (eq? match-result 'failed)
        the-empty-stream
        (singleton-stream match-result))))
#+end_src

The basic pattern matcher returns either the symbol ~failed~ or an extension of the given frame.  The basic idea of the matcher is to check the pattern against the data, element by element, accumulating bindings for the pattern variables.  If the pattern and the data object are the same, the match succeeds and we return the frame of bindings accumulated so far.  Otherwise, if the pattern is a variable we extend the current frame by binding the variable to the data, so long as this is consistent with the bindings already in the frame.  If the pattern and the data are both pairs, we (recursively) match the ~car~ of the pattern against the ~car~ of the data to produce a frame; in this frame we then match the ~cdr~ of the pattern against the ~cdr~ of the data.  If none of these cases are applicable, the match fails and we return the symbol ~failed~.

#+begin_src scheme
(define (pattern-match pat dat frame)
  (cond ((eq? frame 'failed) 'failed)
        ((equal? pat dat) frame)
        ((var? pat) (extend-if-consistent pat dat frame))
        ((and (pair? pat) (pair? dat))
         (pattern-match (cdr pat)
                        (cdr dat)
                        (pattern-match (car pat)
                                       (car dat)
                                       frame)))
        (else 'failed)))
#+end_src

Here is the procedure that extends a frame by adding a new binding, if this is consistent with the bindings already in the frame:

#+begin_src scheme
(define (extend-if-consistent var dat frame)
  (let ((binding (binding-in-frame var frame)))
    (if binding
        (pattern-match (binding-value binding) dat frame)
        (extend var dat frame))))
#+end_src

If there is no binding for the variable in the frame, we simply add the binding of the variable to the data.  Otherwise we match, in the frame, the data against the value of the variable in the frame.  If the stored value contains only constants, as it must if it was stored during pattern matching by ~extend-if-consistent~, then the match simply tests whether the stored and new values are the same.  If so, it returns the unmodified frame; if not, it returns a failure indication.  The stored value may, however, contain pattern variables if it was stored during unification (see section [[#section-4.4.4.4][4.4.4.4]]).  The recursive match of the stored pattern against the new data will add or check bindings for the variables in this pattern.  For example, suppose we have a frame in which ~?x~ is bound to ~(f ?y)~ and ~?y~ is unbound, and we wish to augment this frame by a binding of ~?x~ to ~(f b)~.  We look up ~?x~ and find that it is bound to ~(f ?y)~.  This leads us to match ~(f ?y)~ against the proposed new value ~(f b)~ in the same frame.  Eventually this match extends the frame by adding a binding of ~?y~ to ~b~.  ~?X~ remains bound to ~(f ?y)~.  We never modify a stored binding and we never store more than one binding for a given variable.

The procedures used by ~extend-if-consistent~ to manipulate bindings are defined in section [[#section-4.4.4.8][4.4.4.8]].

*Patterns with dotted tails*

If a pattern contains a dot followed by a pattern variable, the pattern variable matches the rest of the data list (rather than the next element of the data list), just as one would expect with the dotted-tail notation described in [[#exercise-2.20][Exercise 2.20]].  Although the pattern matcher we have just implemented doesn't look for dots, it does behave as we want.  This is because the Lisp ~read~ primitive, which is used by ~query-driver-loop~ to read the query and represent it as a list structure, treats dots in a special way.

When ~read~ sees a dot, instead of making the next item be the next element of a list (the ~car~ of a ~cons~ whose ~cdr~ will be the rest of the list) it makes the next item be the ~cdr~ of the list structure.  For example, the list structure produced by ~read~ for the pattern ~(computer ?type)~ could be constructed by evaluating the expression ~(cons 'computer (cons '?type '()))~, and that for ~(computer .  ?type)~ could be constructed by evaluating the expression ~(cons 'computer '?type)~.

Thus, as ~pattern-match~ recursively compares 'car's and 'cdr's of a data list and a pattern that had a dot, it eventually matches the variable after the dot (which is a ~cdr~ of the pattern) against a sublist of the data list, binding the variable to that list.  For example, matching the pattern ~(computer .  ?type)~ against ~(computer programmer trainee)~ will match ~?type~ against the list ~(programmer trainee)~.

**** 4.4.4.4 Rules and Unification
:properties:
:custom_id: section-4.4.4.4
:end:

~apply-rules~ is the rule analog of ~find-assertions~ (section [[#section-4.4.4.3][4.4.4.3]]).  It takes as input a pattern and a frame, and it forms a stream of extension frames by applying rules from the data base.  ~stream-flatmap~ maps ~apply-a-rule~ down the stream of possibly applicable rules (selected by ~fetch-rules~, section [[#section-4.4.4.5][4.4.4.5]]) and combines the resulting streams of frames.

#+begin_src scheme
(define (apply-rules pattern frame)
  (stream-flatmap (lambda (rule)
                    (apply-a-rule rule pattern frame))
                  (fetch-rules pattern frame)))
#+end_src

~apply-a-rule~ applies rules using the method outlined in section [[#section-4.4.2][4.4.2]].  It first augments its argument frame by unifying the rule conclusion with the pattern in the given frame.  If this succeeds, it evaluates the rule body in this new frame.

Before any of this happens, however, the program renames all the variables in the rule with unique new names.  The reason for this is to prevent the variables for different rule applications from becoming confused with each other.  For instance, if two rules both use a variable named ~?x~, then each one may add a binding for ~?x~ to the frame when it is applied.  These two ~?x~'s have nothing to do with each other, and we should not be fooled into thinking that the two bindings must be consistent.  Rather than rename variables, we could devise a more clever environment structure; however, the renaming approach we have chosen here is the most straightforward, even if not the most efficient.  (See [[#exercise-4.79][Exercise 4.79]].)  Here is the ~apply-a-rule~ procedure:

#+begin_src scheme
(define (apply-a-rule rule query-pattern query-frame)
  (let ((clean-rule (rename-variables-in rule)))
    (let ((unify-result
           (unify-match query-pattern
                        (conclusion clean-rule)
                        query-frame)))
      (if (eq? unify-result 'failed)
          the-empty-stream
          (qeval (rule-body clean-rule)
                 (singleton-stream unify-result))))))
#+end_src

The selectors ~rule-body~ and ~conclusion~ that extract parts of a rule are defined in section [[#section-4.4.4.7][4.4.4.7]].

We generate unique variable names by associating a unique identifier (such as a number) with each rule application and combining this identifier with the original variable names.  For example, if the rule-application identifier is 7, we might change each ~?x~ in the rule to ~?x-7~ and each ~?y~ in the rule to ~?y-7~.  (~make-new-variable~ and ~new-rule-application-id~ are included with the syntax procedures in section [[#section-4.4.4.7][4.4.4.7]].)

#+begin_src scheme
(define (rename-variables-in rule)
  (let ((rule-application-id (new-rule-application-id)))
    (define (tree-walk exp)
      (cond ((var? exp)
             (make-new-variable exp rule-application-id))
            ((pair? exp)
             (cons (tree-walk (car exp))
                   (tree-walk (cdr exp))))
            (else exp)))
    (tree-walk rule)))
#+end_src

The unification algorithm is implemented as a procedure that takes as inputs two patterns and a frame and returns either the extended frame or the symbol ~failed~.  The unifier is like the pattern matcher except that it is symmetrical--variables are allowed on both sides of the match.  ~unify-match~ is basically the same as ~pattern-match~, except that there is extra code (marked "~***~" below) to handle the case where the object on the right side of the match is a variable.

#+begin_src scheme
(define (unify-match p1 p2 frame)
  (cond ((eq? frame 'failed) 'failed)
        ((equal? p1 p2) frame)
        ((var? p1) (extend-if-possible p1 p2 frame))
        ((var? p2) (extend-if-possible p2 p1 frame)) ; ***
        ((and (pair? p1) (pair? p2))
         (unify-match (cdr p1)
                      (cdr p2)
                      (unify-match (car p1)
                                   (car p2)
                                   frame)))
        (else 'failed)))
#+end_src

In unification, as in one-sided pattern matching, we want to accept a proposed extension of the frame only if it is consistent with existing bindings.  The procedure ~extend-if-possible~ used in unification is the same as the ~extend-if-consistent~ used in pattern matching except for two special checks, marked "~***~" in the program below.  In the first case, if the variable we are trying to match is not bound, but the value we are trying to match it with is itself a (different) variable, it is necessary to check to see if the value is bound, and if so, to match its value.  If both parties to the match are unbound, we may bind either to the other.

The second check deals with attempts to bind a variable to a pattern that includes that variable.  Such a situation can occur whenever a variable is repeated in both patterns.  Consider, for example, unifying the two patterns ~(?x ?x)~ and '(?y <EXPRESSION INVOLVING ~?Y~>)' in a frame where both ~?x~ and ~?y~ are unbound.  First ~?x~ is matched against ~?y~, making a binding of ~?x~ to ~?y~.  Next, the same ~?x~ is matched against the given expression involving ~?y~.  Since ~?x~ is already bound to ~?y~, this results in matching ~?y~ against the expression.  If we think of the unifier as finding a set of values for the pattern variables that make the patterns the same, then these patterns imply instructions to find a ~?y~ such that ~?y~ is equal to the expression involving ~?y~.  There is no general method for solving such equations, so we reject such bindings; these cases are recognized by the predicate ~depends-on?~.[fn:284] On the other hand, we do not want to reject attempts to bind a variable to itself.  For example, consider unifying ~(?x ?x)~ and ~(?y ?y)~.  The second attempt to bind ~?x~ to ~?y~ matches ~?y~ (the stored value of ~?x~) against ~?y~ (the new value of ~?x~).  This is taken care of by the ~equal?~ clause of ~unify-match~.

#+begin_src scheme
(define (extend-if-possible var val frame)
  (let ((binding (binding-in-frame var frame)))
    (cond (binding
           (unify-match
            (binding-value binding) val frame))
          ((var? val)                   ; ***
           (let ((binding (binding-in-frame val frame)))
             (if binding
                 (unify-match
                  var (binding-value binding) frame)
                 (extend var val frame))))
          ((depends-on? val var frame)  ; ***
           'failed)
          (else (extend var val frame)))))
#+end_src

~depends-on?~ is a predicate that tests whether an expression proposed to be the value of a pattern variable depends on the variable.  This must be done relative to the current frame because the expression may contain occurrences of a variable that already has a value that depends on our test variable.  The structure of ~depends-on?~ is a simple recursive tree walk in which we substitute for the values of variables whenever necessary.

#+begin_src scheme
(define (depends-on? exp var frame)
  (define (tree-walk e)
    (cond ((var? e)
           (if (equal? var e)
               true
               (let ((b (binding-in-frame e frame)))
                 (if b
                     (tree-walk (binding-value b))
                     false))))
          ((pair? e)
           (or (tree-walk (car e))
               (tree-walk (cdr e))))
          (else false)))
  (tree-walk exp))
#+end_src

**** 4.4.4.5 Maintaining the Data Base
:properties:
:custom_id: section-4.4.4.5
:end:

One important problem in designing logic programming languages is that of arranging things so that as few irrelevant data-base entries as possible will be examined in checking a given pattern.  In our system, in addition to storing all assertions in one big stream, we store all assertions whose 'car's are constant symbols in separate streams, in a table indexed by the symbol.  To fetch an assertion that may match a pattern, we first check to see if the ~car~ of the pattern is a constant symbol.  If so, we return (to be tested using the matcher) all the stored assertions that have the same ~car~.  If the pattern's ~car~ is not a constant symbol, we return all the stored assertions.  Cleverer methods could also take advantage of information in the frame, or try also to optimize the case where the ~car~ of the pattern is not a constant symbol.  We avoid building our criteria for indexing (using the ~car~, handling only the case of constant symbols) into the program; instead we call on predicates and selectors that embody our criteria.

#+begin_src scheme
(define THE-ASSERTIONS the-empty-stream)

(define (fetch-assertions pattern frame)
  (if (use-index? pattern)
      (get-indexed-assertions pattern)
      (get-all-assertions)))

(define (get-all-assertions) THE-ASSERTIONS)

(define (get-indexed-assertions pattern)
  (get-stream (index-key-of pattern) 'assertion-stream))
#+end_src

~get-stream~ looks up a stream in the table and returns an empty stream if nothing is stored there.

#+begin_src scheme
(define (get-stream key1 key2)
  (let ((s (get key1 key2)))
    (if s s the-empty-stream)))
#+end_src

Rules are stored similarly, using the ~car~ of the rule conclusion.  Rule conclusions are arbitrary patterns, however, so they differ from assertions in that they can contain variables.  A pattern whose ~car~ is a constant symbol can match rules whose conclusions start with a variable as well as rules whose conclusions have the same ~car~.  Thus, when fetching rules that might match a pattern whose ~car~ is a constant symbol we fetch all rules whose conclusions start with a variable as well as those whose conclusions have the same ~car~ as the pattern.  For this purpose we store all rules whose conclusions start with a variable in a separate stream in our table, indexed by the symbol ~?~.

#+begin_src scheme
(define THE-RULES the-empty-stream)

(define (fetch-rules pattern frame)
  (if (use-index? pattern)
      (get-indexed-rules pattern)
      (get-all-rules)))

(define (get-all-rules) THE-RULES)

(define (get-indexed-rules pattern)
  (stream-append
   (get-stream (index-key-of pattern) 'rule-stream)
   (get-stream '? 'rule-stream)))
#+end_src

~add-rule-or-assertion!~ is used by ~query-driver-loop~ to add assertions and rules to the data base.  Each item is stored in the index, if appropriate, and in a stream of all assertions or rules in the data base.

#+begin_src scheme
(define (add-rule-or-assertion! assertion)
  (if (rule? assertion)
      (add-rule! assertion)
      (add-assertion! assertion)))

(define (add-assertion! assertion)
  (store-assertion-in-index assertion)
  (let ((old-assertions THE-ASSERTIONS))
    (set! THE-ASSERTIONS
      (cons-stream assertion old-assertions))
    'ok))

(define (add-rule! rule)
  (store-rule-in-index rule)
  (let ((old-rules THE-RULES))
    (set! THE-RULES (cons-stream rule old-rules))
    'ok))
#+end_src

To actually store an assertion or a rule, we check to see if it can be indexed.  If so, we store it in the appropriate stream.

#+begin_src scheme
(define (store-assertion-in-index assertion)
  (if (indexable? assertion)
      (let ((key (index-key-of assertion)))
        (let ((current-assertion-stream
               (get-stream key 'assertion-stream)))
          (put key
               'assertion-stream
               (cons-stream assertion
                            current-assertion-stream))))))

(define (store-rule-in-index rule)
  (let ((pattern (conclusion rule)))
    (if (indexable? pattern)
        (let ((key (index-key-of pattern)))
          (let ((current-rule-stream
                 (get-stream key 'rule-stream)))
            (put key
                 'rule-stream
                 (cons-stream rule
                              current-rule-stream)))))))
#+end_src

The following procedures define how the data-base index is used.  A pattern (an assertion or a rule conclusion) will be stored in the table if it starts with a variable or a constant symbol.

#+begin_src scheme
(define (indexable? pat)
  (or (constant-symbol? (car pat))
      (var? (car pat))))
#+end_src

The key under which a pattern is stored in the table is either ~?~ (if it starts with a variable) or the constant symbol with which it starts.

#+begin_src scheme
(define (index-key-of pat)
  (let ((key (car pat)))
    (if (var? key) '? key)))
#+end_src

The index will be used to retrieve items that might match a pattern if the pattern starts with a constant symbol.

#+begin_src scheme
(define (use-index? pat)
  (constant-symbol? (car pat)))
#+end_src

**** Exercise 4.70
:properties:
:custom_id: exercise-4.70
:end:

What is the purpose of the ~let~ bindings in the procedures ~add-assertion!~ and ~add-rule!~ ?  What would be wrong with the following implementation of ~add-assertion!~ ?  Hint: Recall the definition of the infinite stream of ones in section [[#section-3.5.2][3.5.2]]: ~(define ones (cons-stream 1 ones))~.

#+begin_src scheme
(define (add-assertion! assertion)
  (store-assertion-in-index assertion)
  (set! THE-ASSERTIONS
    (cons-stream assertion THE-ASSERTIONS))
  'ok)
#+end_src

**** 4.4.4.6 Stream Operations
:properties:
:custom_id: section-4.4.4.6
:end:

The query system uses a few stream operations that were not presented in [[#section-3][Chapter 3]].

~stream-append-delayed~ and ~interleave-delayed~ are just like ~stream-append~ and ~interleave~ (section [[#section-3.5.3][3.5.3]]), except that they take a delayed argument (like the ~integral~ procedure in section [[#section-3.5.4][3.5.4]]).  This postpones looping in some cases (see [[#exercise-4.71][Exercise 4.71]]).

#+begin_src scheme
(define (stream-append-delayed s1 delayed-s2)
  (if (stream-null? s1)
      (force delayed-s2)
      (cons-stream
       (stream-car s1)
       (stream-append-delayed (stream-cdr s1) delayed-s2))))

(define (interleave-delayed s1 delayed-s2)
  (if (stream-null? s1)
      (force delayed-s2)
      (cons-stream
       (stream-car s1)
       (interleave-delayed (force delayed-s2)
                           (delay (stream-cdr s1))))))
#+end_src

~stream-flatmap~, which is used throughout the query evaluator to map a procedure over a stream of frames and combine the resulting streams of frames, is the stream analog of the ~flatmap~ procedure introduced for ordinary lists in section [[#section-2.2.3][2.2.3]].  Unlike ordinary ~flatmap~, however, we accumulate the streams with an interleaving process, rather than simply appending them (see [[#exercise-4.72][Exercise 4.72]] and [[#exercise-4.73][Exercise 4.73]]).

#+begin_src scheme
(define (stream-flatmap proc s)
  (flatten-stream (stream-map proc s)))

(define (flatten-stream stream)
  (if (stream-null? stream)
      the-empty-stream
      (interleave-delayed
       (stream-car stream)
       (delay (flatten-stream (stream-cdr stream))))))
#+end_src

The evaluator also uses the following simple procedure to generate a stream consisting of a single element:

#+begin_src scheme
(define (singleton-stream x)
  (cons-stream x the-empty-stream))
#+end_src

**** 4.4.4.7 Query Syntax Procedures
:properties:
:custom_id: section-4.4.4.7
:end:

~type~ and ~contents~, used by ~qeval~ (section [[#section-4.4.4.2][4.4.4.2]]), specify that a special form is identified by the symbol in its ~car~.  They are the same as the ~type-tag~ and ~contents~ procedures in section [[#section-2.4.2][2.4.2]], except for the error message.

#+begin_src scheme
(define (type exp)
  (if (pair? exp)
      (car exp)
      (error "Unknown expression TYPE" exp)))

(define (contents exp)
  (if (pair? exp)
      (cdr exp)
      (error "Unknown expression CONTENTS" exp)))
#+end_src

The following procedures, used by ~query-driver-loop~ (in section [[#section-4.4.4.1][4.4.4.1]]), specify that rules and assertions are added to the data base by expressions of the form '(assert! <RULE-OR-ASSERTION>)':

#+begin_src scheme
(define (assertion-to-be-added? exp)
  (eq? (type exp) 'assert!))

(define (add-assertion-body exp)
  (car (contents exp)))
#+end_src

Here are the syntax definitions for the ~and~, ~or~, ~not~, and ~lisp-value~ special forms (section [[#section-4.4.4.2][4.4.4.2]]):

#+begin_src scheme
(define (empty-conjunction? exps) (null? exps))
(define (first-conjunct exps) (car exps))
(define (rest-conjuncts exps) (cdr exps))

(define (empty-disjunction? exps) (null? exps))
(define (first-disjunct exps) (car exps))
(define (rest-disjuncts exps) (cdr exps))

(define (negated-query exps) (car exps))

(define (predicate exps) (car exps))
(define (args exps) (cdr exps))
#+end_src

The following three procedures define the syntax of rules:

#+begin_src scheme
(define (rule? statement)
  (tagged-list? statement 'rule))

(define (conclusion rule) (cadr rule))

(define (rule-body rule)
  (if (null? (cddr rule))
      '(always-true)
      (caddr rule)))
#+end_src

~query-driver-loop~ (section [[#section-4.4.4.1][4.4.4.1]]) calls ~query-syntax-process~ to transform pattern variables in the expression, which have the form ~?symbol~, into the internal format ~(?  symbol)~.  That is to say, a pattern such as ~(job ?x ?y)~ is actually represented internally by the system as ~(job (?  x) (?  y))~.  This increases the efficiency of query processing, since it means that the system can check to see if an expression is a pattern variable by checking whether the ~car~ of the expression is the symbol ~?~, rather than having to extract characters from the symbol.  The syntax transformation is accomplished by the following procedure:[fn:285]

#+begin_src scheme
(define (query-syntax-process exp)
  (map-over-symbols expand-question-mark exp))

(define (map-over-symbols proc exp)
  (cond ((pair? exp)
         (cons (map-over-symbols proc (car exp))
               (map-over-symbols proc (cdr exp))))
        ((symbol? exp) (proc exp))
        (else exp)))

(define (expand-question-mark symbol)
  (let ((chars (symbol->string symbol)))
    (if (string=? (substring chars 0 1) "?")
        (list '?
              (string->symbol
               (substring chars 1 (string-length chars))))
        symbol)))
#+end_src

Once the variables are transformed in this way, the variables in a pattern are lists starting with ~?~, and the constant symbols (which need to be recognized for data-base indexing, section [[#section-4.4.4.5][4.4.4.5]]) are just the symbols.

#+begin_src scheme
(define (var? exp)
  (tagged-list? exp '?))

(define (constant-symbol? exp) (symbol? exp))
#+end_src

Unique variables are constructed during rule application (in section [[#section-4.4.4.4][4.4.4.4]]) by means of the following procedures.  The unique identifier for a rule application is a number, which is incremented each time a rule is applied.

#+begin_src scheme
(define rule-counter 0)

(define (new-rule-application-id)
  (set! rule-counter (+ 1 rule-counter))
  rule-counter)

(define (make-new-variable var rule-application-id)
  (cons '? (cons rule-application-id (cdr var))))
#+end_src

When ~query-driver-loop~ instantiates the query to print the answer, it converts any unbound pattern variables back to the right form for printing, using

#+begin_src scheme
(define (contract-question-mark variable)
  (string->symbol
   (string-append "?"
                  (if (number? (cadr variable))
                      (string-append (symbol->string (caddr variable))
                                     "-"
                                     (number->string (cadr variable)))
                      (symbol->string (cadr variable))))))
#+end_src

**** 4.4.4.8 Frames and Bindings
:properties:
:custom_id: section-4.4.4.8
:end:

Frames are represented as lists of bindings, which are variable-value pairs:

#+begin_src scheme
(define (make-binding variable value)
  (cons variable value))

(define (binding-variable binding)
  (car binding))

(define (binding-value binding)
  (cdr binding))

(define (binding-in-frame variable frame)
  (assoc variable frame))

(define (extend variable value frame)
  (cons (make-binding variable value) frame))
#+end_src

**** Exercise 4.71
:properties:
:custom_id: exercise-4.71
:end:

Louis Reasoner wonders why the ~simple-query~ and ~disjoin~ procedures (section [[#section-4.4.4.2][4.4.4.2]]) are implemented using explicit ~delay~ operations, rather than being defined as follows:

#+begin_src scheme
(define (simple-query query-pattern frame-stream)
  (stream-flatmap
   (lambda (frame)
     (stream-append (find-assertions query-pattern frame)
                    (apply-rules query-pattern frame)))
   frame-stream))

(define (disjoin disjuncts frame-stream)
  (if (empty-disjunction? disjuncts)
      the-empty-stream
      (interleave
       (qeval (first-disjunct disjuncts) frame-stream)
       (disjoin (rest-disjuncts disjuncts) frame-stream))))
#+end_src

Can you give examples of queries where these simpler definitions would lead to undesirable behavior?

**** Exercise 4.72
:properties:
:custom_id: exercise-4.72
:end:

Why do ~disjoin~ and ~stream-flatmap~ interleave the streams rather than simply append them?  Give examples that illustrate why interleaving works better.  (Hint: Why did we use ~interleave~ in section [[#section-3.5.3][3.5.3]]?)

**** Exercise 4.73
:properties:
:custom_id: exercise-4.73
:end:

Why does ~flatten-stream~ use ~delay~ explicitly?  What would be wrong with defining it as follows:

#+begin_src scheme
(define (flatten-stream stream)
  (if (stream-null? stream)
      the-empty-stream
      (interleave
       (stream-car stream)
       (flatten-stream (stream-cdr stream)))))
#+end_src

**** Exercise 4.74
:properties:
:custom_id: exercise-4.74
:end:

Alyssa P. Hacker proposes to use a simpler version of ~stream-flatmap~ in ~negate~, ~lisp-value~, and ~find-assertions~.  She observes that the procedure that is mapped over the frame stream in these cases always produces either the empty stream or a singleton stream, so no interleaving is needed when combining these streams.

a. Fill in the missing expressions in Alyssa's program.

#+begin_src scheme
(define (simple-stream-flatmap proc s)
  (simple-flatten (stream-map proc s)))

(define (simple-flatten stream)
  (stream-map <??>
              (stream-filter <??> stream)))
#+end_src

b. Does the query system's behavior change if we change it in this way?

**** Exercise 4.75
:properties:
:custom_id: exercise-4.75
:end:

Implement for the query language a new special form called ~unique~.  ~unique~ should succeed if there is precisely one item in the data base satisfying a specified query.  For example,

#+begin_src scheme
(unique (job ?x (computer wizard)))
#+end_src

should print the one-item stream

#+begin_src scheme
(unique (job (Bitdiddle Ben) (computer wizard)))
#+end_src

since Ben is the only computer wizard, and

#+begin_src scheme
(unique (job ?x (computer programmer)))
#+end_src

should print the empty stream, since there is more than one computer programmer.  Moreover,

#+begin_src scheme
(and (job ?x ?j) (unique (job ?anyone ?j)))
#+end_src

should list all the jobs that are filled by only one person, and the people who fill them.

There are two parts to implementing ~unique~.  The first is to write a procedure that handles this special form, and the second is to make ~qeval~ dispatch to that procedure.  The second part is trivial, since ~qeval~ does its dispatching in a data-directed way.  If your procedure is called ~uniquely-asserted~, all you need to do is

#+begin_src scheme
(put 'unique 'qeval uniquely-asserted)
#+end_src

and ~qeval~ will dispatch to this procedure for every query whose ~type~ (~car~) is the symbol ~unique~.

The real problem is to write the procedure ~uniquely-asserted~.  This should take as input the ~contents~ (~cdr~) of the ~unique~ query, together with a stream of frames.  For each frame in the stream, it should use ~qeval~ to find the stream of all extensions to the frame that satisfy the given query.  Any stream that does not have exactly one item in it should be eliminated.  The remaining streams should be passed back to be accumulated into one big stream that is the result of the ~unique~ query.  This is similar to the implementation of the ~not~ special form.

Test your implementation by forming a query that lists all people who supervise precisely one person.

**** Exercise 4.76
:properties:
:custom_id: exercise-4.76
:end:

Our implementation of ~and~ as a series combination of queries ([[figure-4.5][Figure 4.5]]) is elegant, but it is inefficient because in processing the second query of the ~and~ we must scan the data base for each frame produced by the first query.  If the data base has n elements, and a typical query produces a number of output frames proportional to n (say n/k), then scanning the data base for each frame produced by the first query will require n^2/k calls to the pattern matcher.  Another approach would be to process the two clauses of the ~and~ separately, then look for all pairs of output frames that are compatible.  If each query produces n/k output frames, then this means that we must perform n^2/k^2 compatibility checks--a factor of k fewer than the number of matches required in our current method.

Devise an implementation of ~and~ that uses this strategy.  You must implement a procedure that takes two frames as inputs, checks whether the bindings in the frames are compatible, and, if so, produces a frame that merges the two sets of bindings.  This operation is similar to unification.

**** Exercise 4.77
:properties:
:custom_id: exercise-4.77
:end:

In section [[#section-4.4.3][4.4.3]] we saw that ~not~ and ~lisp-value~ can cause the query language to give "wrong" answers if these filtering operations are applied to frames in which variables are unbound.  Devise a way to fix this shortcoming.  One idea is to perform the filtering in a "delayed" manner by appending to the frame a "promise" to filter that is fulfilled only when enough variables have been bound to make the operation possible.  We could wait to perform filtering until all other operations have been performed.  However, for efficiency's sake, we would like to perform filtering as soon as possible so as to cut down on the number of intermediate frames generated.

**** Exercise 4.78
:properties:
:custom_id: exercise-4.78
:end:

Redesign the query language as a nondeterministic program to be implemented using the evaluator of section [[#section-4.3][4.3]], rather than as a stream process.  In this approach, each query will produce a single answer (rather than the stream of all answers) and the user can type ~try-again~ to see more answers.  You should find that much of the mechanism we built in this section is subsumed by nondeterministic search and backtracking.  You will probably also find, however, that your new query language has subtle differences in behavior from the one implemented here.  Can you find examples that illustrate this difference?

**** Exercise 4.79
:properties:
:custom_id: exercise-4.79
:end:

When we implemented the Lisp evaluator in section [[#section-4.1][4.1]], we saw how to use local environments to avoid name conflicts between the parameters of procedures.  For example, in evaluating

#+begin_src scheme
(define (square x)
  (* x x))

(define (sum-of-squares x y)
  (+ (square x) (square y)))

(sum-of-squares 3 4)
#+end_src

there is no confusion between the ~x~ in ~square~ and the ~x~ in ~sum-of-squares~, because we evaluate the body of each procedure in an environment that is specially constructed to contain bindings for the local variables.  In the query system, we used a different strategy to avoid name conflicts in applying rules.  Each time we apply a rule we rename the variables with new names that are guaranteed to be unique.  The analogous strategy for the Lisp evaluator would be to do away with local environments and simply rename the variables in the body of a procedure each time we apply the procedure.

Implement for the query language a rule-application method that uses environments rather than renaming.  See if you can build on your environment structure to create constructs in the query language for dealing with large systems, such as the rule analog of block-structured procedures.  Can you relate any of this to the problem of making deductions in a context (e.g., "If I supposed that P were true, then I would be able to deduce A and B.") as a method of problem solving?  (This problem is open-ended.  A good answer is probably worth a Ph.D.)
