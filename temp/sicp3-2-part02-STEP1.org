*** 3.2.2 Aplicación de procedimientos simples
:properties:
:custom_id: section-3.2.2
:end:

Cuando introdujimos el modelo de sustitución en la sección [[#section-1.1.5][1.1.5]] mostramos cómo la combinación ~(f 5)~ se evalúa a 136, dadas las siguientes definiciones de procedimiento:

#+begin_src scheme
(define (square x)
  (* x x))

(define (sum-of-squares x y)
  (+ (square x) (square y)))

(define (f a)
  (sum-of-squares (+ a 1) (* a 2)))
#+end_src

Podemos analizar el mismo ejemplo usando el modelo de entornos. [[figure-3.4][Figura 3.4]] muestra los tres objetos de procedimiento creados al evaluar las definiciones de ~f~, ~square~ y ~sum-of-squares~ en el entorno global. Cada objeto de procedimiento consiste en algún código, junto con un puntero al entorno global.

<<figure-3.4>> Objetos de procedimiento en el marco global.

#+begin_example
           +--------------------------------------------+
           | sum-of-squares:                            |
 global -->| square:                                    |
 env       | f: --+                                     |
           +------|--------------+--------------+-------+
                  |     ^        |     ^        |     ^
                  |     |        |     |        |     |
                  V     |        V     |        V     |
              .---.---. |    .---.---. |    .---.---. |
              | O | O-+-+    | O | O-+-+    | O | O-+-+
              `-|-^---'      `-|-^---'      `-|-^---'
                |              |              |
                V              V              V
    parameters: a          parameters: x  parameters: x, y
    body: (sum-of-squares  body: (* x x)  body: (+ (square x)
            (+ a 1)                                (square y))
            (* a 2))
#+end_example

En [[figure-3.5][Figura 3.5]] vemos la estructura de entorno creada al evaluar la expresión ~(f 5)~. La llamada a ~f~ crea un nuevo entorno E1 que comienza con un marco en el que ~a~, el parámetro formal de ~f~, está enlazado al argumento 5. En E1, evaluamos el cuerpo de ~f~:

#+begin_src scheme
(sum-of-squares (+ a 1) (* a 2))
#+end_src

<<figure-3.5>> Entornos creados al evaluar ~(f 5)~ usando los procedimientos en [[figure-3.4][Figura 3.4]].

#+begin_example
           +-----------------------------------------------------+
 global -->|                                                     |
 env       +-----------------------------------------------------+
             ^              ^                ^               ^
 (f 5)       |              |                |               |
         +------+       +-------+        +------+        +-------+
   E1 -->| a: 5 |  E2 ->| x: 6  |  E3 -->| x: 6 |  E4 -->| x: 10 |
         |      |       | y: 10 |        |      |        |       |
         +------+       +-------+        +------+        +-------+
    (sum-of-squares   (+ (square x)       (* x x)         (* x x)
      (+ a 1)            (square u))
      (+ a 2))
#+end_example

Para evaluar esta combinación, primero evaluamos las subexpresiones. La primera subexpresión, ~sum-of-squares~, tiene un valor que es un objeto de procedimiento. (Observa cómo se encuentra este valor: Primero buscamos en el primer marco de E1, que no contiene ningún enlace para ~sum-of-squares~. Luego procedemos al entorno circundante, es decir, el entorno global, y encontramos el enlace mostrado en [[figure-3.4][Figura 3.4]].) Las otras dos subexpresiones se evalúan aplicando las operaciones primitivas ~+~ y ~*~ para evaluar las dos combinaciones ~(+ a 1)~ y ~(* a 2)~ para obtener 6 y 10, respectivamente.

Ahora aplicamos el objeto de procedimiento ~sum-of-squares~ a los argumentos 6 y 10. Esto resulta en un nuevo entorno E2 en el que los parámetros formales ~x~ y ~y~ están enlazados a los argumentos. Dentro de E2 evaluamos la combinación ~(+ (square x) (square y))~. Esto nos lleva a evaluar ~(square x)~, donde ~square~ se encuentra en el marco global y ~x~ es 6. Una vez más, establecemos un nuevo entorno, E3, en el que ~x~ está enlazado a 6, y dentro de este evaluamos el cuerpo de ~square~, que es ~(* x x)~. También como parte de aplicar ~sum-of-squares~, debemos evaluar la subexpresión ~(square y)~, donde ~y~ es 10. Esta segunda llamada a ~square~ crea otro entorno, E4, en el que ~x~, el parámetro formal de ~square~, está enlazado a 10. Y dentro de E4 debemos evaluar ~(* x x)~.

El punto importante a observar es que cada llamada a ~square~ crea un nuevo entorno que contiene un enlace para ~x~. Podemos ver aquí cómo los diferentes marcos sirven para mantener separadas las diferentes variables locales todas llamadas ~x~. Observa que cada marco creado por ~square~ apunta al entorno global, ya que este es el entorno indicado por el objeto de procedimiento ~square~.

Después de que se evalúan las subexpresiones, se devuelven los resultados. Los valores generados por las dos llamadas a ~square~ son sumados por ~sum-of-squares~, y este resultado es devuelto por ~f~. Dado que nuestro enfoque aquí está en las estructuras de entorno, no nos detendremos en cómo estos valores devueltos se pasan de llamada a llamada; sin embargo, este es también un aspecto importante del proceso de evaluación, y volveremos a él en detalle en [[#section-5][Capítulo 5]].

**** Ejercicio 3.9
:properties:
:custom_id: exercise-3.9
:end:

En la sección [[#section-1.2.1][1.2.1]] utilizamos el modelo de sustitución para analizar dos procedimientos para calcular factoriales, una versión recursiva

#+begin_src scheme
(define (factorial n)
  (if (= n 1)
      1
      (* n (factorial (- n 1)))))
#+end_src

y una versión iterativa

#+begin_src scheme
(define (factorial n)
  (fact-iter 1 1 n))

(define (fact-iter product counter max-count)
  (if (> counter max-count)
      product
      (fact-iter (* counter product)
                 (+ counter 1)
                 max-count)))
#+end_src

Muestra las estructuras de entorno creadas al evaluar ~(factorial 6)~ usando cada versión del procedimiento ~factorial~.[fn:142]

