**** 4.4.4.8 Marcos y Ligaduras
:properties:
:custom_id: section-4.4.4.8
:end:

Los marcos se representan como listas de ligaduras, que son pares variable-valor:

#+begin_src scheme
(define (make-binding variable value)
  (cons variable value))

(define (binding-variable binding)
  (car binding))

(define (binding-value binding)
  (cdr binding))

(define (binding-in-frame variable frame)
  (assoc variable frame))

(define (extend variable value frame)
  (cons (make-binding variable value) frame))
#+end_src

**** Ejercicio 4.71
:properties:
:custom_id: exercise-4.71
:end:

Louis Reasoner se pregunta por qué los procedimientos ~simple-query~ y ~disjoin~ (sección [[#section-4.4.4.2][4.4.4.2]]) se implementan usando operaciones ~delay~ explícitas, en lugar de estar definidos de la siguiente manera:

#+begin_src scheme
(define (simple-query query-pattern frame-stream)
  (stream-flatmap
   (lambda (frame)
     (stream-append (find-assertions query-pattern frame)
                    (apply-rules query-pattern frame)))
   frame-stream))

(define (disjoin disjuncts frame-stream)
  (if (empty-disjunction? disjuncts)
      the-empty-stream
      (interleave
       (qeval (first-disjunct disjuncts) frame-stream)
       (disjoin (rest-disjuncts disjuncts) frame-stream))))
#+end_src

¿Puedes dar ejemplos de consultas donde estas definiciones más simples llevarían a un comportamiento indeseable?

**** Ejercicio 4.72
:properties:
:custom_id: exercise-4.72
:end:

¿Por qué ~disjoin~ y ~stream-flatmap~ intercalan los flujos en lugar de simplemente añadirlos? Da ejemplos que ilustren por qué el intercalado funciona mejor. (Pista: ¿Por qué usamos ~interleave~ en la sección [[#section-3.5.3][3.5.3]]?)

**** Ejercicio 4.73
:properties:
:custom_id: exercise-4.73
:end:

¿Por qué ~flatten-stream~ usa ~delay~ explícitamente? ¿Qué estaría mal al definirlo de la siguiente manera:

#+begin_src scheme
(define (flatten-stream stream)
  (if (stream-null? stream)
      the-empty-stream
      (interleave
       (stream-car stream)
       (flatten-stream (stream-cdr stream)))))
#+end_src

